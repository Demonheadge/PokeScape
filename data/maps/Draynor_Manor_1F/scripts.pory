mapscripts Draynor_Manor_1F_MapScripts {

    MAP_SCRIPT_ON_LOAD {
        call(DraynorManor_Lever_STATE)
        call(STORYMODE_STATE_DRAYNORMANOR_1F)
        }
    MAP_SCRIPT_ON_TRANSITION {
        setvar(VAR_EVOLUTION_MAP_STATE, 1) //MAP LOCATIONS: 0-NOTHING, 1-LUMBRIDGE, 16-VARROCK, 3-FALADOR, 4-MEMBERS, 5-OCEAN, 6-WILDERNESS, 7-MORYTANIA, 8-CAVES, 9-ABYSS, 10-OFFWORLD, 11-DESERT,
    }

    MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_POKESCAPE_WIZARDS_TOWER_STATE, 7: HAM_EVENT_DRAYNORMANOR_1F
        VAR_TEMP_B, 1: HAM_EVENT_DRAYNORMANOR_1F
    ]

}

script STORYMODE_STATE_DRAYNORMANOR_1F {
    checkitem (ITEM_FISH_FOOD,1)
	if (var(VAR_RESULT)){
        setmetatile(9, 10, METATILE_PokeScapeIndoors_CUPBOARD_SPARKLE, 1)
        setmetatile(12, 2, METATILE_PokeScapeIndoors_CUPBOARD_SPARKLE, 1)
        setmetatile(13, 2, METATILE_PokeScapeIndoors_CUPBOARD_SPARKLE, 1)
        special(DrawWholeMapView)
    }
    else {
        setmetatile(9, 10, METATILE_PokeScapeIndoors_CUPBOARD_ITEMS, 1)
        setmetatile(12, 2, METATILE_PokeScapeIndoors_CUPBOARD_ITEMS, 1)
        setmetatile(13, 2, METATILE_PokeScapeIndoors_CUPBOARD_ITEMS, 1)
        special(DrawWholeMapView)
    }

    setflag(FLAG_TEMP_1E) 
    removeobject(18) //HAM

    switch (var(VAR_POKESCAPE_STORYMODE_PROGRESS)){ 
            case 20:
            case 25: 
                clearflag(FLAG_TEMP_1E)
                addobject(18) //HAM
                setobjectxyperm(18, 18, 20)
            case 26: 
                setflag(FLAG_TEMP_1E) 
                removeobject(18) //HAM
            case 27: 
                setflag(FLAG_TEMP_1E) 
                removeobject(18) //HAM
            case 28: 
                clearflag(FLAG_TEMP_1E)
                addobject(18) //HAM
                setobjectxyperm(18, 35, 18)
    }

    checkfollower
    if (var(VAR_RESULT)) {
        setvar(VAR_TEMP_A, 1) //Sets triggers for leaving.
    }
    else {
        removeobject(17) //ARIANE
        setflag(FLAG_TEMP_1F)
    }
    switch (var(VAR_POKESCAPE_WIZARDS_TOWER_STATE)){ 
            case 8: 
                checkfollower
                if (!var(VAR_RESULT)) {
                    addobject(17) //ARIANE
                    clearflag(FLAG_TEMP_1F)
                    setvar(VAR_TEMP_B, 1)
                }
    }
    return
}

script DraynorManor_HAM_GRUNT {
    lock
    switch (var(VAR_POKESCAPE_STORYMODE_PROGRESS)){ 
            case 25: 
                delay(15)
                playse(SE_PIN)
                applymovement(OBJ_EVENT_ID_PLAYER, Movement_DraynorManor_Chase_1)
                applymovement(OBJ_EVENT_ID_FOLLOWER, Movement_DraynorManor_Chase_2)
			    waitmovement(0)
                namebox("Ariane")
                msgbox(format("Oooh!\pIs that an item!?"))
                hidenamebox
                closemessage
                special(SpawnCameraObject)
                applymovement(OBJ_EVENT_ID_CAMERA, Movement_DraynorManor_Camera_1)
                applymovement(OBJ_EVENT_ID_PLAYER, Movement_InteractFACEDOWN)
                applymovement(18, Movement_DraynorManor_Chase_4)
                applymovement(OBJ_EVENT_ID_FOLLOWER, Movement_DraynorManor_Chase_3)
			    waitmovement(OBJ_EVENT_ID_CAMERA)
                namebox("Ariane")
                msgbox(format("I wonder what it is…"))
                hidenamebox
                closemessage
                playse(SE_BIKE_HOP)
                waitse
                applymovement(OBJ_EVENT_ID_FOLLOWER, Movement_DraynorManor_Chase_5)
                namebox("Ariane")
                msgbox(format("…Huh?"))
                hidenamebox
                closemessage
			    waitmovement(0)
                
                applymovement(18, Movement_DraynorManor_Chase_6)
                applymovement(OBJ_EVENT_ID_FOLLOWER, Movement_DraynorManor_Chase_7)
			    waitmovement(0)
                delay(60)
                playse(SE_PIN)
                applymovement(18, Movement_DraynorManor_Chase_8)
                applymovement(OBJ_EVENT_ID_FOLLOWER, Movement_DraynorManor_Chase_8)
			    waitmovement(0)
                namebox("Ariane")
                msgbox(format("EEEEEEEEEEK!!"))
                hidenamebox
                closemessage
                applymovement(OBJ_EVENT_ID_FOLLOWER, Movement_DraynorManor_Chase_10)
                applymovement(18, Movement_DraynorManor_Chase_9)
                applymovement(OBJ_EVENT_ID_CAMERA, Movement_DraynorManor_Camera_2)
			    waitmovement(0)

                setobjectxy(18, 22, 11)
                setobjectxyperm(18, 22, 11)
                applymovement(OBJ_EVENT_ID_FOLLOWER, Movement_DraynorManor_Chase_11)
			    waitmovement(0)
                applymovement(18, Movement_DraynorManor_Chase_12)
                
                namebox("Ariane")
                msgbox(format("EEEK!\nThat was him!\pQuick lets give chase!"))
                hidenamebox
                closemessage
                waitmovement(0)
                special(RemoveCameraObject)
                setflag(FLAG_TEMP_1E) 
                removeobject(18) //HAM
                setvar(VAR_POKESCAPE_STORYMODE_PROGRESS, 26)
            case 26: 
                applymovement(8, Movement_InteractFACEUP)
			    waitmovement(0)
                delay(30)
                applymovement(8, Movement_InteractFACEDOWN)
			    waitmovement(0)
                namebox("H.A.M. Grunt")
                msgbox(format("…{PAUSE 15}…{PAUSE 15}…Did that chair{PAUSE 15}…\p{PAUSE 15}…{PAUSE 15}…{PAUSE 15}just move{PAUSE 15}…?"))
                hidenamebox
                closemessage
                playse(SE_PIN)
                applymovement(11, MOVEMENT_EXCLAMATIONMARK_FACEPLAYER)
			    waitmovement(0)
                namebox("H.A.M. Grunt")
                msgbox(format("Aaak!"))
                hidenamebox
                closemessage
                applymovement(11, Movement_DraynorManor_Chase_13)
			    waitmovement(0)
                facefollower
                namebox("Ariane")
                msgbox(format("I think we are wearing him out, let's get him!"))
                hidenamebox
                closemessage
                setobjectxyperm(11, 14, 19)
                setvar(VAR_POKESCAPE_STORYMODE_PROGRESS, 27)
            case 27: 
                //battle
                trainerbattle_no_intro(TRAINER_DRAYNORMANOR_HAM, "No way…\pTwo against one is just not fair…")
                setflag(FLAG_TEMP_1E) 
                removeobject(11) //HAM upstairs
                setvar(VAR_POKESCAPE_STORYMODE_PROGRESS, 28)
            case 28: 
                setflag(FLAG_TEMP_1E) 
                removeobject(18) //HAM
                setvar(VAR_POKESCAPE_STORYMODE_PROGRESS, 29)
    }
    release
    end
}

movement Movement_DraynorManor_Chase_1 {
    face_right
    lock_facing_direction
    walk_left
    unlock_facing_direction
    step_end
}
movement Movement_DraynorManor_Chase_2 {
    emote_exclamation_mark
    walk_down
    step_end
}
movement Movement_DraynorManor_Chase_3 {
    walk_down * 3
    step_end
}
movement Movement_DraynorManor_Chase_4 {
    face_right
    step_end
}
movement Movement_DraynorManor_Chase_5 {
    emote_question_mark
    step_end
}
movement Movement_DraynorManor_Chase_6 {
    face_left
    step_end
}
movement Movement_DraynorManor_Chase_7 {
    face_right
    step_end
}
movement Movement_DraynorManor_Chase_8 {
    lock_facing_direction
    emote_exclamation_mark
    jump_in_place_down
    unlock_facing_direction
    step_end
}
movement Movement_DraynorManor_Chase_9 {
    walk_fast_left
    walk_fast_up * 10
    step_end
}
movement Movement_DraynorManor_Chase_10 {
    walk_fast_left * 2
    walk_fast_up
    walk_fast_left
    face_right
    step_end
}
movement Movement_DraynorManor_Chase_11 {
    walk_fast_right * 2
    walk_up
    face_player
    step_end
}
movement Movement_DraynorManor_Chase_12 {
    walk_fast_down * 7
    walk_fast_right * 5
    step_end
}
movement Movement_DraynorManor_Camera_1 {
    walk_slow_down * 3
    step_end
}
movement Movement_DraynorManor_Camera_2 {
    walk_slow_up * 3
    step_end
}
movement Movement_DraynorManor_Chase_13 {
    walk_fast_up * 2
    walk_fast_left * 8
    step_end
}

movement Movement_DraynorManor_Chase_14 {
    face_up
    delay_16 * 3
    face_right
    step_end
}

script DraynorManor_Lever_STATE{
        switch (var(VAR_POKESCAPE_DRAYNORMANOR_LEVER_STATE)){ 
            case 0: 
                setmetatile(6, 15, METATILE_DraynorManor_BASEMENT_LEVER_UP_1, 1)
                setmetatile(6, 16, METATILE_DraynorManor_BASEMENT_LEVER_UP_2, 1)
                setmetatile(9, 16, METATILE_PokeScapeIndoors_BOOKCASE_SPARKLE, 1)
                //LEFT
                setmetatile(7, 17, 602, 1)
                setmetatile(7, 18, 602, 1)
                setmetatile(7, 19, 602, 1)
                setmetatile(7, 20, 602, 1)
                setmetatile(7, 21, 602, 1)
                //RIGHT
                setmetatile(8, 17, 604, 1)
                setmetatile(8, 18, 604, 1)
                setmetatile(8, 19, 604, 1)
                setmetatile(8, 20, 604, 1)
                setmetatile(8, 21, 604, 1)
                special(DrawWholeMapView) 
                return
            case 1: 
                setmetatile(9, 16, METATILE_PokeScapeIndoors_BOOKCASE_INTERACT_L, 1)
                setmetatile(6, 15, METATILE_DraynorManor_BASEMENT_LEVER_DOWN_1, 1)
                setmetatile(6, 16, METATILE_DraynorManor_BASEMENT_LEVER_DOWN_2, 1)
                //LEFT
                setmetatile(7, 17, 570, 1)
                setmetatile(7, 18, 578, 1)
                setmetatile(7, 19, 586, 1)
                setmetatile(7, 20, 516, 0)
                setmetatile(7, 21, 594, 1)
                //RIGHT
                setmetatile(8, 17, 572, 1)
                setmetatile(8, 18, 580, 1)
                setmetatile(8, 19, 588, 1)
                setmetatile(8, 20, 516, 0)
                setmetatile(8, 21, 596, 1)
                special(DrawWholeMapView) 
                return
            }
    }




script HAM_EVENT_DRAYNORMANOR_1F {
    lockall
    switch (var(VAR_POKESCAPE_WIZARDS_TOWER_STATE)){ 
            case 7: 

                getplayerxy(VAR_TEMP_1, VAR_TEMP_2)
                if (var(VAR_TEMP_1) == 24){
                    setobjectxyperm(17, 24, 22)
                } 
                elif (var(VAR_TEMP_1) == 25){
                    setobjectxyperm(17, 25, 22)
                }
                setobjectmovementtype(17, MOVEMENT_TYPE_FACE_UP)
                
            //applies movement to player walking 2 steps
                applymovement(OBJ_EVENT_ID_PLAYER, Movement_DraynorManor_Entry_1)
			    waitmovement(0)
                delay(32)
                playse(SE_DOOR)
                addobject(17) //ARIANE
                clearflag(FLAG_TEMP_1F)
                delay(32)
            //applymovement comes in the door slowly walks behind the player.
                applymovement(17, Movement_DraynorManor_Entry_2)
			    waitmovement(0)
                applymovement(OBJ_EVENT_ID_PLAYER, MOVEMENT_FACE_DOWN)
			    waitmovement(0)

                namebox("Ariane")
                msgbox(format("…{PAUSE 32}…{PAUSE 32}…\nHe… Hey you…\pCan… can we go together?"))
                closemessage
                hidenamebox
            //apply movement walks infront of the player.
                applymovement(17, Movement_DraynorManor_Entry_4)
			    waitmovement(0)
                applymovement(OBJ_EVENT_ID_PLAYER, MOVEMENT_FACE_LEFT)
			    waitmovement(0)
                namebox("Ariane")
                msgbox(format("Pssh!\pI'm not scared, what do you take me for?"))
                hidenamebox
                closemessage
            //RAT
                applymovement(6, Movement_DraynorManor_Entry_Rat)
                playmoncry(SPECIES_RAT_NORMAL_FORM, 1)
                namebox("Rat")
                msgbox(format("Squeeek!"))
                waitmovement(0)
                removeobject(6)
                setflag(FLAG_TEMP_19)
                waitmoncry
            
                playse(SE_LEDGE)
                applymovement(17, MOVEMENT_EXCLAMATIONMARK_JUMP_UP)
			    waitmovement(0)
                namebox("Ariane")
                msgbox(format("Eeek!"))
            //apply movement ariane runs behind the player agaain
                applymovement(17, Movement_DraynorManor_Entry_5)
			    waitmovement(0)
                applymovement(OBJ_EVENT_ID_PLAYER, MOVEMENT_FACE_DOWN)
			    waitmovement(0)
                msgbox(format("Okay, maybe I am a little scared…\pYou lead the way."))
                closemessage
                hidenamebox
                setvar(VAR_POKESCAPE_WIZARDS_TOWER_STATE, 8)
                setvar(VAR_POKESCAPE_STORYMODE_PROGRESS, 25)

            //ARIANE JOINS THE PARTY and Follows!
                playfanfare(MUS_OBTAIN_SYMBOL)
                msgbox(format("ARIANE JOINS THE PARTY!"))
                waitfanfare
                closemessage
                hidenamebox
                checkfollower
                if (var(VAR_RESULT)) {
                    call(FOLLOWER_REMOVE)
                }
                setflag(FLAG_TOGGLE_FORCED_WILD_DOUBLE_BATTLES)
                setflag(FLAG_PARTNER_BATTLE)
                setvar (VAR_PARTNER_TRAINER, TRAINER_ARIANE_1)
                setvar (VAR_PARTNER_SPRITE, TRAINER_BACK_PIC_ARIANE)
                loadword(0, FOLLOWING_Ariane)
                setfollower(17, 0x80)
                setvar(VAR_TEMP_A, 1) //Sets triggers for leaving.
            case 8:
                applymovement(OBJ_EVENT_ID_PLAYER, Movement_DraynorManor_Entry_3)
			    waitmovement(0)

                getplayerxy(VAR_TEMP_1, VAR_TEMP_2)
                if (var(VAR_TEMP_1) == 24){
                    applymovement(17, MOVEMENT_WALKLEFT)
			        waitmovement(0)
                    applymovement(17, MOVEMENT_WALKLEFT)
			        waitmovement(0)
                } 
                elif (var(VAR_TEMP_1) == 25){
                    applymovement(17, MOVEMENT_WALKLEFT)
			        waitmovement(0)
                }

                


                applymovement(OBJ_EVENT_ID_PLAYER, MOVEMENT_FACE_RIGHT)
			    waitmovement(0)
                namebox("Ariane")
                msgbox(format("…{PAUSE 32}…{PAUSE 32}…\nHe… Hey you came back…\pCan… can we please go together?\pI'm scared."))
                hidenamebox
                //ARIANE JOINS THE PARTY and Follows!
                playfanfare(MUS_OBTAIN_SYMBOL)
                msgbox(format("ARIANE JOINS THE PARTY!"))
                waitfanfare
                closemessage
                hidenamebox
                checkfollower
                if (var(VAR_RESULT)) {
                    call(FOLLOWER_REMOVE)
                }
                setflag(FLAG_TOGGLE_FORCED_WILD_DOUBLE_BATTLES)
                setflag(FLAG_PARTNER_BATTLE)
                setvar (VAR_PARTNER_TRAINER, TRAINER_ARIANE_1)
                setvar (VAR_PARTNER_SPRITE, TRAINER_BACK_PIC_ARIANE)
                loadword(0, FOLLOWING_Ariane)
                setfollower(17, 0x80)
                setvar(VAR_TEMP_A, 1) //Sets triggers for leaving.
                setvar(VAR_TEMP_B, 0) //Ends this mapscript
                    
    }
    release
    return
}


                
                
                

movement Movement_DraynorManor_Entry_1 {
    walk_slow_up * 2
}
movement Movement_DraynorManor_Entry_2 {
    delay_16
    walk_slow_up
}
movement Movement_DraynorManor_Entry_3 {
    walk_slow_up
}

movement Movement_DraynorManor_Entry_4 {
    walk_left
    walk_up
    face_player
}
movement Movement_DraynorManor_Entry_5 {
    walk_faster_down
    walk_faster_right
    face_player
}

movement MOVEMENT_EXCLAMATIONMARK_JUMP_LEFT {
    emote_exclamation_mark
    jump_in_place_left
}
movement MOVEMENT_EXCLAMATIONMARK_JUMP_UP {
    emote_exclamation_mark
    jump_in_place_up
}

movement Movement_DraynorManor_Entry_Rat {
    walk_faster_left * 6
    walk_faster_up * 9
}

script FOLLOWING_Ariane {

    namebox("Ariane")
    facefollower 

    specialvar (VAR_RESULT, GetCurrentMap)
    if (var(VAR_RESULT) == MAP_DRAYNOR_MANOR_B1F) {
        random(3)
        switch (var(VAR_RESULT)) {
        case 0:
            msgbox(format("As mysterious as this is, we are looking for the pink man.\pNot some lever side quest."))
        case 1:
            msgbox(format("We should probably go back upstairs…"))
        case 2:
            msgbox(format("Eww… it stinks of old oil cans down here."))
        }
        hidenamebox
        closemessage
        release
        end
    }

    random(9)
    switch (var(VAR_RESULT)) {
        case 0:
            msgbox(format("This place is 2spooky4me!"))
        case 1:
            msgbox(format("Where do you think the pink guy went?"))
        case 2:
            msgbox(format("Hey my name is Ariane, what is yours?\p…{PAUSE 32}{PLAYER}?\nThat's a cute name."))
        case 3:
            msgbox(format("You know what?\pWith you here this place isn't as scary."))
            playse(SE_LEDGE)
            applymovement(17, MOVEMENT_EXCLAMATIONMARK_JUMP_UP)
            waitmovement(0)
            msgbox(format("EEK!\nWhat was that!?"))
        case 4:
            msgbox(format("What's with all of these weird-looking chickens around the place?"))
        case 5:
            msgbox(format("We haven't checked the basement yet, we should look there."))
        case 6:
            msgbox(format("Did…{PAUSE 32} that armour just move?!"))
        case 7:
            msgbox(format("How can anyone even live here…"))
        case 8:
            msgbox(format("Eeek!\pI think I just saw a ghost!"))
    }
    
    hidenamebox
    closemessage
    release
    end
}

script TRIGGER_ARIANE_DRAYNORMANOR_1F_1 {
    facefollower 
    namebox("Ariane")
    msgbox(format("What are you doing?\pThat guy is still in here somewhere.\pWait you aren't going to leave me here are you?"), MSGBOX_YESNO)
    if (var(VAR_RESULT))
    { 
        msgbox(format("Eeek!!"))
        closemessage
        hidenamebox
        setvar(VAR_TEMP_A, 0) //Sets triggers for leaving.
        getplayerxy(VAR_TEMP_1, VAR_TEMP_2)
        if (var(VAR_TEMP_1) == 24){
            warp(MAP_DRAYNOR_MANOR, 0, 0, 0)
        } 
        elif (var(VAR_TEMP_1) == 25){
            warp(MAP_DRAYNOR_MANOR, 1, 0, 0)
        }
        
        call(FOLLOWER_REMOVE)
    }
    else {
        closemessage
        hidenamebox
        applymovement(OBJ_EVENT_ID_PLAYER, MOVEMENT_WALK_UP)
        applymovement(OBJ_EVENT_ID_FOLLOWER, MOVEMENT_WALK_UP)
        waitmovement(0)
    }
}

script TRIGGER_ARIANE_DRAYNORMANOR_1F_2 {
    checkfollower
    if (var(VAR_RESULT)) {
        facefollower 
        
        playse(SE_LEDGE)
        applymovement(OBJ_EVENT_ID_FOLLOWER, MOVEMENT_EXCLAMATIONMARK_JUMP_LEFT)
        waitmovement(0)
        namebox("Ariane")
        msgbox(format("Eeek!!\pDid that chair just move?!"))
        closemessage
        hidenamebox
        setvar(VAR_TEMP_C, 1)
    }
    release
    end
}
script TRIGGER_ARIANE_DRAYNORMANOR_1F_3 {
    checkfollower
    if (var(VAR_RESULT)) {
        delay(32)
        applymovement(OBJ_EVENT_ID_FOLLOWER, MOVEMENT_FACE_LEFT)
        waitmovement(0)
        delay(32)
        facefollower
        namebox("Ariane")
        msgbox(format("I hear noises coming from the basement, lets go look!"))
        closemessage
        hidenamebox
        setvar(VAR_TEMP_E, 1)
    }
    release
    end
}
script TRIGGER_ARIANE_DRAYNORMANOR_1F_4 {
    checkfollower
    if (var(VAR_RESULT)) {
        facefollower
        namebox("Ariane")
        msgbox(format("I doubt he would be outside.\pLets go look elsewhere."))
        closemessage
        hidenamebox
        applymovement(OBJ_EVENT_ID_PLAYER, MOVEMENT_WALK_DOWN)
        applymovement(OBJ_EVENT_ID_FOLLOWER, MOVEMENT_WALK_DOWN)
        waitmovement(0)
    }
    release
    end
}
script TRIGGER_ARIANE_DRAYNORMANOR_1F_5 {
    checkfollower
    if (var(VAR_RESULT)) {
        facefollower
        namebox("Ariane")
        msgbox(format("As mysterious as this is, we are looking for the pink man.\pNot some lever side quest."))
        closemessage
        hidenamebox
        applymovement(OBJ_EVENT_ID_PLAYER, MOVEMENT_WALK_RIGHT)
        applymovement(OBJ_EVENT_ID_FOLLOWER, MOVEMENT_WALK_RIGHT)
        waitmovement(0)
        //setvar(VAR_TEMP_9, 1)
    }
    release
    end
}
script TRIGGER_ARIANE_DRAYNORMANOR_1F_6 {
    checkfollower
    if (var(VAR_RESULT)) {
        facefollower
        namebox("Ariane")
        switch (var(VAR_POKESCAPE_STORYMODE_PROGRESS)){ 
            case 25: 
            case 26: 
            case 27: 
            case 28: 
                msgbox(format("Eek! I'm getting some sinister vibes from down there, maybe we could look around the rest of the manor before we look down there."))
            closemessage
            hidenamebox
            release
            end
        }
        msgbox(format("Uh…{PAUSE 32} I know he ran down there, but that basement is giving of some serious sinister vibes.\pI think I'll stay up here while you go ahead."))
        closemessage
        hidenamebox
        applymovement(OBJ_EVENT_ID_FOLLOWER, MOVEMENT_WALK_RIGHT)
        waitmovement(0)
        facefollower
        applymovement(OBJ_EVENT_ID_PLAYER, MOVEMENT_FACE_LEFT)
        delay(32)
        setvar(VAR_TEMP_9, 1)
        warp(MAP_DRAYNOR_MANOR_CRYPT, 0, 0, 0)
        call(FOLLOWER_REMOVE)
    }
    release
    end
}
script TRIGGER_ARIANE_DRAYNORMANOR_B1F_1 {
    checkfollower
    if (var(VAR_RESULT)) {
        facefollower
        namebox("Ariane")
        msgbox(format("I feel like the pink man isn't down here.\pWe should probably turn back."))
        closemessage
        hidenamebox
        //applymovement(OBJ_EVENT_ID_PLAYER, MOVEMENT_WALK_RIGHT)
        //applymovement(OBJ_EVENT_ID_FOLLOWER, MOVEMENT_WALK_RIGHT)
        //waitmovement(0)
        release
    }
    setvar(VAR_TEMP_9, 1)
    end
}









script NPC_DraynorManor_Ava{
    lock
	faceplayer
	namebox("Ava")



    if (var(VAR_POKESCAPE_ERNEST_QUEST_STATE) == 1) {
        checkitem (ITEM_OIL_CAN,1)
        if (!var(VAR_RESULT)){
            msgbox(format("Oil Can?\pLast time I used it was to oil up my bed to stop it from creaking.\pWhere did I leave it?\pUh…{PAUSE 32}… I believe I left it in the basement over there.\pSo if you want it you will have to go get it."))
            hidenamebox
            closemessage
            release
            end
        }
        else {
            msgbox(format("Hey you found the Oil Can!\pMy bed is starting to creek again, can I use it before you go haha."))
            closemessage
            hidenamebox
            closemessage
            release
            end
        }
    }


    random(5)
        switch (var(VAR_RESULT)) { 
        case 0: 
            msgbox(format("I think the walls are watching me…"))
            hidenamebox
            closemessage
            release
            end

        case 1: 
            msgbox(format("Who thought it was a good idea to put piranhas in the fountain…\pIt is no where near big enough!"))
            hidenamebox
            closemessage
            release
            end

        case 2: 
            msgbox(format("If you see any animated chairs…\pI assure you, it was not my fault."))
            hidenamebox
            closemessage
            release
            end

        case 3: 
            msgbox(format("I'm overjoyed with my new mattress!\pIt almost feels like I'm being consumed by a fluffy…{PAUSE 15}{PAUSE 15} undead…{PAUSE 15}{PAUSE 15} zombie…{PAUSE 15} chicken…?"))
            hidenamebox
            closemessage
            release
            end
            
        case 4: 
            msgbox(format("If only there were more trainers gullible enough to travel across the land searching far and wide for my questionable research."))
            hidenamebox
            closemessage
            release
            end

        case 5: 
            msgbox(format("EEK!\nA HUMAN…!\p…Sorry…{PAUSE 15} I have spent way too much time here, I'm not used to seeing corporeal beings."))
            hidenamebox
            closemessage
            release
            end

        }
    
}

script NPC_DraynorManor_Quester{
    lock
	faceplayer
	namebox("Quester")
    if (var(VAR_POKESCAPE_DRAYNORMANOR_LEVER_STATE) == 1) 
    {
        msgbox(format("Did you find the hidden switch?\pWhere was it?!"))
    }
    else
    {
        msgbox(format("I heard there was a hidden switch somewhere in this room but I am yet to find it."))
    }
    //hidenamebox
	closemessage
	release
	end
}

script NPC_DraynorManor_Witch{
    lock
	faceplayer
	namebox("Witch")
    msgbox(format("I have so much poison in my kitchen cupboards!\pI don't think a blueberry special will save you from how toxic that stuff is!"))
    hidenamebox
	closemessage
	release
	end
}

script NPC_DraynorManor_Chair{
    lock
    random(15)
    switch (var(VAR_RESULT)) { 
        case 0: 
        case 1: 
        case 2: 
        case 3: 
        case 4: 
        case 5: 
        case 6: 
            goto(ENCOUNTER_CHAIR)
    }
    msgbox(format("It's just a chair."))
    hidenamebox
	closemessage
	release
	end
}

script NPC_DraynorManor_Chair1{
    lock
    msgbox(format("It's just a chair."))
    hidenamebox
	closemessage
	release
	end
}


script NPC_PIANO{
    lock
    msgbox(format("Play the Piano?"), MSGBOX_YESNO)
    if (var(VAR_RESULT))
    { 
        specialvar (VAR_RESULT, GetCurrentMap)
        if (var(VAR_RESULT) == MAP_DRAYNOR_MANOR_2F) {
            goto(NPC_SKELETON_PIANO)
        }
        closemessage
        delay(15)
        random(8)
        switch (var(VAR_RESULT)) { 
            case 0: 
                playse(SE_NOTE_A)
            case 1: 
                playse(SE_NOTE_B)
            case 2: 
                playse(SE_NOTE_C)
            case 3: 
                playse(SE_NOTE_D)
            case 4: 
                playse(SE_NOTE_E)
            case 5: 
                playse(SE_NOTE_F)
            case 6: 
                playse(SE_NOTE_G)
            case 7: 
                playse(SE_NOTE_C_HIGH)
                waitse
                goto(ENCOUNTER_GHOST)
        }
        waitse
    }
    closemessage
	release
	end
}

script NPC_DraynorManor_Skeleton_1{
    lock
	faceplayer
    namebox("Skeleton")
    msgbox(format("I would really like to play the piano.\pBut I have nobody to play with."))
    hidenamebox
	closemessage
	release
	end
}

script NPC_SKELETON_PIANO {
    closemessage
    playse(SE_NOTE_A)
    waitse
    playse(SE_NOTE_B)
    waitse
    playse(SE_NOTE_B)
    waitse
    playse(SE_NOTE_C)
    waitse
    playse(SE_NOTE_A)
    waitse
    playse(SE_NOTE_D)
    waitse
    playse(SE_NOTE_A)
    waitse
    playse(SE_NOTE_G)
    waitse
    applymovement(6, MOVEMENT_SHOCKED_FACEPLAYER)
    waitmovement(0)
    namebox("Skeleton")
    msgbox(format("Yo that's pretty groovy.\pSay you know that piano downstairs?\pThere's a broken note on it that angers many of the monsters in the manor, so be careful."))
    hidenamebox
	closemessage
    release
    end
}


//TRAINERS-------------------------


script BATTLE_DRAYNORMANOR_Trainer_1
{
    trainerbattle_single(TRAINER_DRAYNOR_MANOR_SKELETON_1, format("The skeleton stares deep into your eyes before it challenges you to a battle."),format("The skeleton stares intently into your eyes but refuses to move."))
    //namebox ("Vertebrae")
    msgbox(format("The skeleton stares intently into your eyes but refuses to move."))
    hidenamebox
    release
    end
}

script BATTLE_DRAYNORMANOR_Trainer_2
{
    trainerbattle_single(TRAINER_DRAYNOR_MANOR_SKELETON_2, format("Do not steal the Rubber Tube!"),format("Oh you want to return it?"))
    //namebox ("McBoneFace")
    msgbox(format("You are going to return the Rubber Tube to Oddenstein?\pSure go for it."))
    hidenamebox
    release
    end
}

script BATTLE_DRAYNORMANOR_Trainer_3
{
    trainerbattle_single(TRAINER_DRAYNOR_MANOR_TRAINER_3, format("Fear me and my monsters!"),format("Ahhh!\pI fear you and your monsters!"))
    namebox ("Dark Wizard")
    msgbox(format("Ahhh!\pI fear you and your monsters!"))
    hidenamebox
    release
    end
}

script BATTLE_DRAYNORMANOR_Trainer_4
{
    trainerbattle_single(TRAINER_DRAYNOR_MANOR_SKELETON_4, format("Boo!"),format("The skeleton is giggling at your previous reaction."))
    //namebox ("Tibia")
    msgbox(format("The skeleton is giggling at your previous reaction."))
    hidenamebox
    release
    end
}

script BATTLE_DRAYNORMANOR_Trainer_5
{
    trainerbattle_single(TRAINER_DRAYNOR_MANOR_SKELETON_5, format("The skeleton strikes a menacing pose."),format("The skeleton stands perfectly still in a menacing pose."))
    //namebox ("Sternum")
    msgbox(format("The skeleton stands perfectly still in a menacing pose."))
    hidenamebox
    release
    end
}

script BATTLE_DRAYNORMANOR_Trainer_6
{
    trainerbattle_single(TRAINER_DRAYNOR_MANOR_SKELETON_6, format("The skeleton looks like it forgot what it was about to say."),format("The skeleton remembered, but the chance to say it is lost."))
    //namebox ("CUPHOLDER")
    msgbox(format("The skeleton remembered, but the chance to say it is lost."))
    hidenamebox
    release
    end
}

script BATTLE_DRAYNORMANOR_Trainer_7
{
    trainerbattle_single(TRAINER_DRAYNOR_MANOR_SKELETON_7, format("The skeleton loses an arm walking towards you."),format("The skeleton is frantically looking for his arm."))
    //namebox ("CUPHOLDER")
    msgbox(format("The skeleton is frantically looking for his arm."))
    hidenamebox
    release
    end
}

script BATTLE_DRAYNORMANOR_Trainer_8
{
    trainerbattle_single(TRAINER_DRAYNOR_MANOR_SKELETON_8, format("…"),format("…………Hi."))
    //namebox ("CUPHOLDER")
    msgbox(format("………………Hi."))
    hidenamebox
    release
    end
}




script SIGN_DRAYNORMANOR_LEVER_1 {
    switch (var(VAR_POKESCAPE_DRAYNORMANOR_LEVER_STATE)) {
                case 0:
			lock
            msgbox(format("You pull a hidden lever!"))
            
            //LEFT
			//setmetatile(7, 17, 570, 1)
            //setmetatile(7, 18, 578, 1)
            //setmetatile(7, 19, 586, 1)
            //setmetatile(7, 20, 516, 0)
            //setmetatile(7, 21, 594, 1)
            //RIGHT
			//setmetatile(8, 17, 572, 1)
            //setmetatile(8, 18, 580, 1)
            ///setmetatile(8, 19, 588, 1)
            //setmetatile(8, 20, 516, 0)
            //setmetatile(8, 21, 596, 1)

			//special(DrawWholeMapView) 
            playse(SE_TRUCK_DOOR)
			setvar(VAR_POKESCAPE_DRAYNORMANOR_LEVER_STATE, 1)
            call(DraynorManor_Lever_STATE)
			release
			end

        case 1:
			lock
            msgbox(format("You pull a hidden lever!"))
            //LEFT
			//setmetatile(7, 17, 602, 1)
            //setmetatile(7, 18, 602, 1)
            //setmetatile(7, 19, 602, 1)
           // setmetatile(7, 20, 602, 1)
            //setmetatile(7, 21, 602, 1)
            //RIGHT
			//setmetatile(8, 17, 604, 1)
            //setmetatile(8, 18, 604, 1)
            //setmetatile(8, 19, 604, 1)
            //setmetatile(8, 20, 604, 1)
            //setmetatile(8, 21, 604, 1)

			//special(DrawWholeMapView) 
            playse(SE_TRUCK_DOOR)
			setvar(VAR_POKESCAPE_DRAYNORMANOR_LEVER_STATE, 0)
            call(DraynorManor_Lever_STATE)
			release
			end
			
    }
}