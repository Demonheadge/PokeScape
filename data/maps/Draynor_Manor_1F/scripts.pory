mapscripts Draynor_Manor_1F_MapScripts {

    MAP_SCRIPT_ON_LOAD {
        call(DraynorManor_Lever_STATE)
        call(STORYMODE_STATE_DRAYNORMANOR_1F)
        }
    MAP_SCRIPT_ON_TRANSITION {
        setvar(VAR_EVOLUTION_MAP_STATE, 1) //MAP LOCATIONS: 0-NOTHING, 1-LUMBRIDGE, 16-VARROCK, 3-FALADOR, 4-MEMBERS, 5-OCEAN, 6-WILDERNESS, 7-MORYTANIA, 8-CAVES, 9-ABYSS, 10-OFFWORLD, 11-DESERT,
    }

    MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_POKESCAPE_WIZARDS_TOWER_STATE, 7: HAM_EVENT_DRAYNORMANOR_1F
        VAR_TEMP_B, 1: HAM_EVENT_DRAYNORMANOR_1F
    ]

}

script STORYMODE_STATE_DRAYNORMANOR_1F {

    

    checkfollower
    if (var(VAR_RESULT)) {
        setvar(VAR_TEMP_A, 1) //Sets triggers for leaving.
    }
    else {
        removeobject(17) //ARIANE
        setflag(FLAG_TEMP_1F)
    }
    switch (var(VAR_POKESCAPE_WIZARDS_TOWER_STATE)){ 
            case 8: 
                checkfollower
                if (!var(VAR_RESULT)) {
                    addobject(17) //ARIANE
                    clearflag(FLAG_TEMP_1F)
                    setvar(VAR_TEMP_B, 1)
                }
    }
    return
}


script HAM_EVENT_DRAYNORMANOR_1F {
    lockall
    switch (var(VAR_POKESCAPE_WIZARDS_TOWER_STATE)){ 
            case 7: 

                getplayerxy(VAR_TEMP_1, VAR_TEMP_2)
                if (var(VAR_TEMP_1) == 24){
                    setobjectxyperm(17, 24, 22)
                } 
                elif (var(VAR_TEMP_1) == 25){
                    setobjectxyperm(17, 25, 22)
                }
                setobjectmovementtype(17, MOVEMENT_TYPE_FACE_UP)
                
            //applies movement to player walking 2 steps
                applymovement(OBJ_EVENT_ID_PLAYER, Movement_DraynorManor_Entry_1)
			    waitmovement(0)
                delay(32)
                playse(SE_DOOR)
                addobject(17) //ARIANE
                clearflag(FLAG_TEMP_1F)
                delay(32)
            //applymovement comes in the door slowly walks behind the player.
                applymovement(17, Movement_DraynorManor_Entry_2)
			    waitmovement(0)
                applymovement(OBJ_EVENT_ID_PLAYER, MOVEMENT_FACE_DOWN)
			    waitmovement(0)

                namebox("Ariane")
                msgbox(format("…{PAUSE 32}…{PAUSE 32}…\nHe… Hey you…\pCan… can we go together?"))
                closemessage
                hidenamebox
            //apply movement walks infront of the player.
                applymovement(17, Movement_DraynorManor_Entry_4)
			    waitmovement(0)
                applymovement(OBJ_EVENT_ID_PLAYER, MOVEMENT_FACE_LEFT)
			    waitmovement(0)
                msgbox(format("Pssh!\pI'm not scared, what do you take me for?"))
                hidenamebox
            //RAT
                applymovement(6, Movement_DraynorManor_Entry_Rat)
                playmoncry(SPECIES_RAT_NORMAL_FORM, 1)
                namebox("Rat")
                msgbox(format("Squeeek!"))
                waitmovement(0)
                removeobject(6)
                setflag(FLAG_TEMP_19)
                waitmoncry
            
                playse(SE_LEDGE)
                applymovement(17, MOVEMENT_EXCLAMATIONMARK_JUMP_UP)
			    waitmovement(0)
                namebox("Ariane")
                msgbox(format("Eeek!"))
            //apply movement ariane runs behind the player agaain
                applymovement(17, Movement_DraynorManor_Entry_5)
			    waitmovement(0)
                applymovement(OBJ_EVENT_ID_PLAYER, MOVEMENT_FACE_DOWN)
			    waitmovement(0)
                msgbox(format("Okay, maybe I am a little scared…\pYou lead the way."))
                closemessage
                hidenamebox
                setvar(VAR_POKESCAPE_WIZARDS_TOWER_STATE, 8)
                setvar(VAR_POKESCAPE_STORYMODE_PROGRESS, 25)

            //ARIANE JOINS THE PARTY and Follows!
                playfanfare(MUS_OBTAIN_SYMBOL)
                msgbox(format("ARIANE JOINS THE PARTY!"))
                waitfanfare
                closemessage
                hidenamebox
                loadword(0, FOLLOWING_Ariane)
                setfollower(17, 0x80)
                setvar(VAR_TEMP_A, 1) //Sets triggers for leaving.
            case 8:
                applymovement(OBJ_EVENT_ID_PLAYER, Movement_DraynorManor_Entry_3)
			    waitmovement(0)

                getplayerxy(VAR_TEMP_1, VAR_TEMP_2)
                if (var(VAR_TEMP_1) == 24){
                    applymovement(17, MOVEMENT_WALKLEFT)
			        waitmovement(0)
                    applymovement(17, MOVEMENT_WALKLEFT)
			        waitmovement(0)
                } 
                elif (var(VAR_TEMP_1) == 25){
                    applymovement(17, MOVEMENT_WALKLEFT)
			        waitmovement(0)
                }

                


                applymovement(OBJ_EVENT_ID_PLAYER, MOVEMENT_FACE_RIGHT)
			    waitmovement(0)
                namebox("Ariane")
                msgbox(format("…{PAUSE 32}…{PAUSE 32}…\nHe… Hey you came back…\pCan… can we please go together?\pI'm scared."))
                hidenamebox
                //ARIANE JOINS THE PARTY and Follows!
                playfanfare(MUS_OBTAIN_SYMBOL)
                msgbox(format("ARIANE JOINS THE PARTY!"))
                waitfanfare
                closemessage
                hidenamebox
                loadword(0, FOLLOWING_Ariane)
                setfollower(17, 0x80)
                setvar(VAR_TEMP_A, 1) //Sets triggers for leaving.
                setvar(VAR_TEMP_B, 0) //Ends this mapscript
                    
    }
    release
    return
}


                
                
                

movement Movement_DraynorManor_Entry_1 {
    walk_slow_up * 2
}
movement Movement_DraynorManor_Entry_2 {
    delay_16
    walk_slow_up
}
movement Movement_DraynorManor_Entry_3 {
    walk_slow_up
}

movement Movement_DraynorManor_Entry_4 {
    walk_left
    walk_up
    face_player
}
movement Movement_DraynorManor_Entry_5 {
    walk_faster_down
    walk_faster_right
    face_player
}

movement MOVEMENT_EXCLAMATIONMARK_JUMP_LEFT {
    emote_exclamation_mark
    jump_in_place_left
}
movement MOVEMENT_EXCLAMATIONMARK_JUMP_UP {
    emote_exclamation_mark
    jump_in_place_up
}

movement Movement_DraynorManor_Entry_Rat {
    walk_faster_left * 6
    walk_faster_up * 9
}

script FOLLOWING_Ariane {

    namebox("Ariane")
    facefollower 
    random(9)
    switch (var(VAR_RESULT)) {
        case 0:
            msgbox(format("This place is 2spooky4me!"))
        case 1:
            msgbox(format("Where do you think the pink guy went?"))
        case 2:
            msgbox(format("Hey mine name is Ariane, what is yours?\p…{PAUSE 32}{PLAYER}?\nThat's a cute name."))
        case 3:
            msgbox(format("You know what?\pWith you here this place isn't as scary."))
            playse(SE_LEDGE)
            applymovement(17, MOVEMENT_EXCLAMATIONMARK_JUMP_UP)
            waitmovement(0)
            msgbox(format("EEK!\nWhat was that!?"))
        case 4:
            msgbox(format("What's with all of these weird-looking chickens around the place?"))
        case 5:
            msgbox(format("We haven't checked the basement yet, we should look there."))
        case 6:
            msgbox(format("Did…{PAUSE 32} that armour just move?!"))
        case 7:
            msgbox(format("How can anyone even live here…"))
        case 8:
            msgbox(format("Eeek!\pI think I just saw a ghost!"))
    }
    
    hidenamebox
    closemessage
    release
    end
}

script TRIGGER_ARIANE_DRAYNORMANOR_1F_1 {
    facefollower 
    namebox("Ariane")
    msgbox(format("What are you doing?\pThat guy is still in here somewhere.\pWait you aren't going to leave me here are you?"), MSGBOX_YESNO)
    if (var(VAR_RESULT))
    { 
        msgbox(format("Eeek!!"))
        closemessage
        hidenamebox
        setvar(VAR_TEMP_A, 0) //Sets triggers for leaving.
        getplayerxy(VAR_TEMP_1, VAR_TEMP_2)
        if (var(VAR_TEMP_1) == 24){
            warp(MAP_DRAYNOR_MANOR, 0, 0, 0)
        } 
        elif (var(VAR_TEMP_1) == 25){
            warp(MAP_DRAYNOR_MANOR, 1, 0, 0)
        }
        
        destroyfollower
    }
    else {
        closemessage
        hidenamebox
        applymovement(OBJ_EVENT_ID_PLAYER, MOVEMENT_WALK_UP)
        applymovement(OBJ_EVENT_ID_FOLLOWER, MOVEMENT_WALK_UP)
        waitmovement(0)
    }
}

script TRIGGER_ARIANE_DRAYNORMANOR_1F_2 {
    checkfollower
    if (var(VAR_RESULT)) {
        facefollower 
        
        playse(SE_LEDGE)
        applymovement(OBJ_EVENT_ID_FOLLOWER, MOVEMENT_EXCLAMATIONMARK_JUMP_LEFT)
        waitmovement(0)
        namebox("Ariane")
        msgbox(format("Eeek!!\pDid that chair just move?!"))
        closemessage
        hidenamebox
        setvar(VAR_TEMP_C, 1)
    }
    release
    end
}
script TRIGGER_ARIANE_DRAYNORMANOR_1F_3 {
    checkfollower
    if (var(VAR_RESULT)) {
        delay(32)
        applymovement(OBJ_EVENT_ID_FOLLOWER, MOVEMENT_FACE_LEFT)
        waitmovement(0)
        delay(32)
        facefollower
        namebox("Ariane")
        msgbox(format("I hear noises coming from the basement, lets go look!"))
        closemessage
        hidenamebox
        setvar(VAR_TEMP_E, 1)
    }
    release
    end
}
script TRIGGER_ARIANE_DRAYNORMANOR_1F_4 {
    checkfollower
    if (var(VAR_RESULT)) {
        facefollower
        namebox("Ariane")
        msgbox(format("I doubt he would be outside.\pLets go look elsewhere."))
        closemessage
        hidenamebox
        applymovement(OBJ_EVENT_ID_PLAYER, MOVEMENT_WALK_DOWN)
        applymovement(OBJ_EVENT_ID_FOLLOWER, MOVEMENT_WALK_DOWN)
        waitmovement(0)
    }
    release
    end
}
script TRIGGER_ARIANE_DRAYNORMANOR_1F_5 {
    checkfollower
    if (var(VAR_RESULT)) {
        facefollower
        namebox("Ariane")
        msgbox(format("As mysterious as this is, we are looking for the pink man.\pNot some lever side quest."))
        closemessage
        hidenamebox
        applymovement(OBJ_EVENT_ID_PLAYER, MOVEMENT_WALK_RIGHT)
        applymovement(OBJ_EVENT_ID_FOLLOWER, MOVEMENT_WALK_RIGHT)
        waitmovement(0)
        setvar(VAR_TEMP_9, 1)
    }
    release
    end
}
script TRIGGER_ARIANE_DRAYNORMANOR_1F_6 {
    checkfollower
    if (var(VAR_RESULT)) {
        facefollower
        namebox("Ariane")
        msgbox(format("Uh…{PAUSE 32} Actually on second thoughts I'll stay up here.\pYou check down there without me."))
        closemessage
        hidenamebox
        applymovement(OBJ_EVENT_ID_FOLLOWER, MOVEMENT_WALK_RIGHT)
        waitmovement(0)
        facefollower
        applymovement(OBJ_EVENT_ID_PLAYER, MOVEMENT_FACE_LEFT)
        delay(32)
        setvar(VAR_TEMP_9, 1)
        warp(MAP_DRAYNOR_MANOR_CRYPT, 0, 0, 0)
        destroyfollower
    }
    release
    end
}




script DraynorManor_Lever_STATE{
        switch (var(VAR_POKESCAPE_DRAYNORMANOR_LEVER_STATE)){ 
            case 0: 
                //LEFT
                setmetatile(7, 17, 602, 1)
                setmetatile(7, 18, 602, 1)
                setmetatile(7, 19, 602, 1)
                setmetatile(7, 20, 602, 1)
                setmetatile(7, 21, 602, 1)
                //RIGHT
                setmetatile(8, 17, 604, 1)
                setmetatile(8, 18, 604, 1)
                setmetatile(8, 19, 604, 1)
                setmetatile(8, 20, 604, 1)
                setmetatile(8, 21, 604, 1)
                special(DrawWholeMapView) 
                return
            case 1: 
                //LEFT
                setmetatile(7, 17, 570, 1)
                setmetatile(7, 18, 578, 1)
                setmetatile(7, 19, 586, 1)
                setmetatile(7, 20, 516, 0)
                setmetatile(7, 21, 594, 1)
                //RIGHT
                setmetatile(8, 17, 572, 1)
                setmetatile(8, 18, 580, 1)
                setmetatile(8, 19, 588, 1)
                setmetatile(8, 20, 516, 0)
                setmetatile(8, 21, 596, 1)
                special(DrawWholeMapView) 
                return
            }
    }







script SIGN_DRAYNORMANOR_LEVER_1 {
    switch (var(VAR_POKESCAPE_DRAYNORMANOR_LEVER_STATE)) {
                case 0:
			lock
            msgbox(format("You pull a hidden lever!"))
            //LEFT
			setmetatile(7, 17, 570, 1)
            setmetatile(7, 18, 578, 1)
            setmetatile(7, 19, 586, 1)
            setmetatile(7, 20, 516, 0)
            setmetatile(7, 21, 594, 1)
            //RIGHT
			setmetatile(8, 17, 572, 1)
            setmetatile(8, 18, 580, 1)
            setmetatile(8, 19, 588, 1)
            setmetatile(8, 20, 516, 0)
            setmetatile(8, 21, 596, 1)

			special(DrawWholeMapView) 
			setvar(VAR_POKESCAPE_DRAYNORMANOR_LEVER_STATE, 1)
			release
			end

        case 1:
			lock
            msgbox(format("You pull a hidden lever!"))
            //LEFT
			setmetatile(7, 17, 602, 1)
            setmetatile(7, 18, 602, 1)
            setmetatile(7, 19, 602, 1)
            setmetatile(7, 20, 602, 1)
            setmetatile(7, 21, 602, 1)
            //RIGHT
			setmetatile(8, 17, 604, 1)
            setmetatile(8, 18, 604, 1)
            setmetatile(8, 19, 604, 1)
            setmetatile(8, 20, 604, 1)
            setmetatile(8, 21, 604, 1)

			special(DrawWholeMapView) 
			setvar(VAR_POKESCAPE_DRAYNORMANOR_LEVER_STATE, 0)
			release
			end
			
    }
}




script NPC_DraynorManor_LEVER_A{
    msgbox(format("You pull the lever and hear a gate open."))
    //namebox("LEVER A")
    //msgbox(format("LEVER A - ACTIVATED!\pGate 1 OPEN!"))
    closemessage
    //hidenamebox
    setmetatile(17, 16, 516, 0) // 1
    special(DrawWholeMapView)
    end

}

script NPC_DraynorManor_LEVER_B{
    msgbox(format("You pull the lever and hear a gate open."))
    //namebox("LEVER B")
    //msgbox(format("LEVER B - ACTIVATED!\pGate 8 OPEN!"))
    closemessage
    //hidenamebox
    setmetatile(11, 10, 516, 0) // 8
    
    special(DrawWholeMapView)
	end
}

script NPC_DraynorManor_LEVER_C{
    msgbox(format("You pull the lever and hear a gate open."))
    //namebox("LEVER C")
    //msgbox(format("LEVER C - ACTIVATED!\pGate 3 OPEN!"))
    closemessage
    //hidenamebox
    setmetatile(11, 16, 516, 0) // 3
    special(DrawWholeMapView)
	end
}

script NPC_DraynorManor_LEVER_D{
    msgbox(format("You pull the lever and hear a gate open."))
    //namebox("LEVER D")
    //msgbox(format("LEVER D - ACTIVATED!\pGate 4 & 7 OPEN!"))
    closemessage
    //hidenamebox
    setmetatile(8, 13, 516, 0) // 4
    setmetatile(14, 7, 516, 0) // 7
    special(DrawWholeMapView)
	end
}


script NPC_DraynorManor_LEVER_E{
    msgbox(format("You pull the lever and hear a gate open."))
    //namebox("LEVER E")
    //msgbox(format("LEVER E - ACTIVATED!\pGate 2 & 6 OPEN!"))
    closemessage
    //hidenamebox
    setmetatile(14, 13, 516, 0) // 2
    setmetatile(8, 7, 516, 0) // 6
    special(DrawWholeMapView)
	end
}

script NPC_DraynorManor_LEVER_F{
    msgbox(format("You pull the lever and hear a gate open."))
    //namebox("LEVER F")
    //msgbox(format("LEVER F - ACTIVATED!\pGate 5 & 9 OPEN!"))
    closemessage
    //hidenamebox
    setmetatile(4, 10, 516, 0) // 5
    setmetatile(8, 19, 516, 0) // 9
    special(DrawWholeMapView)
	end
}

script NPC_DraynorManor_LEVER_G{
    msgbox(format("You pull the lever and hear a gate open."))
    //namebox("LEVER G")
    //msgbox(format("LEVER G - ACTIVATED!\pGate 9 OPEN!"))
    closemessage
    //hidenamebox
    setmetatile(8, 19, 516, 0) // 9
    special(DrawWholeMapView)
	end
}








script NPC_DraynorManor_Ava{
    lock
	faceplayer
	namebox("Ava")



    if (var(VAR_POKESCAPE_ERNEST_QUEST_STATE) == 1) {
        checkitem (ITEM_OIL_CAN,1)
        if (!var(VAR_RESULT)){
            msgbox(format("Oil Can?\pLast time I used it was to oil up my bed to stop it from creeking.\pWhere did I leave it?\pUh…{PAUSE 32}… I believe I left it in the basement over there.\pSo if you want it you will have to go get it."))
            hidenamebox
            closemessage
            release
            end
        }
        else {
            msgbox(format("Hey you found the Oil Can!\pMy bed is starting to creek again, can I use it before you go haha."))
            closemessage
            hidenamebox
            closemessage
            release
            end
        }
    }


    random(5)
        switch (var(VAR_RESULT)) { 
        case 0: 
            msgbox(format("I think the walls are watching me…"))
            hidenamebox
            closemessage
            release
            end

        case 1: 
            msgbox(format("Who thought it was a good idea to put piranhas in the fountain…\pIt is no where near big enough!"))
            hidenamebox
            closemessage
            release
            end

        case 2: 
            msgbox(format("If you see any animated chairs…\pI assure you, it was not my fault."))
            hidenamebox
            closemessage
            release
            end

        case 3: 
            msgbox(format("I'm overjoyed with my new mattress!\pIt almost feels like I'm being consumed by a fluffy…{PAUSE 15}{PAUSE 15} undead…{PAUSE 15}{PAUSE 15} zombie…{PAUSE 15} chicken…?"))
            hidenamebox
            closemessage
            release
            end
            
        case 4: 
            msgbox(format("If only there were more trainers gullible enough to travel across the land searching far and wide for my questionable research."))
            hidenamebox
            closemessage
            release
            end

        case 5: 
            msgbox(format("EEK!\nA HUMAN…!\p…Sorry…{PAUSE 15} I have spent way too much time here, I'm not used to seeing corporeal beings."))
            hidenamebox
            closemessage
            release
            end

        }
    
}

script NPC_DraynorManor_Quester{
    lock
	faceplayer
	//namebox("Quester")
    if (var(VAR_POKESCAPE_DRAYNORMANOR_LEVER_STATE) == 1) 
    {
        msgbox(format("Did you find the hidden switch?\pWhere was it?!"))
    }
    else
    {
        msgbox(format("I heard there was a hidden switch somewhere in this room but I am yet to find it."))
    }
    //hidenamebox
	closemessage
	release
	end
}

script NPC_DraynorManor_Witch{
    lock
	faceplayer
	namebox("Witch")
    msgbox(format("I have so much poison in my kitchen cupboards!\pI don't think a blueberry special will save you from how toxic that stuff is!"))
    hidenamebox
	closemessage
	release
	end
}

script NPC_DraynorManor_Chair{
    lock
    msgbox(format("It's a chair."))
    hidenamebox
	closemessage
	release
	end
}

script NPC_DraynorManor_Skeleton_1{
    lock
	faceplayer
    namebox("Skeleton")
    msgbox(format("I would really like to play the piano.\pBut I have nobody to play with."))
    hidenamebox
	closemessage
	release
	end
}


//TRAINERS-------------------------


script BATTLE_DRAYNORMANOR_Trainer_1
{
    trainerbattle_single(TRAINER_DRAYNOR_MANOR_SKELETON_1, format("The skeleton stares deep into your eyes before it challenges you to a battle."),format("The skeleton stares intently into your eyes but refuses to move."))
    //namebox ("Vertebrae")
    msgbox(format("The skeleton stares intently into your eyes but refuses to move."))
    hidenamebox
    release
    end
}

script BATTLE_DRAYNORMANOR_Trainer_2
{
    trainerbattle_single(TRAINER_DRAYNOR_MANOR_SKELETON_2, format("Do not steal the Rubber Tube!"),format("Oh you want to return it?"))
    namebox ("McBoneFace")
    msgbox(format("You are going to return the Rubber Tube to Oddenstein?\pSure go for it."))
    hidenamebox
    release
    end
}

script BATTLE_DRAYNORMANOR_Trainer_3
{
    trainerbattle_single(TRAINER_DRAYNOR_MANOR_TRAINER_3, format("Fear me and my monsters!"),format("Ahhh!\pI fear you and your monsters!"))
    namebox ("Dark Wizard")
    msgbox(format("Ahhh!\pI fear you and your monsters!"))
    hidenamebox
    release
    end
}

script BATTLE_DRAYNORMANOR_Trainer_4
{
    trainerbattle_single(TRAINER_DRAYNOR_MANOR_SKELETON_4, format("Boo!"),format("The skeleton is giggling at your previous reaction."))
    //namebox ("Tibia")
    msgbox(format("The skeleton is giggling at your previous reaction."))
    hidenamebox
    release
    end
}

script BATTLE_DRAYNORMANOR_Trainer_5
{
    trainerbattle_single(TRAINER_DRAYNOR_MANOR_SKELETON_5, format("The skeleton strikes a menacing pose."),format("The skeleton stands perfectly still in a menacing pose."))
    //namebox ("Sternum")
    msgbox(format("The skeleton stands perfectly still in a menacing pose."))
    hidenamebox
    release
    end
}

script BATTLE_DRAYNORMANOR_Trainer_6
{
    trainerbattle_single(TRAINER_DRAYNOR_MANOR_SKELETON_6, format("The skeleton looks like it forgot what it was about to say."),format("The skeleton remembered, but the chance to say it is lost."))
    //namebox ("CUPHOLDER")
    msgbox(format("The skeleton remembered, but the chance to say it is lost."))
    hidenamebox
    release
    end
}

script BATTLE_DRAYNORMANOR_Trainer_7
{
    trainerbattle_single(TRAINER_DRAYNOR_MANOR_SKELETON_7, format("The skeleton loses an arm walking towards you."),format("The skeleton is franticly looking for his arm."))
    //namebox ("CUPHOLDER")
    msgbox(format("The skeleton is franticly looking for his arm."))
    hidenamebox
    release
    end
}

script BATTLE_DRAYNORMANOR_Trainer_8
{
    trainerbattle_single(TRAINER_DRAYNOR_MANOR_SKELETON_8, format("…"),format("…………Hi."))
    //namebox ("CUPHOLDER")
    msgbox(format("………………Hi."))
    hidenamebox
    release
    end
}