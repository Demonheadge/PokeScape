mapscripts Tzhaar_1F_MapScripts {
    MAP_SCRIPT_ON_TRANSITION {
        setvar(VAR_EVOLUTION_MAP_STATE, 8) //MAP LOCATIONS: 0-NOTHING, 1-LUMBRIDGE, 16-VARROCK, 3-FALADOR, 4-MEMBERS, 5-OCEAN, 6-WILDERNESS, 7-MORYTANIA, 8-CAVES, 9-ABYSS, 10-OFFWORLD, 11-DESERT, 15-KARAMJA
        call(TZHAAR_STATE)
    }
    MAP_SCRIPT_ON_FRAME_TABLE [
		VAR_POKESCAPE_STORYMODE_PROGRESS, 150: STORYMODE_TZHAAR_NPC
    ]
}


script TZHAAR_STATE {
    setflag(FLAG_TEMP_1C) 	//mithrilman
	removeobject(25)		//mithrilman
	if (var(VAR_MITHRILMAN_STATE) == 10) {
		clearflag(FLAG_TEMP_1C)
		addobject(25)
	}

    if (flag(FLAG_TZHAAR_FOLLOW_GYM_INFO) == TRUE) {
		setobjectxyperm(19, 4, 32)
	}

    

    if (var(VAR_POKESCAPE_STORYMODE_PROGRESS) == 150) {
        clearflag(FLAG_TEMP_1F)
    }
    else {
        setflag(FLAG_TEMP_1F)
    }
}

script TZHAAR_HEAL_ZONE {
    setrespawn(HEAL_LOCATION_TZHAAR_1F)
    setvar(VAR_TEMP_0, 1)
    end
}

script STORYMODE_TZHAAR_NPC
{
    applymovement(OBJ_EVENT_ID_PLAYER, Movement_InteractFACEUP)
    waitmovement(0)
    namebox ("Tzhaar")
    msgbox(format("JalYt, are you well?\pWe wanted to thank you for chasing off those pink-coloured JalYt's.\pThey have been very destructive to the area and attacking our kind.\pFeel free to use our facilities, and if you are an MvM challenger we have a gym located to the west of here.\pWe thank you again.\pGoodbye."))
    closemessage
    hidenamebox
    applymovement(26, HAM_EVENT_CRANDOR_VOLCANO_31)
    waitmovement(0)
    setvar(VAR_POKESCAPE_STORYMODE_PROGRESS, 155)
    subquestmenu (QUEST_MENU_COMPLETE_QUEST, QUEST_MAIN_STORY, 9)
    setflag(FLAG_TEMP_1F)
    removeobject(26)
    setrespawn(HEAL_LOCATION_TZHAAR_1F)
    release
    end
}

movement HAM_EVENT_CRANDOR_VOLCANO_31 {
    walk_up * 8
}


//NPCS
script TZHAAR_GAAL
{
    faceplayer
    namebox ("Ga'al")
    msgbox(format("Guh?\pTzHaar?"))
    closemessage
    hidenamebox
    release
    end
}

script TRIGGER_TZHAAR_GYM_GAAL_1
{
    //if gym badge not obtained
    faceplayer
    namebox ("Ga'al")
    msgbox(format("Allow us to escort you back to the Tzhaar City."))
    closemessage
    hidenamebox
    //applymovement 
    //warp
    release
    end
}



script TZHAAR_FIGHTCAVES_NPC
{
    faceplayer
    namebox ("Tzhaar")
    msgbox(format("Welcome to the Tzhaar Fight Caves!\pOnly those with the Tzhaar badge may enter."))
    closemessage
    hidenamebox
    release
    end
}


script TZHAAR_GYM_NPC
{
    lock
    faceplayer
    namebox ("Ga'Al")
    msgbox(format("Welcome to the Tzhaar Gym."))
    closemessage
    hidenamebox
    release
    end
}

script TZHAAR_NPC_1
{
    faceplayer
    namebox ("Tzhaar")
    if (flag(FLAG_TZHAAR_FOLLOW_GYM_INFO) == FALSE) {
        msgbox(format("We Tzhaar are strong.\pOur strongest participate in what you call MvM Championships.\pWe even have a gym located down this corridor.\pWould you like me to show you?"), MSGBOX_YESNO)
        if (var(VAR_RESULT)) {
            msgbox(format("Okay Jalyt, follow me."))
            closemessage
            hidenamebox
            applymovement(19, MOVEMENT_TZHAAR_GYMWALK_1)

            getplayerxy(VAR_TEMP_1, VAR_TEMP_2)
            if (var(VAR_TEMP_1) == 14){
                applymovement(OBJ_EVENT_ID_PLAYER, MOVEMENT_TZHAAR_GYMWALK_3)
                waitmovement(OBJ_EVENT_ID_PLAYER)
                applymovement(OBJ_EVENT_ID_PLAYER, MOVEMENT_TZHAAR_GYMWALK_5)
            } 
            elif (var(VAR_TEMP_1) == 16){
                applymovement(OBJ_EVENT_ID_PLAYER, MOVEMENT_TZHAAR_GYMWALK_2)
                waitmovement(OBJ_EVENT_ID_PLAYER)
                applymovement(OBJ_EVENT_ID_PLAYER, MOVEMENT_TZHAAR_GYMWALK_5)
            }
            elif (var(VAR_TEMP_2) == 25) {
                applymovement(OBJ_EVENT_ID_PLAYER, MOVEMENT_TZHAAR_GYMWALK_4)
            }
            waitmovement(0)

            msgbox(format("Here we are this is the Tzhaar Gym, our champion is one of the strongest Tzhaar!\pI will be over here if you have anymore questions about the gym."))
            closemessage
            hidenamebox
            applymovement(19, MOVEMENT_TZHAAR_GYMWALK_6)
            waitmovement(0)
            setflag(FLAG_TZHAAR_FOLLOW_GYM_INFO)
        }
    }
    else {
        random(3) 
        switch (var(VAR_RESULT)) { 
            case 0: 
                msgbox(format("The Tzhaar Gym challenge has 5 waves you must battle through before you can battle our champion."))
            case 1: 
                msgbox(format("Our champion knows a move that will do double damage if your monsters are burnt.\pSo watch out for that!"))
            case 2: 
                msgbox(format("Our Fight Caves are basically just a harder version of the gym challenge.\pBut in order to participate in them you must first have our gym badge."))
        }
        closemessage
        hidenamebox
    }
    release
    end
}
movement MOVEMENT_TZHAAR_GYMWALK_1 {
    walk_down * 2
    walk_left * 3
    walk_down
    walk_left
    walk_down
    walk_left
    walk_down
    walk_left
    walk_down
    walk_left * 5
    face_right
}
movement MOVEMENT_TZHAAR_GYMWALK_2 {
    walk_left
}
movement MOVEMENT_TZHAAR_GYMWALK_3 {
    walk_right
}
movement MOVEMENT_TZHAAR_GYMWALK_4 {
    lock_facing_direction
    walk_left
    unlock_facing_direction
    walk_right
    walk_down * 1
    walk_left * 3
    walk_down
    walk_left
    walk_down
    walk_left
    walk_down
    walk_left
    walk_down
    walk_left * 4
}
movement MOVEMENT_TZHAAR_GYMWALK_5 {
    walk_down * 2
    walk_left * 3
    walk_down
    walk_left
    walk_down
    walk_left
    walk_down
    walk_left
    walk_down
    walk_left * 4
}
movement MOVEMENT_TZHAAR_GYMWALK_6 {
    walk_down * 2
    face_up
}

script TZHAAR_NPC_2
{
    lock
    faceplayer
    namebox ("Tzhaar")
    msgbox(format("Through here is the Elder Kiln.\pThis is for Tzhaar only.\pYou are not Tzhaar."))


    //namebox ("Tzhaar")
    //msgbox(format("Through here is the Elder Kiln, which is where you can find our Gym Leader.\pWould you like me to escort you?"))
    closemessage
    hidenamebox
    //Warp to gym leader.

    //if gym badge is got:
    //msgbox(format("Through here is the Elder Kiln, which is where you can find our Gym Leader.\pYou seem skilled enough that you no longer need an escort."))
    release
    end
}

script TZHAAR_NPC_3
{
    faceplayer
    namebox ("Tzhaar")
    msgbox(format("Ever since we have participated in the MvM championships there have been many of your kind passing through our city.\pWe have had to reinforce our tunnels for safety."))
    closemessage
    hidenamebox
    release
    end
}

script TZHAAR_NPC_4
{
    faceplayer
    namebox ("Tzhaar")
    msgbox(format("If you wish to heal check out our bank by the Fight Caves to the south east."))
    closemessage
    hidenamebox
    release
    end
}

script TZHAAR_NPC_5
{
    faceplayer
    namebox ("Tzhaar")
    msgbox(format("Be careful, many of your kind get lost in city.\pIf you get lost try talking to us."))
    closemessage
    hidenamebox
    release
    end
}

script TZHAAR_NPC_6
{
    faceplayer
    namebox ("Tzhaar")
    msgbox(format("To the north are our sacred Kilns.\pTo the west leads deep into the volcano's inferno.\pTo the south-east leads to the Fight Caves.\pTo the south leads to the island.\pSo many ways to go, which way will you take?"))
    closemessage
    hidenamebox
    release
    end
}

script TZHAAR_NPC_7
{
    faceplayer
    namebox ("Tzhaar")
    msgbox(format("I have to constantly clear this corridor as we get cave-in's quite often."))
    closemessage
    hidenamebox
    release
    end
}

script TZHAAR_NPC_8
{
    faceplayer
    namebox ("Tzhaar")
    msgbox(format("While we like to fight, we also like to make friends.\pWill you be our friend?"))
    closemessage
    hidenamebox
    release
    end
}

script TZHAAR_NPC_9
{
    lock
    faceplayer
    namebox ("Tzhaar")
    msgbox(format("When defeated we Tzhaar turn into TokKul.\pWhich is what we use for currency around here.\pBut for all of your MvM supplies we charge you the regular GP."))
    closemessage
    hidenamebox
    release
    end
}

script TZHAAR_NPC_10
{
    lock
    faceplayer
    namebox ("Tzhaar")
    msgbox(format("I could have swore I heard this TokKul talking to me.\pEh, probably nothing."))
    closemessage
    hidenamebox
    release
    end
}

script TZHAAR_NPC_11
{
    lock
    faceplayer
    namebox ("Tzhaar")
    msgbox(format("There are 3 different races.\pTokHaar, Jal and Tzhaar.\pThere are also the Ga'al, they are new.\pSo I suppose there are 4 now."))
    closemessage
    hidenamebox
    release
    end
}

script TZHAAR_NPC_RUNE_ARMOUR
{
    lock
    faceplayer
    namebox ("Choob")
    msgbox(format("These Fight Caves are so hard.\pI keep getting 1-hit…"))
    closemessage
    hidenamebox
    release
    end
}

script TZHAAR_NPC_12
{
    lock
    faceplayer
    namebox ("Tzhaar")
    msgbox(format("These tunnels lead to deep into the heart of the volcano.\pYou are not ready to enter yet."))
    closemessage
    hidenamebox
    release
    end
}

script TZHAAR_NPC_BANKER
{
    lock
    faceplayer
    namebox ("Tzhaar")
    msgbox(format("Greetings, welcome to the Bank of Tzhaar!\pWe are learning new things from this JalYt.\pWhat can I help you with today?"))
    closemessage
    hidenamebox
    goto(BANK_OPTIONS)
    release
    end
}

script TZHAAR_NPC_13
{
    faceplayer
    namebox ("Tzhaar-Ket")
    msgbox(format("JalYt, beyond here the tunnels are unstable and our kind may attack you.\pProceed at your own risk."))
    closemessage
    hidenamebox
    release
    end
}


script TZHAAR_NPC_14
{
    faceplayer
    namebox ("Tzhaar")
    msgbox(format("There are rumours of TzHaar who left our city centuries ago and settled in a volcano somewhere to the west.\pI wonder what life is like for them compared to here."))
    closemessage
    hidenamebox
    release
    end
}

script TZHAAR_NPC_15
{
    faceplayer
    namebox ("Tzhaar")
    msgbox(format("We TzHaar place our eggs in the lava to uncover our ancestors' memories.\pBut some TzHaar placed eggs too deep and what hatched from the eggs were feral monsters who fought each other.\pI think they've now been locked away in separate tunnels for adventurers to try and tame."))
    closemessage
    hidenamebox
    release
    end
}


//Would like to include some variable dialogue for after you have completed the Gym challenge? Maybe 'Ga'al not die in...caves like other Ga'al. All thanks to...you!'

script TZHAAR_OZAN
{
    faceplayer
    namebox ("Ozan")
    msgbox(format("I've been adventuring with a goblin friend called Zanik lately.\pShe says a brave adventurer saved her city and defeated an evil god, and when I look into those big eyes I can't believe she's exaggerating.\pI am sure she's around here somewhere."))
    closemessage
    hidenamebox
    release
    end
}

script TZHAAR_ZANIK
{
    faceplayer
    namebox ("Zanik")
    msgbox(format("I have been fighting in the Fight Caves.\pI haven't gotten to the end yet though.\pThey say there's some huge creature called TzTok-Jad."))
    closemessage
    hidenamebox
    release
    end
}

//Possibly include some variable dialogue that acknowledges you have seen Jad if you have the badge?



script TZHAAR_OUTSIDE_1
{
    faceplayer
    namebox ("Tzhaar")
    msgbox(format("JalYt, are you here to visit our city?\pWe put up a sign so your kind can find it."))
    closemessage
    hidenamebox
    release
    end
}

script TZHAAR_OUTSIDE_2
{
    faceplayer
    namebox ("Tzhaar")
    msgbox(format("Is the lava too hot for you?\pWe find that it is too cold out here.\pWe think we might go back into the city instead."))
    closemessage
    hidenamebox
    release
    end
}
script TZHAAR_SUMMONER
{
    faceplayer
    namebox ("Summoner")
    msgbox(format("They said there was a gym in the Volcano on Karamja.\pBut I don't see any gym!\pAll I see are rocks and monsters."))
    closemessage
    hidenamebox
    release
    end
}
script TZHAAR_IRONMAN
{
    faceplayer
    namebox ("Ironman")
    msgbox(format("I am currently restricted from entering the Tzhaar city.\pOh, it's not because they aren't inviting.\pIt's just the rules I have imposed on myself.\pMakes me feel more accomplished if I survive on my own instead of relying on others, ya know?"))
    closemessage
    hidenamebox
    release
    end
}
script TZHAAR_ARCHAEOLOGIST
{
    faceplayer
    namebox ("Archaeologist")
    msgbox(format("I've had to translate the signposts the Tzhaar have been putting up.\pAs they are writing it in their own language and confusing all the MvM challengers."))
    closemessage
    hidenamebox
    release
    end
}



script TZHAAR_LANGUAGE_BOOK
{
    msgbox(format("It is a book containing a list of commonly used words for the Tzhaar language.\pJalYt - Human.\nMor Ul Rek - Tzhaar City.\pKimit-zil - Familiar.\pThe book seems unfinished."))
    closemessage
    hidenamebox
    release
    end
}
script TZHAAR_SIGNPOST_1
{
    msgbox(format("Mor Ul Rek."))
    closemessage
    hidenamebox
    release
    end
}
script TZHAAR_SIGNPOST_2
{
    msgbox(format("The signpost is scribbled out and replaced with:\pTzhaar City, located inside this volcano."))
    closemessage
    hidenamebox
    release
    end
}






//MITHRILMAN ENCOUNTER 5--------
script TRIGGER_Mithrilman_TZHAAR {
    lockall
	playse(SE_PIN)
	applymovement(25, Movement_Tzhaar_Mithrilman_1)
	waitmovement(0)
	waitse
	applymovement(25, Movement_Tzhaar_Mithrilman_2)
	waitmovement(0)
	goto(Mithrilman_TZHAAR1)
	end
}
movement Movement_Tzhaar_Mithrilman_1 {
	emote_exclamation_mark
	face_left
}
movement Movement_Tzhaar_Mithrilman_2 {
	walk_down
    walk_left
}



script Mithrilman_TZHAAR1 {
    playbgm (MUS_ENCOUNTER_MAY, 1)
    namebox("Mithrilman")
    msgbox(format("Fromst the scorching heats of the dunes to a pleasant vaction I see knave.\pGreetings.\pThoust must be here to battle the Tzhaar?\pAllow thine to grant yee some knowledge as thoust superior in battle provesst.\pFirst of thoust shouldn't use iron inside a volcano.\pIt would smelts and yee would end in malfigured armour.\pNot for the goodst of thoust body, mayhaps a battle helps thoust?\pWell yee should show thine your provess then, beware, for the heat of this battle will be nothing like the volcano!"))
    trainerbattle_no_intro(TRAINER_MITHRILMAN3,format("…"))
    namebox("Mithrilman")
    msgbox(format("Thoust battle fair?\pJokest thine not, the heat from the volcano has malfigured mine gracious Mithril armour.\pWe shalt call this a draw for now, whilst thine seek a smith to fix thine armour.\pFare thoust less than well,\pKnave!"))
    waitmessage
	closemessage
    hidenamebox
	fadescreenswapbuffers (FADE_TO_BLACK)
	removeobject(2)
    setvar(VAR_MITHRILMAN_STATE, 15)
    setflag(FLAG_TEMP_1C) 	//mithrilman
	removeobject(25)		//mithrilman
	fadescreenswapbuffers(FADE_FROM_BLACK)
    subquestmenu (QUEST_MENU_COMPLETE_QUEST, QUEST_MITHRILMAN_QUEST, 2)
    playse(SE_PIN)
	applymovement(OBJ_EVENT_ID_PLAYER, MOVEMENT_QUESTIONMARK)
	waitmovement(0)
	waitse
	msgbox(format("Oh?\pMithrilman dropped something while in a hurry."))
	finditem(ITEM_CHAOS_RUNE, 1)
    savebgm(MUS_DUMMY)
	fadedefaultbgm
    release
    end
}
//-----------------------------

