mapscripts AsgarnianDungeon_B4F_MapScripts 
{
    MAP_SCRIPT_ON_TRANSITION 
    {
        call(AsgarnianDungeon_B4F_STATE)
    }
}


script AsgarnianDungeon_B4F_STATE
{
    if ((var(VAR_WISE_OLD_MAN_RIVAL) == 10) && (var(VAR_POKESCAPE_GAMEMODE_STATE) == 0)) {
        addobject(1)
        clearflag(FLAG_TEMP_1F)
    }
    else {
        removeobject(1)
        setflag(FLAG_TEMP_1F)
    }
    return
}




script TRIGGER_WISEOLDMAN_RIVAL_2{
    lock
    playse(SE_PIN)
    applymovement (1, movement_wom_asgDung_0)
    waitmovement(0)
    delay(60)
    applymovement (1, MOVEMENT_WALK_DOWN)
    waitmovement(0)
    playbgm (MUS_PS_WISE_OLD_MAN_THEME, 1)
    waitmovement(0)
    namebox("Wise Old Man")
    msgbox(format("Aah {PLAYER}, so you decided to humour this old man after all.\pThis place makes for a fine setpiece doesn't it?\pReally makes you tingle for a battle.\pYou have come this far, it is a bit late to turn back now."))
    hidenamebox
    closemessage
    trainerbattle_no_intro(TRAINER_RIVAL_WISE_OLD_MAN_2, format("You are looking more promising by the minute.")) //On defeat."Can you even mine Bluerite in your current state?
    namebox("Wise Old Man")
    msgbox(format("This really is becoming a habit at this point huh?\pYou are growing stronger every battle.\pBy the way, have you seen Melzar the Mad at Rimmington?\pA strong trainer like you could probably learn a thing or two from that place.\pI hope to see you again in the future, it has been fun."))
    hidenamebox
    closemessage
    fadescreen(FADE_TO_BLACK)
    savebgm(MUS_DUMMY)
	fadedefaultbgm
    removeobject(1)
    setflag(FLAG_TEMP_1F) //Wise old man
    setvar(VAR_WISE_OLD_MAN_RIVAL, 15) //15 - Met at Asgarnia Dungeon. (Optional)
    fadescreen(FADE_FROM_BLACK)
    release
    end
}
movement movement_wom_asgDung_0{
    face_down
    emote_exclamation_mark
    delay_16
}

