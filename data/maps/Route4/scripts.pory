const OBJ_EVENT_ID_DAYCAREWATCHER = 2
const OBJ_EVENT_ID_PREZLEEK = 19
const OBJ_EVENT_ID_BAROO = 20
const OBJ_EVENT_ID_SEREN = 21
const OBJ_EVENT_ID_ZAROS = 10
const OBJ_EVENT_ID_TUMEKEN = 12
const OBJ_EVENT_ID_GODLESS = 25
const OBJ_EVENT_ID_ARMADYL = 10


mapscripts Route4_MapScripts {
    MAP_SCRIPT_ON_TRANSITION {
        setvar(VAR_EVOLUTION_MAP_STATE, 1) //MAP LOCATIONS: 0-NOTHING, 1-LUMBRIDGE, 16-VARROCK, 3-FALADOR, 4-MEMBERS, 5-OCEAN, 6-WILDERNESS, 7-MORYTANIA, 8-CAVES, 9-ABYSS, 10-OFFWORLD, 11-DESERT, 15-KARAMJA
        call(ROUTE_4_STATE)
        call(POTTERINGTON_FARM_OUTDOOR_STATE)
        goto (TryMoveDaycareMan)
    }
}


script GOD_FARM_TEST_TRIGGER {
    clearflag(FLAG_TEMP_1)
    clearflag(FLAG_TEMP_2)
    clearflag(FLAG_TEMP_3)
    clearflag(FLAG_TEMP_4)
    clearflag(FLAG_TEMP_5)
    clearflag(FLAG_TEMP_6)
    clearflag(FLAG_TEMP_7)
    clearflag(FLAG_TEMP_8)
    clearflag(FLAG_TEMP_9)
    clearflag(FLAG_TEMP_10)
    clearflag(FLAG_TEMP_11)
    clearflag(FLAG_TEMP_12)
    clearflag(FLAG_TEMP_A)
    addobject(20)
    addobject(21)
    addobject(10)
    addobject(12)
    addobject(25)
    addobject(10)
    addobject(1)
    addobject(2)
    addobject(3)
    addobject(4)
    addobject(5)
    addobject(6)
    addobject(7)
    addobject(8)
    addobject(9)
    addobject(10)
    addobject(11)
    addobject(21)
}



script ROUTE_4_STATE {
//HAM
    removeobject(9)
    removeobject(11)
    setflag(FLAG_TEMP_1F)
//PREZLEEK
    setflag(FLAG_TEMP_4)
    removeobject(OBJ_EVENT_ID_PREZLEEK)
    removeobject(OBJ_EVENT_ID_BAROO)
//Seren
    setflag(FLAG_TEMP_A)
    removeobject(OBJ_EVENT_ID_SEREN)
    removeobject(OBJ_EVENT_ID_ZAROS)
    setflag(FLAG_TEMP_5)


    checkitem (ITEM_TERRORBIRD_POUCH, 1)
    if (var(VAR_RESULT))
    {
        clearflag(FLAG_TEMP_4)
        addobject(OBJ_EVENT_ID_PREZLEEK)
        addobject(OBJ_EVENT_ID_BAROO)
    }
    special(Is_InParty_SEREN)
    if (var(VAR_RESULT) == TRUE || (flag(FLAG_RECEIVED_SERELING_EGG))) {
        clearflag(FLAG_TEMP_A)
        addobject(OBJ_EVENT_ID_SEREN)
    }
    special(Is_InParty_ZAROS)
    if (var(VAR_RESULT) == TRUE || (flag(FLAG_RECEIVED_ZAROLING_EGG))) {
        clearflag(FLAG_TEMP_5)
        addobject(OBJ_EVENT_ID_ZAROS)
    }
    special(Is_InParty_TUMEKEN)
    if (var(VAR_RESULT) == TRUE || (flag(FLAG_RECEIVED_TUMEKLING_EGG))) {
        clearflag(FLAG_TEMP_6)
        addobject(OBJ_EVENT_ID_TUMEKEN)
    }
    special(Is_InParty_GODLESS)
    if (var(VAR_RESULT) == TRUE || (flag(FLAG_RECEIVED_UNGODLING_EGG))) {
        clearflag(FLAG_TEMP_7)
        addobject(OBJ_EVENT_ID_GODLESS)
    }
    special(Is_InParty_ARMADYL)
    if (var(VAR_RESULT) == TRUE || (flag(FLAG_RECEIVED_ARMALING_EGG))) {
        clearflag(FLAG_TEMP_8)
        addobject(OBJ_EVENT_ID_ARMADYL)
    }

    if (var(VAR_POKESCAPE_GAMEMODE_STATE) == 0) //if STORYMODE
    {
        switch (var(VAR_POKESCAPE_STORYMODE_PROGRESS)){ 
            case 100:
            case 101:
                addobject(9)
                addobject(11)
                clearflag(FLAG_TEMP_1F)
                return
        }
    }
}


script POTTERINGTON_FARM_OUTDOOR_STATE{
    switch (var(VAR_POKESCAPE_POTTERINGTON_FARM_STATE)){ 
        case 0: 
            return
        case 1: 
            setobjectxyperm(OBJ_EVENT_ID_DAYCAREWATCHER,8,23)
            return
    }
}








script MuggerBattleRoute4{
    trainerbattle_single(TRAINER_MUGGER_ROUTE4, "Stand and deliver!",format("People seem very afraid of me.\pMaybe it's the way I say hello?"))
    namebox ("Miles")
    msgbox(format("People seem very afraid of me.\pMaybe it's the way I say hello?"))
    release
}

script ScammerBattleRoute4{
    trainerbattle_single(TRAINER_SCAMMER_ROUTE4, format("Psst… Want to buy some cabbage?"),format("A no would have been fine…"))
    namebox ("Fr33Trims")
    msgbox(format("Selling rare black lobster!"))
    release
}

script FarmerBattleRoute4{
    trainerbattle_single(TRAINER_FARMER_ROUTE4, format("I breed all my animals here at Potterington farm."),format("Guess I need to breed stronger monsters!"))
    namebox ("Leeroy")
    msgbox(format("Have you checked out the farm?\pIt's a great place to raise monsters of your own!"))
    release
}

script Route4Sign{
    msgbox("Potterington Farm", MSGBOX_SIGN)
    release
}

script TryMoveDaycareMan{
    if (flag(FLAG_PENDING_DAYCARE_EGG)){
        setobjectxyperm(1,19,23)
    }
    else{
        setobjectxyperm(1,19,22)
    }
}







script ROUTE4_TRIGGER1_DaycareWatcher 
{
    playse(SE_PIN)
    applymovement(2, MOVEMENT_ROUTE4_NIECE_1)
    waitmovement (0)

    getplayerxy(VAR_TEMP_1, VAR_TEMP_2)
    if (var(VAR_TEMP_2) == 27){
        applymovement(2, MOVEMENT_ROUTE4_NIECE_2)
        waitmovement (0)
    } 

    applymovement(OBJ_EVENT_ID_PLAYER, MOVEMENT_FACE_UP)
    waitmovement (0)

    namebox ("Niece Pott.")
    msgbox(format("Hey!\nThose monsters…\pLet me see them!\p…{PAUSE 15}…{PAUSE 15}…\pAmazing, I can see that your monsters trust you quite a lot!\pFollow me adventurer, I wish to show you something."))
    closemessage
    hidenamebox

    getplayerxy(VAR_TEMP_1, VAR_TEMP_2)
    if (var(VAR_TEMP_2) == 27){
        applymovement(2, MOVEMENT_ROUTE4_NIECE_3)
        applymovement(OBJ_EVENT_ID_PLAYER, MOVEMENT_ROUTE4_NIECE_3)
        waitmovement (0)
    } 
        applymovement(OBJ_EVENT_ID_PLAYER, MOVEMENT_ROUTE4_NIECE_4)
        applymovement(2, MOVEMENT_ROUTE4_NIECE_5)
        waitmovement (0)
    
    delay(8)
    namebox ("Niece Pott.")
    msgbox(format("This place is called Potterington Farm!\pHere we can look after your Monsters and help them grow while you are away.\pWe have also set up breeding pens for the monsters.\pYou should talk to my mother inside, if that is something that interests you.\pAnyway, that is all I wanted to show you.\pStop by anytime, take care!"))
    closemessage
    hidenamebox
    applymovement(OBJ_EVENT_ID_DAYCAREWATCHER, MOVEMENT_ROUTE4_NIECE_6)
    waitmovement (0)
    setobjectxyperm(OBJ_EVENT_ID_DAYCAREWATCHER,8,23)
    setvar(VAR_POKESCAPE_POTTERINGTON_FARM_STATE, 1)
    release
    end
}



movement MOVEMENT_ROUTE4_NIECE_1
{
    emote_exclamation_mark
    delay_16
}

movement MOVEMENT_ROUTE4_NIECE_2
{
    walk_down
}

movement MOVEMENT_ROUTE4_NIECE_3
{
    walk_up
}

movement MOVEMENT_ROUTE4_NIECE_4
{
    walk_up * 7
    walk_left
}

movement MOVEMENT_ROUTE4_NIECE_5
{
    walk_up * 6
    walk_left * 2
    face_player
}

movement MOVEMENT_ROUTE4_NIECE_6
{
    walk_left * 5
    walk_up * 1
    walk_left * 2
    walk_up * 1
    delay_8
    face_down
}   

script ROUTE4_DaycareWatcher 
{
    faceplayer
    namebox ("Niece Pott.")
    random(5)
        switch (var(VAR_RESULT)) {
            case 0:
                msgbox(format("Some people have been reporting different coloured monsters hatching from their eggs which are different from the parents."))
            case 1:
                msgbox(format("There have been rumours that multiple monsters have been breeding together and creating hybrids.\pBut I've yet to see any of these so called hybrids."))
            case 2:
                msgbox(format("I really enjoy just sitting here and seeing what type of Monsters the next adventurer will bring."))
            case 3:
                msgbox(format("Over time high achieving adventurers visit our little farm and hand out gifts.\pI wonder what makes them decide to visit our little farm."))
            case 4:
                msgbox(format("Did you know that Potterington Farm is a family run buisness?\pMy mother and father run this farm and my grandma runs the farm over near Ardougne!"))
                
        }
    closemessage
    hidenamebox
    release
    end
}








script ROUTE4_PatchFarmer
{
    faceplayer
    namebox ("Farmer")
    msgbox(format("Did you know you can plant crops in farming patches?"))
    closemessage
    hidenamebox
    release
    end
}

script ROUTE4_Summoner
{
    faceplayer
    namebox ("Summoner")
    msgbox(format("These farmers told me that they would raise my monsters, but when I came back they dumped an egg on me…\pI'm not ready to be a parent…"))
    closemessage
    hidenamebox
    release
    end
}

script ROUTE4_Diviner
{
    faceplayer
    namebox ("Diviner")
    msgbox(format("I'm about to go on holiday and I need someone look after my Sheep and Wolf while I am away.\pThankfully Potterington farm offered to look after them while I am away."))
    closemessage
    hidenamebox
    release
    end
}




script POTTERINGTON_FARM_Prezleek
{
    lockall
    faceplayer
    checkitem (ITEM_YAK_POUCH, 1)
	if (!var(VAR_RESULT))
	{
        checkitem (ITEM_TERRORBIRD_POUCH, 1)
        if (var(VAR_RESULT))
        {
            namebox ("Prezleek")
            msgbox(format("Hello.\pWait…{PAUSE 16} What is that…?{PAUSE 16}\nA rideable Terrorbird?!\pUnbelieveable everyone knows Yaks are the best source of transportation."))
            applymovement(OBJ_EVENT_ID_PREZLEEK, MOVEMENT_INTERACT_RIGHT)
            waitmovement(0)
            msgbox(format("Baroo, you agree with me right?"))
            closemessage
            hidenamebox
            //applymovement(OBJ_EVENT_ID_BAROO, Movement_InteractFACERIGHT)
            //waitmovement(0)
            namebox ("Baroo")
            msgbox(format("Baroooo, Baroo!"))
            closemessage
            hidenamebox
            applymovement(OBJ_EVENT_ID_PREZLEEK, Movement_InteractFACEPLAYER)
            waitmovement(0)
            namebox ("Prezleek")
            msgbox(format("See even Baroo agrees.\pHere have this."))
            closemessage
            hidenamebox
            giveitem(ITEM_YAK_POUCH, 1)
        }
    }
    namebox ("Prezleek")
    msgbox(format("See aren't Yaks so much better than Terrorbirds?"))
    closemessage
    hidenamebox
    release
    end   
}

script POTTERINGTON_FARM_Baroo
{
    faceplayer
    namebox ("Baroo")
    //PLAY SOUND
    msgbox(format("Baroo, baroo!"))
    //WAITSOUND
    closemessage
    hidenamebox
    release
    end
}


script POTTERINGTON_FARM_MysteriousFigure
{
    lock
    faceplayer
    if (!flag(FLAG_RECEIVED_ZAROLING_EGG)) {
        
        namebox ("???")
        msgbox(format("Greetings, I've come from the otherside of Gilenor.\pAlong my travels my dark lord asked me to pass this on to a person would I would meet here at this time and this date.\pHere take it."))
        closemessage
        hidenamebox
        //GIVE EGG
        getpartysize
        compare (VAR_RESULT, PARTY_SIZE)
        goto_if_eq (SCRIPT_EGG_NOROOM)
        setflag (FLAG_RECEIVED_ZAROLING_EGG)
        playfanfare (MUS_OBTAIN_ITEM)
        msgbox(format("Obtained an EGG from the Mysterious Figure!"))
        waitfanfare
        giveegg (SPECIES_ZAROLING)
        namebox ("???")
        msgbox(format("The dark lord works in mysterious ways."))
    }
    else {
        namebox ("???")
        msgbox(format("The dark lord works in mysterious ways."))
    }
    hidenamebox
    closemessage
	release
	end
}


script POTTERINGTON_FARM_Camel
{
    lock
    faceplayer
    if (!flag(FLAG_RECEIVED_TUMEKLING_EGG)) {
    
        namebox ("Camel")
        msgbox(format("Urrragghhh!!\pHawk tuah!"))
        closemessage
        hidenamebox
        msgbox(format("The camel spits something at you."))
        closemessage
        hidenamebox
        //GIVE EGG
        getpartysize
        compare (VAR_RESULT, PARTY_SIZE)
        goto_if_eq (SCRIPT_EGG_NOROOM)
        setflag (FLAG_RECEIVED_TUMEKLING_EGG)
        playfanfare (MUS_OBTAIN_ITEM)
        msgbox(format("Obtained an EGG from ???!"))
        waitfanfare
        giveegg (SPECIES_TUMEKLING)
        namebox ("Camel")
        msgbox(format("Hawwwwwwk-{PAUSE 30}\n…{PAUSE 15}Puah…{PAUSE 15}…"))
        closemessage
        hidenamebox
        msgbox(format("The camel seems to have gotten a dry mouth now."))
    }
    else {
        namebox ("Camel")
        msgbox(format("Hawwwwwwk-{PAUSE 30}\n…{PAUSE 15}Puah…{PAUSE 15}…"))
        closemessage
        hidenamebox
        msgbox(format("The camel tries to spit at you!\p…{PAUSE 15}…{PAUSE 15} But it's mouth is too dry."))
    }
    hidenamebox
    closemessage
	release
	end
}

script POTTERINGTON_FARM_Barbarian
{
    lock
    faceplayer
    if (!flag(FLAG_RECEIVED_UNGODLING_EGG)) {
    
        namebox ("Holstein")
        msgbox(format("What is with all of these gods at this farm?!\pDisguesting.\pHere take this."))
        closemessage
        hidenamebox
        //GIVE EGG
        getpartysize
        compare (VAR_RESULT, PARTY_SIZE)
        goto_if_eq (SCRIPT_EGG_NOROOM)
        setflag (FLAG_RECEIVED_UNGODLING_EGG)
        playfanfare (MUS_OBTAIN_ITEM)
        msgbox(format("Obtained an EGG from Holstein!"))
        waitfanfare
        giveegg (SPECIES_UNGODLING)
        namebox ("Holstein")
        msgbox(format("REBEL WITH ME!"))
    }
    else {
        namebox ("Holstein")
        msgbox(format("REBEL WITH ME!\pDOWN WITH ALL THE GODS!"))
    }
    hidenamebox
    closemessage
	release
	end
}
script POTTERINGTON_FARM_ARMADYL
{
    lock
    faceplayer
    if (!flag(FLAG_RECEIVED_ARMALING_EGG)) {
        msgbox(format("It's a carrier pigeon!\pIt appears to have something for you."))
        closemessage
        hidenamebox
        //GIVE EGG
        getpartysize
        compare (VAR_RESULT, PARTY_SIZE)
        goto_if_eq (SCRIPT_EGG_NOROOM)
        setflag (FLAG_RECEIVED_ARMALING_EGG)
        playfanfare (MUS_OBTAIN_ITEM)
        msgbox(format("Obtained an EGG from the pigeon!"))
        waitfanfare
        giveegg (SPECIES_UNGODLING)
        msgbox(format("It came with a note attached.\p{PLAYER} I entr…st you w…th this egg.\nR…ise it well.\p- Fr…m Arm…d-\pThe rest of the words are too covered in bird poop to make out."))
    }
    else {
        playmoncry(SPECIES_PIGEON, 1)
        namebox ("Pigeon")
        msgbox(format("Berr… Berrr…"))
        waitmoncry
        closemessage
        hidenamebox
        msgbox(format("The pigeon is too focused on eating the seeds on the ground."))
        closemessage
        hidenamebox
    }
    hidenamebox
    closemessage
	release
	end
}