const OBJ_EVENT_ID_QUEST_KNIGHT = 39
const OBJ_EVENT_ID_QUEST_BLACKKNIGHT = 51
const OBJ_EVENT_ID_QUEST_BLACKKNIGHT2 = 58
const OBJ_EVENT_ID_QUEST_OWEN = 32
const OBJ_EVENT_ID_QUEST_HAM_1 = 40
const OBJ_EVENT_ID_QUEST_HAM_2 = 33


mapscripts Dwarven_Mine_B1F_MapScripts {
    MAP_SCRIPT_ON_TRANSITION {
        setvar(VAR_EVOLUTION_MAP_STATE, 43) //MAP LOCATIONS: 0-NOTHING, 1-LUMBRIDGE, 16-VARROCK, 3-FALADOR, 4-MEMBERS, 5-OCEAN, 6-WILDERNESS, 7-MORYTANIA, 43-CAVES, 9-ABYSS, 10-OFFWORLD, 11-DESERT, 15-KARAMJA, 
    
        if (var(VAR_POKESCAPE_GAMEMODE_STATE) == 0) //if STORY
    	{
            goto(DWARVENMINES_QUEST_STATE)
    	}
		else
		{
			call(OPENWORLD_STATE_DWARVENMINES)
		}
    }
}

script OPENWORLD_STATE_DWARVENMINES {
		removeobject(OBJ_EVENT_ID_QUEST_KNIGHT)
        removeobject(OBJ_EVENT_ID_QUEST_BLACKKNIGHT)
        removeobject(OBJ_EVENT_ID_QUEST_OWEN)
        removeobject(OBJ_EVENT_ID_QUEST_HAM_1)
        removeobject(OBJ_EVENT_ID_QUEST_HAM_2)
        setflag(FLAG_TEMP_1)
        setflag(FLAG_TEMP_2)
		return
}

script DWARVENMINES_QUEST_STATE{
	switch (var(VAR_POKESCAPE_FALADOR_STATE)){ 
		case 0:
		case 1: 
		case 2:
            removeobject(OBJ_EVENT_ID_QUEST_BLACKKNIGHT)
            removeobject(OBJ_EVENT_ID_QUEST_BLACKKNIGHT2)
			setflag(FLAG_TEMP_2)
            setflag(FLAG_TEMP_F)
			return
        case 3:
        case 4:    
        case 5:
            removeobject(OBJ_EVENT_ID_QUEST_KNIGHT)
            removeobject(OBJ_EVENT_ID_QUEST_BLACKKNIGHT)
            removeobject(OBJ_EVENT_ID_QUEST_OWEN)
            removeobject(OBJ_EVENT_ID_QUEST_HAM_1)
            removeobject(OBJ_EVENT_ID_QUEST_HAM_2)
            setflag(FLAG_TEMP_1)
			setflag(FLAG_TEMP_2)
            setflag(FLAG_TEMP_F)
			return
	}
}



script NPC_SCORPION
{
	lock
	faceplayer
    namebox ("SCORPION")
    playmoncry(SPECIES_GLIGAR, 0)
	msgbox(format("Snipersnip!"))
    waitmoncry
    hidenamebox
	release
	end
}

script NPC_DWARVENMINES_DWARF_1
{
	lock
	faceplayer
    namebox ("DWARF")
	msgbox(format("Sorry we are currently undergoing a Mining & Smithing rework.\pPlease come again later."))
    hidenamebox
	release
	end
}

script NPC_DWARVENMINES_DWARF_2
{
	lock
	faceplayer
    namebox ("DWARF")
	msgbox(format("There has been a cave-in within the Motherlode Mine, it might take us awhile to clear it.\pI feel for those poor adventurers trapped inside when it clapsed though."))
    hidenamebox
	release
	end
}

script NPC_DWARVENMINES_MINECART
{
	lock
	msgbox(format("There seems to be nothing inside this minecart apart from a very boring looking scroll."))
    hidenamebox
	release
	end
}

script NPC_DWARVENMINES_CAVEIN
{
	lock
	msgbox(format("It's impossible to go any further due to a cave-in."))
    hidenamebox
	release
	end
}

script NPC_DWARVENMINES_DWARF_3
{
	lock
	faceplayer
    namebox ("DWARF")
	msgbox(format("My job is to unload the ores from these carts and get them ready for transportation.\pIt can be a hard job when you are sorting through the ores only to find some Scoprions nibbling on Earth Runes."))
    hidenamebox
	release
	end
}

script NPC_DWARVENMINES_DROGO
{
	lock
	faceplayer
    namebox ("Drogo")
	msgbox(format("'Ello, welcome to my Mining shop, friend!\pIs what I would say if I had any stock left! Sorry, I'm still waiting on more supplies arriving from Keldagrim.\pPlease come back again later!"))
    hidenamebox
	release
	end
}

//ITEMS---------------------------------
//HIDDEN ITEMS:
script ITEM_DWARVENMINES_1
{
    finditem(ITEM_ROCKCAKE)
    end
}
script ITEM_DWARVENMINES_2
{
    finditem(ITEM_GRANITE_POUCH)
    end
}
script ITEM_DWARVENMINES_3
{
    finditem(ITEM_EARTH_RUNE)
    end
}
script ITEM_DWARVENMINES_4
{
    finditem(ITEM_TEA)
    end
}

//QUEST--------------
script NPC_FALADORCASTLE_QUEST_PART_3_1
{
    lock
    applymovement(OBJ_EVENT_ID_PLAYER, DWARVENMINES_MOVEMENT_0_1)
    waitmovement(0)
    goto(NPC_FALADORCASTLE_QUEST_PART_3)
}
script NPC_FALADORCASTLE_QUEST_PART_3_2
{
    lock
    applymovement(OBJ_EVENT_ID_PLAYER, DWARVENMINES_MOVEMENT_0_2)
    waitmovement(0)
    goto(NPC_FALADORCASTLE_QUEST_PART_3)
}
script NPC_FALADORCASTLE_QUEST_PART_3_3
{
    lock
    applymovement(OBJ_EVENT_ID_PLAYER, DWARVENMINES_MOVEMENT_0_3)
    waitmovement(0)
    goto(NPC_FALADORCASTLE_QUEST_PART_3)
}




script NPC_FALADORCASTLE_QUEST_PART_3
{
    applymovement(OBJ_EVENT_ID_QUEST_KNIGHT, MOVEMENT_INTERACT_RIGHT)
    namebox ("Sir Theodore")
	msgbox(format("Those men went that way.\nThey have cornered themselves."))
    closemessage
    hidenamebox
    waitmovement(0)
    applymovement(OBJ_EVENT_ID_QUEST_OWEN, MOVEMENT_INTERACT_LEFT)
    namebox ("Sir Owen")
	msgbox(format("Great work Theodore, let's go get them!"))
    closemessage
    hidenamebox
    waitmovement(0)
    applymovement(OBJ_EVENT_ID_QUEST_KNIGHT, DWARVENMINES_MOVEMENT_1)
    playse(SE_PIN)
    waitmovement(0)
    namebox ("Sir Theodore")
	msgbox(format("Wait sir, we have an issue."))
    closemessage
    hidenamebox
    applymovement(OBJ_EVENT_ID_QUEST_OWEN, DWARVENMINES_MOVEMENT_2)
    waitmovement(0)
    namebox ("Sir Owen")
	msgbox(format("Oh?\nAre you perhaps an adventurer?\pFantastic!\pCould I please ask of you to assist us with some backup in case things get a bit ugly?\pYou see we are about to apprehend some suspicious men that have been causing a disturbance in the mines."))
    closemessage
    hidenamebox
    applymovement(OBJ_EVENT_ID_QUEST_KNIGHT, DWARVENMINES_MOVEMENT_6)
    waitmovement(0)
    namebox ("Sir Theodore")
	msgbox(format("Sir, if we don't act now they will get away."))
    closemessage
    hidenamebox
    applymovement(OBJ_EVENT_ID_QUEST_OWEN, DWARVENMINES_MOVEMENT_7)
    waitmovement(0)
    namebox ("Sir Owen")
	msgbox(format("Right you are Theodore, with us adventurer!"))
    closemessage
    hidenamebox
    applymovement(OBJ_EVENT_ID_QUEST_KNIGHT, DWARVENMINES_MOVEMENT_4)
    applymovement(OBJ_EVENT_ID_QUEST_OWEN, DWARVENMINES_MOVEMENT_3)
    applymovement(OBJ_EVENT_ID_PLAYER, DWARVENMINES_MOVEMENT_5)
    waitmovement(0)

    //PLAY MUSIC
    playbgm (MUS_PS_ENCOUNTER_HAM, 1)

    applymovement(OBJ_EVENT_ID_QUEST_HAM_1, MOVEMENT_INTERACT_RIGHT)
    namebox ("H.A.M Member")
	msgbox(format("Is this it?\pJust a scrap of paper?"))
    closemessage
    hidenamebox
    waitmovement(0)
    applymovement(OBJ_EVENT_ID_QUEST_HAM_2, MOVEMENT_INTERACT_LEFT)
    namebox ("H.A.M Grunt")
	msgbox(format("I guess so…\pGuess we should report back to the boss on Karamja."))
    closemessage
    hidenamebox
    waitmovement(0)
    applymovement(OBJ_EVENT_ID_QUEST_OWEN, MOVEMENT_WALK_UP)
    waitmovement(0)
    namebox ("Sir Owen")
	msgbox(format("HALT!"))
    closemessage
    hidenamebox
    
    applymovement(OBJ_EVENT_ID_QUEST_HAM_1, DWARVENMINES_MOVEMENT_1)
    applymovement(OBJ_EVENT_ID_QUEST_HAM_2, DWARVENMINES_MOVEMENT_1)
    playse(SE_PIN)
    //applymovement(OBJ_EVENT_ID_QUEST_HAM_1, DWARVENMINES_MOVEMENT_2)
    //applymovement(OBJ_EVENT_ID_QUEST_HAM_2, DWARVENMINES_MOVEMENT_2)
    waitmovement(0)
    namebox ("H.A.M Member")
	msgbox(format("What?!\pThe White Knights?!"))
    closemessage
    hidenamebox
    namebox ("Sir Owen")
	msgbox(format("You'll be coming with us back to the castle."))
    closemessage
    hidenamebox
    applymovement(OBJ_EVENT_ID_QUEST_HAM_2, MOVEMENT_INTERACT_DOWN)
    namebox ("H.A.M Grunt")
	msgbox(format("Yeh, we don't think so."))
    closemessage
    hidenamebox
    waitmovement(0)
    addobject(OBJ_EVENT_ID_QUEST_BLACKKNIGHT)
    addobject(OBJ_EVENT_ID_QUEST_BLACKKNIGHT2)
    clearflag(FLAG_TEMP_2)
    applymovement(OBJ_EVENT_ID_QUEST_BLACKKNIGHT, DWARVENMINES_MOVEMENT_8)
    applymovement(OBJ_EVENT_ID_QUEST_BLACKKNIGHT2, DWARVENMINES_MOVEMENT_10)
    waitmovement(0)
    applymovement(OBJ_EVENT_ID_PLAYER, DWARVENMINES_MOVEMENT_7)
    applymovement(OBJ_EVENT_ID_QUEST_OWEN, MOVEMENT_FACE_DOWN)
    applymovement(OBJ_EVENT_ID_QUEST_KNIGHT, MOVEMENT_FACE_LEFT)
    waitmovement(0)
    namebox ("Sir Owen")
	msgbox(format("The Black Knights?!"))
    closemessage
    hidenamebox
    namebox ("Black Knight")
	msgbox(format("Time to get rid of you pesky White Knights once and for all!"))
    closemessage
    hidenamebox
    applymovement(OBJ_EVENT_ID_QUEST_OWEN, MOVEMENT_INTERACT_DOWN)
    applymovement(OBJ_EVENT_ID_PLAYER, DWARVENMINES_MOVEMENT_9)
    namebox ("Sir Owen")
	msgbox(format("Looks like we are in a tight spot.\pI'll take these two.\pTheodore, adventurer, I trust you know what to do."))
    closemessage
    hidenamebox
    waitmovement(0)
    applymovement(OBJ_EVENT_ID_PLAYER, DWARVENMINES_MOVEMENT_7)
    applymovement(OBJ_EVENT_ID_QUEST_OWEN, DWARVENMINES_MOVEMENT_9)
    applymovement(OBJ_EVENT_ID_QUEST_KNIGHT, MOVEMENT_INTERACT_LEFT)
    namebox ("Sir Theodore")
	msgbox(format("Understood, Sir Owen!"))
    closemessage
    hidenamebox
    waitmovement(0)
    namebox ("Black Knight")
	msgbox(format("You stand no chance!"))
    closemessage
    hidenamebox
    trainerbattle_no_intro(TRAINER_DWARVENMINES_BLACKKNIGHT, msgbox(format("Blasted White Knights!")))
    namebox ("Black Knight")
	msgbox(format("Impossible… You are so strong…"))
    closemessage
    hidenamebox
    applymovement(OBJ_EVENT_ID_QUEST_HAM_2, MOVEMENT_FACE_LEFT)
    waitmovement(0)
    namebox ("H.A.M Grunt")
	msgbox(format("No way, even with backup we lost…\pWe cannot let them have the map."))
    closemessage
    hidenamebox
    
    applymovement(OBJ_EVENT_ID_QUEST_OWEN, MOVEMENT_FACE_UP)
    waitmovement(0)
    namebox ("Sir Owen")
	msgbox(format("And what map might that be?"))
    closemessage
    hidenamebox
    namebox ("H.A.M Grunt")
	msgbox(format("Uhh… nothing, nothing at all!\pDefinitely not this scrap of paper.\pNope, no map here!"))
    closemessage
    hidenamebox
    namebox ("Sir Owen")
	msgbox(format("Very well, we will just bring you four in for questioning."))
    closemessage
    hidenamebox
    applymovement(OBJ_EVENT_ID_QUEST_OWEN, DWARVENMINES_MOVEMENT_2)
    waitmovement(0)
    namebox ("Sir Owen")
    msgbox(format("Thank you for your assistance challenger.\pIf you could stop by the castle a bit later and assist me in filling out a report that'd be appricated."))
    closemessage
    hidenamebox
    applymovement(OBJ_EVENT_ID_QUEST_BLACKKNIGHT, MOVEMENT_INTERACT_UP)
    namebox ("Black Knight")
	msgbox(format("You'll never get anything from us."))
    closemessage
    hidenamebox
    waitmovement(0)
    namebox ("Sir Owen")
    msgbox(format("We'll see about that…"))
    closemessage
    hidenamebox
    

    savebgm(MUS_DUMMY)
	fadedefaultbgm
    
    fadescreen(FADE_TO_BLACK)
    setflag(FLAG_TEMP_1)
    setflag(FLAG_TEMP_2)
    removeobject(OBJ_EVENT_ID_QUEST_HAM_1)
    removeobject(OBJ_EVENT_ID_QUEST_HAM_2)
    removeobject(OBJ_EVENT_ID_QUEST_KNIGHT)
    removeobject(OBJ_EVENT_ID_QUEST_BLACKKNIGHT)
    removeobject(OBJ_EVENT_ID_QUEST_BLACKKNIGHT2)
    removeobject(OBJ_EVENT_ID_QUEST_OWEN)
    setvar(VAR_POKESCAPE_FALADOR_STATE, 3)
    setvar(VAR_POKESCAPE_STORYMODE_PROGRESS, 115)
    clearflag(FLAG_TEMP_F)
    addobject(59)
    fadescreen(FADE_FROM_BLACK)
    delay(32)
    msgbox(format("It seems the H.A.M Grunt dropped something…"))
    closemessage
    applymovement(OBJ_EVENT_ID_PLAYER, movement_dwarvenmines_pickupmap)
    waitmovement(0)
    setflag(FLAG_TEMP_F)
    removeobject(59)
    giveitem(ITEM_CRANDOR_MAP_PIECE3, 1) // MAP PIECE 3
	release
	end
}


movement DWARVENMINES_MOVEMENT_0_1{
    walk_up
    walk_right
    face_up
}
movement DWARVENMINES_MOVEMENT_0_2{
    walk_up
    walk_right * 2
    face_up
}
movement DWARVENMINES_MOVEMENT_0_3{
    walk_up
    face_up
}
movement DWARVENMINES_MOVEMENT_1{
    emote_exclamation_mark
    delay_16 * 2
    face_down
}
movement DWARVENMINES_MOVEMENT_2{
    face_down
}
movement DWARVENMINES_MOVEMENT_3{
    walk_up * 2
    walk_right * 2
    walk_up * 2
    walk_right * 3
    walk_up * 1
    walk_right * 4
    walk_up * 3
}
movement DWARVENMINES_MOVEMENT_4{
    walk_right
    walk_up * 2
    walk_right * 2
    walk_up * 2
    walk_right * 3
    walk_up
    walk_right * 5
    walk_up * 3
}
movement DWARVENMINES_MOVEMENT_5{
    delay_8 * 2
    walk_up
    walk_up * 2
    walk_right * 2
    walk_up * 2
    walk_right * 3
    walk_up
    walk_right * 5
    walk_up * 2
    face_up

}
movement DWARVENMINES_MOVEMENT_6{
    face_right
}
movement DWARVENMINES_MOVEMENT_7{
    face_left
}
movement DWARVENMINES_MOVEMENT_8{
    walk_fast_up * 1
    walk_fast_right * 2
    walk_fast_up * 2
    walk_fast_right * 3
    walk_fast_up * 1
    walk_fast_right * 4
    walk_fast_up * 3
    face_up
    
}
movement DWARVENMINES_MOVEMENT_9{
    face_up
}
movement DWARVENMINES_MOVEMENT_10{
    delay_16
    walk_fast_up * 1
    walk_fast_right * 2
    walk_fast_up * 2
    walk_fast_right * 3
    walk_fast_up * 1
    walk_fast_right * 4
    walk_fast_up * 2
    face_right
}

movement movement_dwarvenmines_pickupmap{
    walk_up * 3
    face_left
}




//BATLES

script BATTLE_DWARVENMINES_SKILLER
{
    trainerbattle_single(TRAINER_DWARVENMINES_SKILLER, format("Please dont approach me.\pI have a terrible bodyguard and I am not afraid to use it!"),format("Hired help is all a skiller can rely on against level 16 scorpions."))
    namebox ("Treet0ps")
    msgbox(format("Hired help is all a skiller can rely on against level 16 scorpions."))
	hidenamebox
    release
	end
}

script BATTLE_DWARVENMINES_MINER
{
    trainerbattle_single(TRAINER_DWARVENMINES_MINER, format("All I can find is coal around here.\pDo you know where there is any runite?"),format("Mining Guild?\lDo I need to pay them?"))
    namebox ("D33pD1v3")
    msgbox(format("Mining Guild?\lDo I need to pay them?"))
	hidenamebox
    release
	end
}

script BATTLE_DWARVENMINES_DWARF_1
{
    trainerbattle_single(TRAINER_DWARVENMINES_DWARF_1, format("Someone took my lunch.\pNow I am angry!"),format("To subside my anger I need to eat.\pBut there is nothing left."))
    namebox ("Sverre")
    msgbox(format("To subside my anger I need to eat.\pBut there is nothing left."))
	hidenamebox
    release
	end
}

script BATTLE_DWARVENMINES_DWARF_2
{
    trainerbattle_single(TRAINER_DWARVENMINES_DWARF_2, format("Stop, you are trespassing on dwarven land!"),format("Theres no respect for racially established borders for you adventurers."))
    namebox ("Steinar")
    msgbox(format("Theres no respect for racially established borders for you adventurers."))
	hidenamebox
    release
	end
}

script BATTLE_DWARVENMINES_DWARF_3
{
    trainerbattle_single(TRAINER_DWARVENMINES_DWARF_3, format("They say deep down under this mountain the rocks move.\pLets fight to that."),format("Maybe the ones who told the story were just drunks?"))
    namebox ("Valdemar")
    msgbox(format("Maybe the ones who told the story were just drunks?"))
	hidenamebox
    release
	end
}

script BATTLE_DWARVENMINES_DWARF_4
{
    trainerbattle_single(TRAINER_DWARVENMINES_DWARF_4, format("I come from the deepest part of this mountain.\pThe things I have seen would shock you!"),format("The rocks… they moves.\pThey moved!"))
    namebox ("Drobak")
    msgbox(format("The rocks… they moves.\pThey moved!"))
	hidenamebox
    release
	end
}

