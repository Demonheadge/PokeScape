mapscripts Melzars_Maze_B1F_MapScripts{
    MAP_SCRIPT_ON_TRANSITION{
		setvar(VAR_EVOLUTION_MAP_STATE, 0) //MAP LOCATIONS: 0-NOTHING, 1-LUMBRIDGE, 16-VARROCK, 3-FALADOR, 4-MEMBERS, 5-OCEAN, 6-WILDERNESS, 7-MORYTANIA, 8-CAVES, 9-ABYSS, 10-OFFWORLD, 11-DESERT, 15-KARAMJA
	
		call(MELZARS_B1F_STATE)
	}
}

const OBJ_EVENT_ID_BABE = 2

script MELZARS_B1F_STATE {
	if ((var(VAR_POKESCAPE_STORYMODE_PROGRESS) == 105) || (var(VAR_POKESCAPE_STORYMODE_PROGRESS) == 106)) {
		addobject(2)
        addobject(3)
		clearflag(FLAG_TEMP_1D)
		setvar(VAR_TEMP_0, 1)
		setvar(VAR_TEMP_5, 1)
		playbgm(MUS_PS_ENCOUNTER_HAM, TRUE)
		
	} 
	elif ((var(VAR_POKESCAPE_STORYMODE_PROGRESS) == 107)) {
		addobject(2)
        addobject(3)
		clearflag(FLAG_TEMP_1D)
		setflag(FLAG_TEMP_1C)
		setvar(VAR_TEMP_0, 0)
		setvar(VAR_TEMP_5, 1)
		setobjectxyperm(3,7,8)
		setvar(VAR_TEMP_E, 1)
		playbgm(MUS_PS_ENCOUNTER_HAM, TRUE)
	} 
	else {
		removeobject(2)
        removeobject(3)
		setflag(FLAG_TEMP_1B)
		setflag(FLAG_TEMP_1A)
		setflag(FLAG_TEMP_1D)
		setvar(VAR_TEMP_E, 1)
	}
    return   
}



script Melzar_Maze_B1F_HAM_Patty {
	msgbox(format("Hey so this script is unfinished, but count this as Melzars Maze section completed.\pNow go to Falador."))
	closemessage
    setvar(VAR_POKESCAPE_STORYMODE_PROGRESS, 110) //AFTER MELZARS MAZE
	end
}

script Melzar_Maze_B1F_GREEN_KEY {
	faceplayer
	if (var(VAR_POKESCAPE_MELZARS_MAZE_DOORS) == 2) {
		msgbox(format("The skeleton stares at you.\pIt appears to be giving you something."))
		closemessage
		setvar(VAR_POKESCAPE_MELZARS_MAZE_DOORS, 3) //B1F KEY OBTAINED
		playfanfare (MUS_OBTAIN_ITEM)
		msgbox(format("{PLAYER} obtained the GREEN KEY."))
		waitfanfare
		closemessage
	}
	msgbox(format("The skeleton smiles at you.\pOr at least you think that's a smile."))
	closemessage
	release
	end
}








script TRIGGER_Melzar_Maze_HAM_EVENT_MELZAR {
	getplayerxy(VAR_TEMP_1, VAR_TEMP_2)
	if (var(VAR_TEMP_2) == 8){
		applymovement(OBJ_EVENT_ID_PLAYER, movement_MelzarsMazeB1F_Player2)
	} elif (var(VAR_TEMP_2) == 9){
		applymovement(OBJ_EVENT_ID_PLAYER, movement_MelzarsMazeB1F_Player1)
	}
	waitmovement(0)
	applymovement(3, Common_Movement_FacePlayer)
    waitmovement(0)
	namebox("H.A.M Grunt")
	msgbox(format("Hey are you the reinforcements?\pThis crazy old guy is holding me back.\pHere, I'll leave him to you."))
	closemessage
	hidenamebox
	//runs off 
	applymovement(OBJ_EVENT_ID_PLAYER, MOVEMENT_WALK_RIGHT)
	applymovement(3, movement_MelzarsMazeB1F_HAM1)
    waitmovement(0)
	setobjectxyperm(3, 7, 8)
	namebox("Melzar")
	msgbox(format("Begone!\pLet me feed my pet rock!!!\pBy the power of custard I will smite you!"))
	closemessage
	hidenamebox
	//battle
	trainerbattle_no_intro(TRAINER_MELZARSMAZE_MELZAR_THE_MAD, format("My cabbages!") )
	namebox("Melzar")
	msgbox(format("I want to drink my tea in peace…"))
	closemessage
	hidenamebox
	//walks away
	applymovement(1, movement_MelzarsMazeB1F_Melzar1)
    waitmovement(0)
	//opens door.
	setvar(VAR_TEMP_E, 1)
	call(MELZARS_LOCKED_DOOR_B1F_3)
	applymovement(1, movement_MelzarsMazeB1F_Melzar2)
    waitmovement(0)
	setflag(FLAG_TEMP_1C)
	removeobject(1)
	setvar(VAR_TEMP_0, 2)
	setvar(VAR_POKESCAPE_STORYMODE_PROGRESS, 107)
	release
	end
}


script Melzar_Maze_B1F_HAM_GRUNT_1 {
	lock
	faceplayer
	namebox("H.A.M Grunt")
	msgbox(format("So glad you dealt with that guy.\pI'm only a lower middle management grunt, I can't deal with actual opposition."))
	closemessage
	hidenamebox
	release
	end
}

script Melzar_Maze_B1F_HAM_GRUNT_2 {
	lock
	faceplayer
	namebox("H.A.M Grunt")
	msgbox(format("Man, I am so exhausted…"))
	closemessage
	hidenamebox
	release
	end
}

script Melzar_Maze_B1F_HAM_GRUNT_3 {
	lock
	faceplayer
	namebox("H.A.M Grunt")
	msgbox(format("I never want to see a cabbage again…"))
	closemessage
	hidenamebox
	release
	end
}

script Melzar_Maze_B1F_HAM_GRUNT_4 {
	lock
	faceplayer
	namebox("H.A.M Grunt")
	msgbox(format("Now there is a Demon to deal with?!\pWhen does this end…"))
	closemessage
	hidenamebox
	release
	end
}

script Melzar_Maze_B1F_MelzarTheMad {
	lock
	faceplayer
	namebox("Melzar")
	msgbox(format("I gave my pet rock some custard, it's feeling better now."))
	closemessage
	hidenamebox
	release
	end
}

script TRIGGER_Melzar_Maze_HAM_EVENT_BABE {
	//movement faceplayer
	getplayerxy(VAR_TEMP_1, VAR_TEMP_2)
	if (var(VAR_TEMP_2) == 16){
		applymovement(OBJ_EVENT_ID_PLAYER, movement_MelzarsMazeB1F_Player3)
	} elif (var(VAR_TEMP_2) == 17){
		applymovement(OBJ_EVENT_ID_PLAYER, MOVEMENT_WALK_LEFT)
	} elif (var(VAR_TEMP_2) == 18){
		applymovement(OBJ_EVENT_ID_PLAYER, movement_MelzarsMazeB1F_Player4)
	}
	waitmovement(0)
	applymovement(OBJ_EVENT_ID_BABE, MOVEMENT_FACE_RIGHT)
    waitmovement(0)
	namebox("Babe")
	msgbox(format("Oh.\pYou're a new face.\pHere, dispose of this Demon for me."))
	closemessage
	hidenamebox
	applymovement(OBJ_EVENT_ID_PLAYER, MOVEMENT_WALK_LEFT)
	applymovement(4, MOVEMENT_WALK_RIGHT)
	applymovement(OBJ_EVENT_ID_BABE, Movement_lighthouse_ham_3)
    waitmovement(0)

	//Battles demon
	namebox ("Lesser Demon")
	playmoncry(SPECIES_DEMON_LESSER_TONGUE_FORM, 0)
	msgbox(format("GWARAKU!"))
	waitmoncry
    hidenamebox
	closemessage
	delay(1)
	setwildbattle (SPECIES_DEMON_LESSER_TONGUE_FORM, 25, ITEM_NONE)
	dowildbattle
	fadescreen(FADE_TO_BLACK)
	setflag(FLAG_TEMP_1B)
	removeobject(4)
	fadescreen(FADE_FROM_BLACK)
	delay(32)
	applymovement(OBJ_EVENT_ID_PLAYER, MOVEMENT_FACE_DOWN)
	waitmovement(0)
	namebox("Babe")
	msgbox(format("Thanks for that, let me just grab that map so we can move on."))
	closemessage
	hidenamebox
	applymovement(OBJ_EVENT_ID_PLAYER, MOVEMENT_FACE_LEFT)
	//picks up map
	applymovement(OBJ_EVENT_ID_BABE, movement_MelzarsMazeB1F_Babe1)
    waitmovement(0)
	setflag(FLAG_TEMP_1A)
	removeobject(9)
	playfanfare (MUS_OBTAIN_ITEM)
	msgbox(format("BABE obtained the MAP PIECE."))
	waitfanfare
	closemessage
	applymovement(OBJ_EVENT_ID_BABE, movement_MelzarsMazeB1F_Babe2)
    waitmovement(0)
	namebox("Babe")
	msgbox(format("Wait, you're not with us are you?\pWhy are you here?"))
	closemessage
	hidenamebox
	//player interact
	applymovement(OBJ_EVENT_ID_PLAYER, MOVEMENT_INTERACT_LEFT)
	waitmovement(0)
	namebox("Babe")
	msgbox(format("You want this map piece?"), MSGBOX_YESNO)
    while (var(VAR_RESULT) != 1) {
        msgbox(format("Then why are you here?\pDo you want this map piece?"), MSGBOX_YESNO)	
	}
	msgbox(format("Well I can't do that.\pSigmund needs this piece to get us to Crandor so we can exterminate all monsters."))
	closemessage
	hidenamebox
	//player interact
	applymovement(OBJ_EVENT_ID_PLAYER, MOVEMENT_INTERACT_LEFT)
	waitmovement(0)
	namebox("Babe")
	msgbox(format("…{PAUSE 32}…{PAUSE 32}…\pYou really want this map piece don't you?"), MSGBOX_YESNO)
    while (var(VAR_RESULT) != 1) {
        msgbox(format("Then why are you still here?\pDo you want this map piece?"), MSGBOX_YESNO)	
	}
	msgbox(format("I guess the only way to settle this is a battle.\pWinner takes the map piece."))
	closemessage
	hidenamebox

	//Battle babe
	trainerbattle_no_intro(TRAINER_MELZARSMAZE_HAM_ADMIN_BABE, format("Oh, this is not good.") )

	namebox("Babe")
	msgbox(format("Aah I lost.\pSigmund is going to be mad at me again.\pOk here you can have this map piece."))
	closemessage
	hidenamebox
	applymovement(6, movement_MelzarsMazeB1F_HAM2)
	applymovement(7, movement_MelzarsMazeB1F_HAM3)
	
	giveitem(ITEM_CRANDOR_MAP_PIECE2, 1)
	closemessage
	waitmovement(0)
	//grunts walk over
	//babe walks to the grunts
	applymovement(OBJ_EVENT_ID_PLAYER, Movement_lighthouse_ham_3)
	applymovement(OBJ_EVENT_ID_BABE, movement_MelzarsMazeB1F_Babe3)
    waitmovement(0)

	namebox("Babe")
	msgbox(format("Ok guys mission failed, we're going to Musa Point and meet up with Patty's crew."))
	closemessage
	hidenamebox
	applymovement(6, MOVEMENT_INTERACT_LEFT)
	namebox("H.A.M Grunt")
	msgbox(format("What about the batallion sent to Falador?"))
	closemessage
	hidenamebox
	waitmovement(0)
	
	namebox("Babe")
	msgbox(format("Right…{PAUSE 32}\nWell I don't really care about that.\pLet them do as they like I guess."))
	closemessage
	hidenamebox
	//faceplayer
	applymovement(OBJ_EVENT_ID_BABE, MOVEMENT_FACE_DOWN)
    waitmovement(0)

	namebox("Babe")
	msgbox(format("Hey you, you may have won this time, but let me just say you shouldn't give chase after us.\pSome of the higher ups like Johannes and Sigmund can be real scary.\pI wouldn't want to oppose them.\pThis one time I accidentally ate Sigmunds san-"))
	closemessage
	hidenamebox
	applymovement(6, MOVEMENT_INTERACT_LEFT)
	namebox("H.A.M Grunt")
	msgbox(format("We should really move on now…"))
	closemessage
	hidenamebox
	waitmovement(0)
	//face grunt
	applymovement(OBJ_EVENT_ID_BABE, Movement_InteractFACERIGHT)
    waitmovement(0)
	namebox("Babe")
	msgbox(format("Oh right!"))
	closemessage
	hidenamebox
	//faceplay
	applymovement(OBJ_EVENT_ID_BABE, MOVEMENT_FACE_DOWN)
    waitmovement(0)
	namebox("Babe")
	msgbox(format("Bye person I don't know the name of."))
	closemessage
	hidenamebox
	fadescreen(FADE_TO_BLACK)
	setvar(VAR_POKESCAPE_STORYMODE_PROGRESS, 110) //AFTER MELZARS MAZE
	removeobject(6)
	removeobject(7)
	removeobject(8)
	removeobject(3)
	removeobject(2)
	setflag(FLAG_TEMP_1D)
	setvar(VAR_TEMP_5, 2)
	playbgm(MUS_PS_CAVE_F2P, FALSE)
	fadescreen(FADE_FROM_BLACK)
	release
	end
}


movement movement_MelzarsMazeB1F_Player1 {
	walk_right *2
	walk_down * 1
	face_right
}
movement movement_MelzarsMazeB1F_Player2 {
	walk_right *2
	walk_down * 2
	face_right
}
movement movement_MelzarsMazeB1F_HAM1 {
	walk_fast_up * 2
	walk_fast_left * 5
	face_right
}

movement movement_MelzarsMazeB1F_Melzar1 {
	walk_slow_up * 2
	walk_slow_left * 3
	face_up
}
movement movement_MelzarsMazeB1F_Melzar2 {
	walk_slow_up * 5
}

movement movement_MelzarsMazeB1F_Player3 {
	walk_left
	walk_down
	face_left
}
movement movement_MelzarsMazeB1F_Player4 {
	walk_left
	walk_up
	face_left
}

movement movement_MelzarsMazeB1F_Babe1 {
	walk_left
	walk_up
	walk_left
}
movement movement_MelzarsMazeB1F_Babe2 {
	walk_right * 1
}
movement movement_MelzarsMazeB1F_HAM2 {
	walk_down * 2
	walk_left * 4
}
movement movement_MelzarsMazeB1F_HAM3 {
	walk_down * 3
	walk_left * 2
}
movement movement_MelzarsMazeB1F_Babe3 {
	walk_right * 1
}