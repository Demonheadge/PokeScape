mapscripts AsgarnianDungeon_B3F_MapScripts 
{
    MAP_SCRIPT_ON_LOAD 
    {
        call(AsgarnianDungeon_B3F_METATILES)
    }
    MAP_SCRIPT_ON_TRANSITION 
    {
        call(AsgarnianDungeon_B3F_STATE)
    }
}

//VAR_DUNGEONEERING_DOOR_AsgarnianDungeon_STATE
//0 - Nothing, no key found or quest start.
//5 - Quest started
//10 - Found Key
//15 - quest complete, door open.


script AsgarnianDungeon_B3F_STATE
{
	switch (var(VAR_DUNGEONEERING_DOOR_AsgarnianDungeon_STATE)){ 
		case 0:
            clearflag(FLAG_TEMP_D)
            addobject(14)
            //setobjectxy(14, 23, 7)
            //setobjectxyperm(14, 23, 7)
        case 5:
            clearflag(FLAG_TEMP_D)
            addobject(14)
            setmetatile(12,13,METATILE_Lumbridge_Swamp_Caves_ICE_ROCKS_SPAKRLE,1) //SPARKLE
            setmetatile(20,6,METATILE_Lumbridge_Swamp_Caves_ICE_ROCKS_SPAKRLE,1) //SPARKLE
            setmetatile(27,7,METATILE_Lumbridge_Swamp_Caves_ICE_ROCKS_SPAKRLE,1) //SPARKLE
            setmetatile(17,21,METATILE_Lumbridge_Swamp_Caves_ICE_ROCKS_SPAKRLE,1) //SPARKLE
            special(DrawWholeMapView) 
        case 10:
            clearflag(FLAG_TEMP_D)
            addobject(14)
        case 15:
            removeobject(14)
            setflag(FLAG_TEMP_D)
    }
    return
}

script AsgarnianDungeon_B3F_METATILES
{
    switch (var(VAR_DUNGEONEERING_DOOR_AsgarnianDungeon_STATE)){ 
		case 0:
            return
        case 5:
            setmetatile(12,13,METATILE_Lumbridge_Swamp_Caves_ICE_ROCKS_SPAKRLE,1) //SPARKLE
            setmetatile(20,6,METATILE_Lumbridge_Swamp_Caves_ICE_ROCKS_SPAKRLE,1) //SPARKLE
            setmetatile(27,7,METATILE_Lumbridge_Swamp_Caves_ICE_ROCKS_SPAKRLE,1) //SPARKLE
            setmetatile(17,21,METATILE_Lumbridge_Swamp_Caves_ICE_ROCKS_SPAKRLE,1) //SPARKLE
            special(DrawWholeMapView) 
        case 10:
            return
        case 15:
            setmetatile(26, 4, 910, 0)
            special(DrawWholeMapView) 
    }
}

script DUNGEONEERING_DOOR_AsgarnianDungeon_B3F_KEYS
{
    if (var(VAR_DUNGEONEERING_DOOR_AsgarnianDungeon_STATE) == 5) {
        setvar(VAR_DUNGEONEERING_DOOR_AsgarnianDungeon_STATE, 10)
        playfanfare (MUS_OBTAIN_ITEM)
        msgbox(format("{PLAYER} found the DUNGEON KEY."))
        waitfanfare
        closemessage
        setmetatile(12,13,METATILE_Lumbridge_Swamp_Caves_ICE_ROCKS,1) //
        setmetatile(20,6,METATILE_Lumbridge_Swamp_Caves_ICE_ROCKS,1) //
        setmetatile(27,7,METATILE_Lumbridge_Swamp_Caves_ICE_ROCKS,1) //
        setmetatile(17,21,METATILE_Lumbridge_Swamp_Caves_ICE_ROCKS,1) //
        special(DrawWholeMapView) 
    }
    end
}

script DUNGEONEERING_DOOR_AsgarnianDungeon_B3F_FAKE_KEYS
{
    if (var(VAR_DUNGEONEERING_DOOR_AsgarnianDungeon_STATE) == 5) {
        msgbox(format("You search the rubble…\pYou find nothing except some very reflective ice."))
        closemessage
    }
}

script DUNGEONEERING_DOOR_AsgarnianDungeon_B3F
{
    if (!(var(VAR_DUNGEONEERING_DOOR_AsgarnianDungeon_STATE) == 15)) //Door is already open.
    { 
        msgbox(format("It appears to be a mysterious door.\pWould you like to try and pry it open?"), MSGBOX_YESNO)
        closemessage
        if (var(VAR_RESULT))
        {
            if (var(VAR_DUNGEONEERING_DOOR_AsgarnianDungeon_STATE) == 10) //Found Key.
            { 
                playse(SE_SWITCH)
                waitse
                delay(30)
                setmetatile(26, 4, 910, 0)
                special(DrawWholeMapView) 
                playse(SE_ICE_BREAK)
                msgbox(format("The door opens."))
                closemessage
                applymovement(14, Movement_InteractFACERIGHT)
                waitmovement(0)
                applymovement(OBJ_EVENT_ID_PLAYER, MOVEMENT_FACE_LEFT)
                waitmovement(0)
                namebox("???")
                msgbox(format("Hey thanks, that was extremely helpful of you."))
                closemessage
                hidenamebox
                applymovement(OBJ_EVENT_ID_PLAYER, Movement_ASGDUNGEON_3)
                applymovement(14, Movement_ASGDUNGEON_4)
                playse(SE_DOOR)
                waitmovement(0)
                removeobject(14)
                setflag(FLAG_TEMP_D)
                setvar(VAR_DUNGEONEERING_DOOR_AsgarnianDungeon_STATE, 15)
            }
            else 
            {
                playse(SE_FAILURE)
                waitse
                delay(30)
                msgbox(format("The door refuses to open."))
                closemessage
            }
        }
    }
    else
    {
        msgbox(format("The door is already open."))
        closemessage
    } 
    release
    end
}

script TRIGGER_DUNGEONEERING_DOOR_AsgarnianDungeon_B3F
{
    lock
    call(AsgarnianDungeon_B3F_STATE)
    applymovement(14, Movement_ASGDUNGEON_1)
	waitmovement(0)
    namebox("???")
	msgbox(format("Hey you…{PAUSE 15} have you seen my keys?\pI dropped them around here somewhere.\pIf you find them can you help me open this door."))
    closemessage
    hidenamebox
    applymovement(14, Movement_ASGDUNGEON_2)
	waitmovement(0)
    setvar(VAR_DUNGEONEERING_DOOR_AsgarnianDungeon_STATE, 5)
    call(AsgarnianDungeon_B3F_STATE)
    release
    end
}

movement Movement_ASGDUNGEON_1
{
    walk_down * 2
    walk_left
}  

movement Movement_ASGDUNGEON_2
{
    walk_right
    walk_up * 2
    face_down
} 

movement Movement_ASGDUNGEON_3
{
    lock_facing_direction
    walk_down
    unlock_facing_direction
} 

movement Movement_ASGDUNGEON_4
{
    walk_right
    walk_up
} 


script DUNGEONEERING_DOOR_AsgarnianDungeon_B3F_NPC
{
    faceplayer
    namebox("???")
    if (var(VAR_DUNGEONEERING_DOOR_AsgarnianDungeon_STATE) == 10) {
        msgbox(format("You found my keys!\pOh, but my poor hands now have frostbite.\pCould you open the door for me?"))
    }
    else {
        msgbox(format("Have you seen my keys?\nI dropped them somewhere around here."))
    }
    closemessage
    hidenamebox
    release
    end
}















