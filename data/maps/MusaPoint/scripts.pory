const OBJ_EVENT_ID_WISE_OLD_MAN = 20
const OBJ_EVENT_ID_QUESTER = 21
const OBJ_EVENT_ID_CUSTOMS_OFFICER = 7

mapscripts MusaPoint_MapScripts {
	MAP_SCRIPT_ON_LOAD 
    {
        call(MUSAPOINT_METATILES)
    }
    MAP_SCRIPT_ON_TRANSITION {
		setflag(FLAG_VISITED_MUSA_POINT)
		call(MUSAPOINT_STATE)
        setvar(VAR_EVOLUTION_MAP_STATE, 15) //MAP LOCATIONS: 0-NOTHING, 1-LUMBRIDGE, 16-VARROCK, 3-FALADOR, 4-MEMBERS, 5-OCEAN, 6-WILDERNESS, 7-MORYTANIA, 8-CAVES, 9-ABYSS, 10-OFFWORLD, 11-DESERT, 15-KARAMJA
    }
	

}

script MUSAPOINT_METATILES {
	if (var(VAR_WISE_OLD_MAN_RIVAL) <= 15) { //Post Karamaja Gym, 
		setmetatile(12, 35, METATILE_Karamja_SAND_BANANA, 0) 
		setmetatile(10, 36, METATILE_Karamja_SAND_BANANA, 0) 
		setmetatile(15, 35, METATILE_Karamja_SAND_BANANA, 0) 
		special(DrawWholeMapView) 
	}

	if (var(VAR_BANANA_BOAT_QUEST) >= 10) { 
		setmetatile(27, 27, METATILE_PokeScapeOutdoors_GENERAL_SAND, 0) 
		setmetatile(28, 27, METATILE_PokeScapeOutdoors_GENERAL_SAND, 0) 
		setmetatile(29, 27, METATILE_PokeScapeOutdoors_GENERAL_SAND, 0) 
		setmetatile(27, 28, METATILE_PokeScapeOutdoors_GENERAL_SAND, 0) 
		setmetatile(28, 28, METATILE_PokeScapeOutdoors_GENERAL_SAND, 0) 
		setmetatile(29, 28, METATILE_PokeScapeOutdoors_GENERAL_SAND, 0) 
		special(DrawWholeMapView) 
	}

	
}

script MUSAPOINT_STATE {
	if (var(VAR_POKESCAPE_GAMEMODE_STATE) == 0) //if STORYMODE
    {
		if (var(VAR_POKESCAPE_STORYMODE_PROGRESS) <= 155) { //Less than, Next storymoment past obtaining the boat. 
			addobject(OBJ_EVENT_ID_QUESTER)
			clearflag(FLAG_TEMP_1D)
			setobjectxyperm(OBJ_EVENT_ID_CUSTOMS_OFFICER, 38, 29)
		}
		else {
			removeobject(OBJ_EVENT_ID_QUESTER)
			setflag(FLAG_TEMP_1D)
		}
	//Wise Old Man
		if (var(VAR_WISE_OLD_MAN_RIVAL) <= 15) { 
			setvar(VAR_TEMP_F, 1)
			addobject(OBJ_EVENT_ID_WISE_OLD_MAN)
			clearflag(FLAG_TEMP_1F)
			addobject(22)
			addobject(23)
			clearflag(FLAG_TEMP_1E)
		}
		else {
			removeobject(OBJ_EVENT_ID_WISE_OLD_MAN)
			setflag(FLAG_TEMP_1F)
			removeobject(22)
			removeobject(23)
			setflag(FLAG_TEMP_1E)
		}
	//BANANA BOAT QUEST
		if (var(VAR_BANANA_BOAT_QUEST) >= 5) { //QUEST START (Could change to check if quest is started)
			clearflag(FLAG_TEMP_1C)
			clearflag(FLAG_TEMP_1B)
			addobject(24)
			addobject(25)
		}
		else {
			setflag(FLAG_TEMP_1C)
			setflag(FLAG_TEMP_1B)
			removeobject(24)
			removeobject(25)
		}
	}
	return
}



script NPC_MUSAPOINT_SAILOR
{
	lock
    namebox ("Officer")
	if (var(VAR_POKESCAPE_STORYMODE_PROGRESS) <= 155) { //Post Karamaja Gym,  //FLIP >
		msgbox(format("I've confiscated your Karamja Rum.\pYou're in for da cage now."))
	}
	else {
		faceplayer
		msgbox(format("Hi, there would you like to board the boat to Port Sarim?"), MSGBOX_YESNO)
		if (var(VAR_RESULT)==1)
		{
			msgbox(format("All aboard!\lWe set sail for Port Sarim!"))
			warp (MAP_PORT_SARIM,8,0,0)
		}
		else
		{
			msgbox(format("Suit yourself."))
		}
	}
	closemessage
    hidenamebox
	release
	end

	
}



script NPC_LegendArts
{
	lock
	faceplayer
    namebox ("LEGEND ARTS")
	msgbox(format("What…{PAUSE 15}{PAUSE 15}…{PAUSE 15}{PAUSE 15}…\nHow did I get here?"))
    waitmessage
    hidenamebox
	release
	end
}



script SIGNPOST_MUSA_POINT
{
	msgbox(format("Musa Point - The best bannanas in all of Karamaja!"))
    closemessage
	end
}

script SIGNPOST_BOAT_FOR_SALE
{
	msgbox(format("Boat for Sale!\pSee Luthas inside for more details."))
    closemessage
	end
}

//ITEMS
script ITEM_MUSA_POINT_1
{
    finditem(ITEM_PURPLE_SWEETS)
    end
}
script ITEM_MUSA_POINT_2
{
    finditem(ITEM_STAT_BEER)
    end
}

//NPCS
script NPC_MUSA_POINT_1
{
	lock
    namebox ("Sad Pirate")
	msgbox(format("Arr…{PAUSE 32}…\n{PAUSE 32}Sailing…{PAUSE 32}…\pOne day…{PAUSE 32}…{PAUSE 32}\nOne…{PAUSE 32} day…{PAUSE 32}…"))
    closemessage
    hidenamebox
	release
	end
}
script NPC_MUSA_POINT_2
{
	lock
	faceplayer
    namebox ("Pirate")
	msgbox(format("30 years ago I buried me treasure here.\pNow I've spent 5 years digging on the beach to find it only to realize that maybe I buried it in Brimhaven?"))
    closemessage
    hidenamebox
	release
	end
}
script NPC_MUSA_POINT_3
{
	lock
	faceplayer
    namebox ("Pirate")
	msgbox(format("The Customs Officers constantly keep us under supervision, apparently they dont want Karamja Rum to reach the mainland.\pIts all a conspiracy to keep the White knights in power I tell you!"))
    closemessage
    hidenamebox
	release
	end
}
script NPC_MUSA_POINT_4
{
	lock
	faceplayer
    namebox ("Hunter")
	msgbox(format("Up north is a fishing spot, you might find yourself a battle there with the local fishers."))
    closemessage
    hidenamebox
	release
	end
}
script NPC_MUSA_POINT_5
{
	lock
	faceplayer
    namebox ("Pirate")
	msgbox(format("I once saw a Polar Bear in this well, but no one believes me.\pThey all just think I was drunk.\pBut I wasn't!\p… Or was I…"))
    closemessage
    hidenamebox
	release
	end
}
script NPC_MUSA_POINT_6
{
	lock
	faceplayer
    namebox ("Woman")
	msgbox(format("The banana farm is the pride and joy of Musa Point.\pThat or the bar is the only thing keeping us a landmark really."))
    closemessage
    hidenamebox
	release
	end
}
script NPC_MUSA_POINT_7
{
	lock
	faceplayer
    namebox ("Man")
	msgbox(format("Welcome to Karamja.\pMake sure to prepare some anti-poisons and some choc-sunday's for your monsters."))
    closemessage
    hidenamebox
	release
	end
}
script NPC_MUSA_POINT_8
{
	lock
	faceplayer
    namebox ("Officer")
	if (var(VAR_POKESCAPE_STORYMODE_PROGRESS) <= 155) { //Post Karamaja Gym, //FLIP >
		msgbox(format("Sorry we've had an incident of someone trying to smuggle some Rum to the mainland.\pOur ships won't be sailing for some time while we check the cargo."))
	}
	else {
		msgbox(format("This ship sails all over the land.\pThat was true until all of the ports were closed off, now we sail all around Musa Point."))
	}
    closemessage
    hidenamebox
	release
	end

	
}
script NPC_MUSA_POINT_9
{
	lock
	faceplayer
    namebox ("Fisherman")
	msgbox(format("Fishing Levels?"))
    closemessage
    hidenamebox
	release
	end
}
script NPC_MUSA_POINT_10
{
	lock
	faceplayer
    namebox ("Firemaker")
	msgbox(format("Fire, anyone?"))
    closemessage
    hidenamebox
	release
	end
}
script NPC_MUSA_POINT_Luthas
{
	lock
	faceplayer
    namebox ("Luthas")
	msgbox(format("Bananas make the world go round.\pWould you like to buy one?"))
    closemessage
    hidenamebox
	release
	end
}
script NPC_MONKEY_BLOCK
{
	lock
	faceplayer
    namebox ("Monkey")
    playmoncry(SPECIES_MONKEY_NORMAL_FORM, 0)
	msgbox(format("Oook, Ook!"))
    waitmoncry
    hidenamebox
	msgbox(format("This monkey is bananas!"))
	release
	end
}



//TRAINERS
script BATTLE_MUSA_POINT_TRAINER_1
{
    trainerbattle_single(TRAINER_MUSA_POINT_TRAINER_1, format("My Fishing level is 62."),format("I can only use quick chat."))
    //namebox ("Cupholder")
    msgbox(format("I can only use quick chat."))
	hidenamebox
    release
	end
}
script BATTLE_MUSA_POINT_TRAINER_2
{
    trainerbattle_single(TRAINER_MUSA_POINT_TRAINER_2, format("Why am I here?\pFight me to find out!"),format("I love fishing but I cannot afford membership.\pThat is why I am here."))
    //namebox ("Cupholder")
    msgbox(format("I love fishing but I cannot afford membership.\pThat is why I am here."))
	hidenamebox
    release
	end
}
script BATTLE_MUSA_POINT_TRAINER_3
{
    trainerbattle_single(TRAINER_MUSA_POINT_TRAINER_3, format("Noob fishing spot tells me I need a higher fishing level to be here.\pNow I am angry!"),format("My fishing level is 14."))
    //namebox ("Cupholder")
    msgbox(format("My fishing level is 14."))
	hidenamebox
    release
	end
}
script BATTLE_MUSA_POINT_TRAINER_4
{
    trainerbattle_single(TRAINER_MUSA_POINT_TRAINER_4, format("Life is like a fishing spot, you never know what you are going to get."),format("Unless you fish for lobsters, you only get lobsters like this."))
    //namebox ("Cupholder")
    msgbox(format("Life is like a fishing spot, you never know what you are going to get.\pUnless you fish for lobsters, you only get lobsters like this."))
	hidenamebox
    release
	end
}


script NPC_MUSA_POINT_SHOP_1
{
	lock
	faceplayer
    namebox ("Hunter")
	msgbox(format("That Tzhaar Gym was something else, I had to stock up on Drunk Dragon's just to make it through the waves."))
    closemessage
    hidenamebox
	release
	end
}

script NEWSLETTER_1 //MUSA POINT
{
	lock
	faceplayer
	msgbox(format("Newsletter Volume 1.\nPenguins, love, music!\pWould you like to read it?"), MSGBOX_YESNO)
	if (var(VAR_RESULT)) {
	msgbox(format("While adventurering you may occasionally hear some unusual music playing far off in the distance.\pYou may think to yourself, what is this?\pWhy, it is none other than penguins!\pEver since Ping & Pong started up their music band, all penguins have joined in on the fun.\pKeep your eyes peeled, follow the source of the music and you will find penguins!\p - Canada Girll"))
	}
    closemessage
	release
	end
}
script NEWSLETTER_2 //RIMMINGTON
{
	lock
	faceplayer
	msgbox(format("Newsletter Volume 2.\nPenguins and how to spot them!\pWould you like to read it?"), MSGBOX_YESNO)
	if (var(VAR_RESULT)) {
	msgbox(format("Penguins are sneaky, they disgusise themselves in plain sight and blend in with their surroundings.\pSome say they have seen them disgusised as barrels or bushes and some say they have even been spotted as toadstools!\pOne farmer in Lumbridge is adamant that they disgusised themselves as one of his sheep in the herd.\pBut could this be true?\p - Canada Girll"))
	}
    closemessage
	release
	end
}
script NEWSLETTER_3 //VARROCK
{
	lock
	faceplayer
	msgbox(format("Newsletter Volume 3.\nCupholders.\pWould you like to read it?"), MSGBOX_YESNO)
	if (var(VAR_RESULT)) {
	msgbox(format("Cupholders. They hold your cups while you do other things.\p - Demonheadge"))
	}
    closemessage
	release
	end
}
script NEWSLETTER_4 //ALKHARID
{
	lock
	faceplayer
	msgbox(format("Newsletter Volume 4.\nMysterious Mounds.\pWould you like to read it?"), MSGBOX_YESNO)
	if (var(VAR_RESULT)) {
	msgbox(format("Shh, the ground.\nIt moves ever so slightly.\pBe careful as Wyrms are buried deep within the sands!\pThey change type depending on their enviorment.\pSome say you can force these changes to happen with magic.\pI shall continue to track them.\nAnd you should too.\p - Strykewyrm Jim."))
	}
    closemessage
	release
	end
}
script NEWSLETTER_5 //FALADOR
{
	lock
	faceplayer
	msgbox(format("Newsletter Volume 5.\nGotta go fast!\pWould you like to read it?"), MSGBOX_YESNO)
	if (var(VAR_RESULT)) {
	msgbox(format("Got sore feet?\n{PAUSE 15}Run energy drained?\pAgility level too low?\n{PAUSE 15}Well be exhausted no more!\pMy Terrorbird can fix that in a jiffy!\nCome see them in Rimmington.\p - Gnome boy Kevin"))
	}
    closemessage
	release
	end
}
script NEWSLETTER_6 //LUMBRIDGE
{
	lock
	faceplayer
	msgbox(format("Newsletter Volume 6.\nA Button.\pWould you like to read it?"), MSGBOX_YESNO)
	if (var(VAR_RESULT)) {
	msgbox(format("Hello, need help?\pWhere do I go next?\pAre you stuck?\pDoes that door not open?\pJust try pressing the A button!\pWhat is the A button?\pTehehe, aren't you silly.\pYou know what the A button is.\pThe A button can even help you find hidden items.\pJust remember, when in doubt.\nJust press the A button.\p - Anubtot."))
	}
    closemessage
	release
	end
}
script NEWSLETTER_7 //EDGEVILLE
{
	lock
	faceplayer
	msgbox(format("Newsletter Volume 7.\nTo be a slayer master.\pWould you like to read it?"), MSGBOX_YESNO)
	if (var(VAR_RESULT)) {
	msgbox(format("To become the best like no one ever was, you must do 4 simple things.\pStep 1, Get your task from a slayer master.\nStep 2, Complete your task.\pStep 3, ???.\nStep 4, Profit.\p - Wannabe Slayer Master, Erskine"))
	}
    closemessage
	release
	end
}
script NEWSLETTER_8 //PORT SARIM
{
	lock
	faceplayer
	msgbox(format("Newsletter Volume 8.\nSparkle, sparkle.\pWould you like to read it?"), MSGBOX_YESNO)
	if (var(VAR_RESULT)) {
	msgbox(format("HIDDEN ITEMS!\pYou want them, don't you?\pThey are hidden everywhere.\pBut how do you find them?\pSolve this and information shall be yours:\pIn the town, 'A KROC RV'\nI am found.\pCrests of royality are where, I dwell.\nSit with me and drink tea, times 3.\pAll will be revealed.\n - ???"))
	}
    closemessage
	release
	end
}


script NEWSLETTER_9
{
	lock
	faceplayer
	msgbox(format("Newsletter Volume 9.\n???.\pWould you like to read it?"), MSGBOX_YESNO)
	if (var(VAR_RESULT)) {
	msgbox(format("?\p - ???"))
	}
    closemessage
	release
	end
}
script NEWSLETTER_13
{
	lock
	faceplayer
	msgbox(format("Newsletter Volume 13.\pThe end is neigh.\pWould you like to read it?"), MSGBOX_YESNO)
	if (var(VAR_RESULT)) {
	msgbox(format("Sadly the time as come to end..\pThis will be the last newsletter in this series that I will be publishing.\pI thank you for reading.\p - Baraek"))
	}
    closemessage
	release
	end
}









////WISE OLD MAN
//Well then, it truly has been awhile.
//You look quite different since we last met. Youve grown quite a lot.
//How about we check just how much you have truly grown.

//*BATTLE - W: You are really getting this old man riled up.
//So you got stranded on Crandor, passed through volcano passage undereath Karamja Volcano, met with the Tzhaar and now you are here?
//You certainly have been busy haven't you.
//You should really take the MvM championship seriously, I think you might just make it to the end.
//Although that might have to be put on hold for a bit as it seems someone was caught smuggling Karmaja Rum.
//All of the boats are in lockdown until the matter is resolved.
//If you don't want to wait around with me, I heard Luthas was selling a boat but I left my wallet at home.


////LUTHAS
//// BANANA-BOAT QUEST
//Hello, I run the banana plantation. How many I help you today?
//Ah, you interested in the boat that is for sale?
//How much? You are very eager aren't you.
//I can sell it to you but not for gold pieces. What I need is an employee.
//You see I've injured my back and now am running behind on the shipment of Bananas.
//If you could go into the plantation and pick 10 bananas for me then you can have this boat. 
//Is that a deal?


script EventScript_BananaTree{
    msgbox(format("There are a bunch of Bananas on this tree!"))
    random(2)
    if (var(VAR_RESULT) == 0) {
        msgbox(format("You picked a Banana!"))
        giveitem(ITEM_BANANA, 1)
    }
	else {
		msgbox(format("You failed to pick a Banana…"))
	}
	closemessage
	end
}

script EventScript_BananaGround{
	random(6)
	switch (var(VAR_RESULT)){ 
		case 0:
			giveitem(ITEM_BANANA, 1)
		case 1:
			msgbox(format("This banana smells."))
		case 2:
			msgbox(format("There are fresher bananas in the trees."))
		case 3:
			msgbox(format("I've heard of potassium but this isn't it."))
		case 4:
			msgbox(format("As you reach for the banana you sense the gaze a monkey in the distance.\pYou decide to leave it."))
		case 5:
			msgbox(format("Ew, looks like someone stepped on this banana."))
	}
	closemessage
	end
}


script QUEST_BOAT_MUSA_POINT_Smuggler
{
	lock
    namebox ("Quester")
	msgbox(format("Awh man, Frank set me up…"))
    closemessage
    hidenamebox
	release
	end
}



//wom
script TRIGGER_WISEOLDMAN_RIVAL_MUSAPOINT {
	lock
    playse(SE_PIN)
    applymovement (OBJ_EVENT_ID_WISE_OLD_MAN, movement_wom_musapoint_noticeplayer)
    waitmovement(0)
    delay(60)
    playbgm (MUS_PS_WISE_OLD_MAN_THEME, 1)
	getplayerxy(VAR_TEMP_A, VAR_TEMP_B)
    if (var(VAR_TEMP_B) == 26){
        applymovement (OBJ_EVENT_ID_WISE_OLD_MAN, movement_wom_musapoint_01)
		waitmovement(0)
		applymovement (OBJ_EVENT_ID_PLAYER, MOVEMENT_FACE_DOWN)
    } 
    elif (var(VAR_TEMP_B) == 27){
        applymovement (OBJ_EVENT_ID_WISE_OLD_MAN, movement_wom_musapoint_02)
    }
	elif (var(VAR_TEMP_B) == 28){
        applymovement (OBJ_EVENT_ID_WISE_OLD_MAN, movement_wom_musapoint_03)
    }
	waitmovement(0)
	namebox("Wise Old Man")
	msgbox(format("Well then, it truly has been awhile.\nYou look quite different since we last met."))
	switch (var(VAR_WISE_OLD_MAN_RIVAL)) { 
		case 10: // Last encounter was port sarim.
			msgbox(format("It was a shame you didn't take me up on the offer to meet within Asganria Dungeon.\pIt cannot be helped I suppose.\pHow about we check just how much you have truly grown."))
		case 15: //Encountered optional within Asganria Dungeon
			msgbox(format("By the way, did you end up meeting with Melzar?\pHow did that go, did he put up a good fight?\pThat man loves his cabbages.\pEnough, chit chat, this old man wants to see how much you've grown since our last enocunter!"))
		}
	hidenamebox
	closemessage
	trainerbattle_no_intro(TRAINER_RIVAL_WISE_OLD_MAN_3, format("You are really getting this old man riled up.")) 
	namebox("Wise Old Man")
	msgbox(format("So you got stranded on Crandor, passed through volcano passage undereath Karamja Volcano, met with the Tzhaar and now you are here?\pYou certainly have been busy haven't you.\pYou should really take the MvM championship seriously, I think you might just make it to the end."))
	hidenamebox
	closemessage
	applymovement (OBJ_EVENT_ID_WISE_OLD_MAN, movement_wom_musapoint_1)
	waitmovement(0)
	namebox("Wise Old Man")
	msgbox(format("Although that might have to be put on hold for a bit as it seems someone was caught smuggling Karmaja Rum.\pAll of the boats are in lockdown until the matter is resolved.\pIf you don't want to wait around with me, I heard Luthas was selling a boat."))
	applymovement (OBJ_EVENT_ID_WISE_OLD_MAN, MOVEMENT_FACE_UP)
	waitmovement(0)
	msgbox(format("He lives in that shack just over there.\pGo give him a visit if your interested."))
	applymovement (OBJ_EVENT_ID_WISE_OLD_MAN, Common_Movement_FacePlayer)
	waitmovement(0)
	msgbox(format("Me?\pI think I might go visit the pub for a drink.\pI truly am looking forward to seeing your MvM championship progress.\pUntil next time {PLAYER}."))
	hidenamebox
	closemessage
	getplayerxy(VAR_TEMP_A, VAR_TEMP_B)
    if (var(VAR_TEMP_B) == 26){
        applymovement (OBJ_EVENT_ID_WISE_OLD_MAN, movement_wom_musapoint_04)
    } 
    elif (var(VAR_TEMP_B) == 27){
        applymovement (OBJ_EVENT_ID_WISE_OLD_MAN, movement_wom_musapoint_05)
    }
	elif (var(VAR_TEMP_B) == 28){
        applymovement (OBJ_EVENT_ID_WISE_OLD_MAN, movement_wom_musapoint_06)
    }
	waitmovement(0)
	opendoor(18,32)
	waitdooranim
	applymovement (OBJ_EVENT_ID_WISE_OLD_MAN, movement_ENTER_DOOR)
	waitmovement(0)
	removeobject(OBJ_EVENT_ID_WISE_OLD_MAN)
	setflag(FLAG_TEMP_1F)
	closedoor(18,32)
	setvar(VAR_WISE_OLD_MAN_RIVAL, 20) //Karmaja event completed.
	setvar(VAR_TEMP_F, 0)
	savebgm(MUS_DUMMY)
	fadedefaultbgm
	release
	end
	
}

movement movement_wom_musapoint_noticeplayer{
    face_player
    emote_exclamation_mark
    delay_16
}
movement movement_wom_musapoint_01{
    walk_left
	walk_up
	walk_left
	walk_up
	walk_left
	face_up
}
movement movement_wom_musapoint_02{
    walk_left
	walk_up
	walk_up
	walk_left
}
movement movement_wom_musapoint_03{
    walk_left
	walk_up
	walk_left
}
movement movement_wom_musapoint_1{
    face_right
    delay_16 * 6
    face_player
}

movement movement_wom_musapoint_04{
    walk_right
	walk_down * 2
	walk_right
	walk_down * 4
	walk_left * 6
	face_up
}
movement movement_wom_musapoint_05{
    walk_down * 2
	walk_right
	walk_down * 4
	walk_left * 6
	face_up
}
movement movement_wom_musapoint_06{
	walk_down
	walk_right
	walk_down * 4
	walk_left * 6
	face_up
}
movement movement_ENTER_DOOR{
	walk_up
	set_invisible
}




script NPC_WISEOLDMAN_RIVAL_MUSAPOINT_PUB {
	lock
	faceplayer
	namebox("Wise Old Man")
	checkitem(ITEM_SURF_TOOL, 1)
	if (var(VAR_RESULT)){
		msgbox(format("Oh, I see you have the brought the boat.\pYou go on ahead and sail back to the mainland, this old man will wait for the matter at the docks to resolve."))
	}
	else {
		msgbox(format("Hello again {PLAYER}.\pDid you end up going to see Luthas about that boat?"))				
	}
	hidenamebox
	closemessage
	release
	end
}




script LUTHAS_QUEST {
	lock
	faceplayer
	namebox("Luthas")

	//change to switch case

	if (var(VAR_BANANA_BOAT_QUEST) == 25) {
		goto(LUTHAS_QUEST_PART_6)
	}

	if (var(VAR_BANANA_BOAT_QUEST) == 20) {
		goto(LUTHAS_QUEST_PART_5)
	}

	if (var(VAR_BANANA_BOAT_QUEST) == 15) {
		goto(LUTHAS_QUEST_PART_4)
	}

	//if the quest has been completed.
	if (var(VAR_BANANA_BOAT_QUEST) == 10) {
		goto(LUTHAS_QUEST_PART_3)
	}

	//CHECK IF THE QUEST HAS BEEN STARTED
	if (var(VAR_BANANA_BOAT_QUEST) == 5) {
		goto(LUTHAS_QUEST_PART_2)
	}

	//CHECK IF QUEST HASNT BEEN STARTED.
	if (var(VAR_BANANA_BOAT_QUEST) == 0) {
		goto(LUTHAS_QUEST_PART_1)
	}

	msgbox(format("Hello, I run the banana plantation. How many I help you today?"))

}



script LUTHAS_QUEST_PART_1 {
	msgbox(format("Hello, I run the banana plantation.\pHow many I help you today?\pAh, you interested in the boat that is for sale?"))
	//player interact up
	checkitem(ITEM_SURF_TOOL, 1)
	if (var(VAR_RESULT)){
		msgbox(format("Oh, I see you already have a boat.\pMaybe you could help me with a little job then."))
		msgbox(format("You see I've injured my back and now am running behind on the shipment of bananas.\pIf you could go into the plantation and pick 10 bananas for me.\pI've got some nicknacks you can have if you help me with this job."))
	}
	else {
		msgbox(format("How much?\pYou are very eager aren't you.\pI can sell it to you but not for gold pieces.\pWhat I need is an employee."))
		msgbox(format("You see I've injured my back and now am running behind on the shipment of bananas.\pIf you could go into the plantation and pick 10 bananas for me then you can have this boat."))
	}
	msgbox(format("Is that a deal?"), MSGBOX_YESNO)
	if (var(VAR_RESULT)) {
		closemessage
		hidenamebox
		startquest (QUEST_BANANA_BOAT)
		setvar(VAR_BANANA_BOAT_QUEST, 5) //Start quest
		namebox("Luthas")
		msgbox(format("Great!\pRemember I need 10 bananas from the plantation.\pBring them back to me when you have them."))
	}
	else {
		msgbox(format("…\pWell if you change your mind you know where I am."))
	}
	hidenamebox
	closemessage
	release
	end
}


script LUTHAS_QUEST_PART_2 {
	checkitem(ITEM_BANANA, 10)
	if (!var(VAR_RESULT)){
		checkitem(ITEM_BANANA, 1)
		if (var(VAR_RESULT)){
			msgbox(format("Great it looks like you have at least 1 Banana.\pBut I need 10 in total.\pIf you could bring them to me in bulk that would be helpful."))
		}
	msgbox(format("Remember I need 10 bananas from the plantation.\pYou can find the plantation next door.\pThere should be some bananas on the trees."))
	closemessage
	hidenamebox
	release
	end
	}
	msgbox(format("Can I have those bananas to fill the order?"), MSGBOX_YESNO)
	if (!var(VAR_RESULT)) {
		msgbox(format("Oh…"))
		closemessage
		hidenamebox
		release
		end
	}
	removeitem(ITEM_BANANA,10)
	addvar(VAR_BANANA_COUNT, 10)
	subquestmenu (QUEST_MENU_COMPLETE_QUEST, QUEST_BANANA_BOAT, 0)
	msgbox(format("Thank you, as promised here is your reward."))
	closemessage
	hidenamebox
	checkitem(ITEM_SURF_TOOL, 1)
	if (!var(VAR_RESULT)){
		giveitem(ITEM_SURF_TOOL,1)
		namebox("Luthas")
		msgbox(format("As an extra thanks for helping me have these."))
		closemessage
		hidenamebox
	}
	giveitem(ITEM_PURPLE_SWEETS,5)
	giveitem(ITEM_PINEAPPLE,1)
	//QUEST COMPPLETE.
	questmenu(QUEST_MENU_COMPLETE_QUEST, QUEST_BANANA_BOAT)
	setvar(VAR_BANANA_BOAT_QUEST, 10) //Quest complete
	closemessage
	hidenamebox
	release
	end

}


script LUTHAS_QUEST_PART_3 {
	msgbox(format("Thank you for helping me earlier.\pIf you bring me another 20 Bananas I can reward you with this neat Scroll."))
	checkitem(ITEM_BANANA, 20)
	if (var(VAR_RESULT)){
		msgbox(format("Can I have those bananas to fill the order?"), MSGBOX_YESNO)
		if (!var(VAR_RESULT)) {
			msgbox(format("What a shame, I'll hang onto this Scroll incase you change your mind."))
			hidenamebox
			closemessage
			release
			end
		}
		removeitem(ITEM_BANANA, 20)
		addvar(VAR_BANANA_COUNT, 20)
		setvar(VAR_BANANA_BOAT_QUEST, 15)
		subquestmenu (QUEST_MENU_COMPLETE_QUEST, QUEST_BANANA_BOAT, 1)
		msgbox(format("My order is filled, thank you!\pHave this as a reward."))
		hidenamebox
		closemessage
		giveitem(ITEM_TM12_MISTY_TERRAIN, 1) //replace with a banana related move.
	}
	hidenamebox
	closemessage
	release
	end
}

script LUTHAS_QUEST_PART_4 {
	msgbox(format("Back again?\pI always have more work if you are interested.\pCould you please get me another 30 bananas.\pIn terms of a reward I'm sure I can find something for you."))
	setvar(VAR_BANANA_BOAT_QUEST, 20)
	hidenamebox
	closemessage
	release
	end
}

script LUTHAS_QUEST_PART_5 {
	msgbox(format("Could you bring me another 30 bananas?"))
	checkitem(ITEM_BANANA, 30)
	if (var(VAR_RESULT)){
		msgbox(format("Can I have those bananas to fill the order?"), MSGBOX_YESNO)
		if (!var(VAR_RESULT)) {
			msgbox(format("What a shame, come see me if you change your mind."))
			hidenamebox
			closemessage
			release
			end
		}
		removeitem(ITEM_BANANA, 30)
		addvar(VAR_BANANA_COUNT, 30)
		msgbox(format("My order is filled, thank you!\pHave this as a reward."))
		hidenamebox
		closemessage
		call(GIVE_RANDOM_ITEM_28)
		if (var(VAR_BANANA_COUNT) >= 120) {
			namebox("Luthas")
			msgbox(format("Hang on a minute.\pI found this the other day I would like you to have it for helping me out so much."))	
			closemessage
			hidenamebox
			//give mon
			msgbox(format("I hope you will keep assisting me in the future."))
			setvar(VAR_BANANA_BOAT_QUEST, 25)
			subquestmenu (QUEST_MENU_COMPLETE_QUEST, QUEST_BANANA_BOAT, 3)
		}
	}
	hidenamebox
	closemessage
	release
	end
}

script LUTHAS_QUEST_PART_6 {
	msgbox(format("Could you bring me another 30 bananas?"))
	checkitem(ITEM_BANANA, 30)
	if (var(VAR_RESULT)){
		msgbox(format("Can I have those bananas to fill the order?"), MSGBOX_YESNO)
		if (!var(VAR_RESULT)) {
			msgbox(format("What a shame, come see me if you change your mind."))
			hidenamebox
			closemessage
			release
			end
		}
		removeitem(ITEM_BANANA, 30)
		addvar(VAR_BANANA_COUNT, 30)
		msgbox(format("My order is filled, thank you!\pHave this as a reward."))
		hidenamebox
		closemessage
		call(GIVE_RANDOM_ITEM_28)
		if (var(VAR_BANANA_COUNT) >= 1000) {
			namebox("Luthas")
			msgbox(format("Hang on a minute.\pI found this the other day I would like you to have it for helping me out so much."))	
			closemessage
			hidenamebox
			//give mon
			msgbox(format("I hope you will keep assisting me in the future."))
			//setvar(VAR_BANANA_BOAT_QUEST, 25)
		}
	}
	hidenamebox
	closemessage
	release
	end
}

script SIGN_BANANA_COUNT {
	buffernumberstring(0, VAR_BANANA_COUNT)
    msgbox(format("{PLAYER}'s Banana Count: {STR_VAR_1}."))
	closemessage
	end
}



script TOGGLE_INVERSE_BATTLE {
	setflag(FLAG_TOGGLE_INVERSE_BATTLE)
    msgbox(format("Inverse battle - ENABLED"))
	closemessage
	end
}

script BATTLE_scorp {
	setwildbattle (SPECIES_PET_ROCK, 50, ITEM_NONE)
	dowildbattle
	end
}
