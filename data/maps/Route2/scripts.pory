mapscripts Route2_MapScripts{
    MAP_SCRIPT_ON_LOAD {
        if (var(VAR_POKESCAPE_GAMEMODE_STATE) == 0) //if STORY
    	{
            goto(QUEST_STATE_WizardsTower_ROUTE2)
    	}
    }
    MAP_SCRIPT_ON_TRANSITION {
        setvar(VAR_EVOLUTION_MAP_STATE, 1) //MAP LOCATIONS: 0-NOTHING, 1-LUMBRIDGE, 16-VARROCK, 3-FALADOR, 4-MEMBERS, 5-OCEAN, 6-WILDERNESS, 7-MORYTANIA, 8-CAVES, 9-ABYSS, 10-OFFWORLD, 11-DESERT, 15-KARAMJA
        if (var(VAR_POKESCAPE_GAMEMODE_STATE) == 0) //if STORY
    	{
            call(STORYMODE_STATE_ROUTE2)
    	}
		else
		{
			call(OPENWORLD_STATE_ROUTE2)
		}
    }
}

script STORYMODE_STATE_ROUTE2 {
    if (!(var(VAR_WISE_OLD_MAN_RIVAL) == 0)) {
        removeobject(14)
        setflag(FLAG_TEMP_1F) //Wise old man
        setvar(VAR_WISE_OLD_MAN_RIVAL, 1)
    }
}


script OPENWORLD_STATE_ROUTE2 {
        setflag(FLAG_TEMP_1)
        setflag(FLAG_TEMP_2) //HIDE MUGGER
        setvar(VAR_TEMP_2, 1) //HIDE MUGGER TRIGGER
        removeobject(1)
        removeobject(2)
        removeobject(3)
        removeobject(14)
        setflag(FLAG_TEMP_1F) //Wise old man
		return
}



script QUEST_STATE_WizardsTower_ROUTE2{
        switch (var(VAR_POKESCAPE_WIZARDS_TOWER_STATE)){ 
            case 0: 
                setflag(FLAG_TEMP_1)
                setflag(FLAG_TEMP_2) //HIDE MUGGER
                setvar(VAR_TEMP_2, 1) //HIDE MUGGER TRIGGER
                removeobject(1)
                removeobject(2)
                removeobject(3)
                release
                end
            case 1: 
            case 2: 
                //HIDE NPCS
                setflag(FLAG_TEMP_1)
                removeobject(2)
                removeobject(3)
                removeobject(14)
                setflag(FLAG_TEMP_1F) //Wise old man
                release
                end
            case 3: 
                //SHOW NPCs EVENT
                clearflag(FLAG_TEMP_1)
                addobject(2)
                addobject(3)
                removeobject(14)
                setflag(FLAG_TEMP_1F) //Wise old man
                release
                end
            case 4: 
            case 5:
            case 6: 
            case 7: 
            case 8: 
            case 9: 
                //HIDE NPCs
                setflag(FLAG_TEMP_1)
                removeobject(2)
                removeobject(3)
                removeobject(14)
                setflag(FLAG_TEMP_1F) //Wise old man
                release
                end
            case 10: 
		    case 11: 
                setflag(FLAG_TEMP_1)
                setflag(FLAG_TEMP_2) //HIDE MUGGER
                setvar(VAR_TEMP_2, 1) //HIDE MUGGER TRIGGER
                removeobject(1)
                removeobject(2)
                removeobject(3)
                removeobject(14)
                setflag(FLAG_TEMP_1F) //Wise old man
                release
                end
            }
    }

















//-----------------------------------------------------------

script HAM_EVENT_WizardsTower_4{
    lockall
    namebox("Ariane")
    msgbox(format("I wonder if he went this way?"))
    closemessage
    hidenamebox
    release
    end
}



script HAM_EVENT_Route2_MAIN_1{
    lockall
    playbgm (MUS_PS_ENCOUNTER_HAM, 1)
    applymovement(2, HAM_Movement5_1)
    namebox("H.A.M. member")
    msgbox(format("Move!\nGet out of my way!"))
    hidenamebox
    closemessage
    waitmovement(0)
    applymovement(1, HAM_Movement5_7)
    namebox("Highwayman")
    msgbox(format("Stand and deliver!"))
    hidenamebox
    closemessage
    waitmovement(0)
    playse(SE_WALL_HIT)
    applymovement(1, HAM_Movement5_2)
    applymovement(2, HAM_Movement5_3)
    waitmovement(0)
    namebox("H.A.M. member")
    msgbox(format("Ayyyyeeeeek!"))
    hidenamebox
    closemessage
    applymovement(2, HAM_Movement5_4)
    delay(16)
    applymovement(1, HAM_Movement5_8)
    playse(SE_PIN)
    applymovement(3, HAM_Movement5_5)
    waitmovement(0)
    return
}

script HAM_EVENT_Route2_MAIN_2{
    removeobject(2)
    removeobject(3)
    setflag(FLAG_TEMP_1)
    setvar(VAR_POKESCAPE_WIZARDS_TOWER_STATE, 4)
    savebgm(MUS_DUMMY)
	fadedefaultbgm
    release
    end
}


script HAM_EVENT_Route2_TRIGGER_1{
    call(HAM_EVENT_Route2_MAIN_1)
    namebox("Ariane")
    msgbox(format("There you are!"))
    hidenamebox
    closemessage
    applymovement(3, HAM_Movement5_6)
    waitmovement(0)
    goto(HAM_EVENT_Route2_MAIN_2)
}

script HAM_EVENT_Route2_TRIGGER_2{
    call(HAM_EVENT_Route2_MAIN_1)
    namebox("Ariane")
    msgbox(format("There you are!"))
    hidenamebox
    closemessage
    applymovement(3, HAM_Movement5_9)
    waitmovement(0)
    goto(HAM_EVENT_Route2_MAIN_2)
}
script HAM_EVENT_Route2_TRIGGER_3{
    applymovement(3, MOVEMENT_FACE_DOWN)
    applymovement (OBJ_EVENT_ID_PLAYER, MOVEMENT_FACE_UP)
    waitmovement(0)
    namebox("Ariane")
    msgbox(format("Have you seen a man in pink?\pI don't know which way he went…"))
    hidenamebox
    closemessage
    applymovement(3, MOVEMENT_FACE_LEFT)
    applymovement (OBJ_EVENT_ID_PLAYER, movement_WALKRIGHT)
    waitmovement(0)
}
movement movement_WALKRIGHT{
    walk_right
}

movement HAM_Movement5_1{
    walk_in_place_fast_right * 3
}

movement HAM_Movement5_2{
    walk_fast_left
}

movement HAM_Movement5_3{
    slide_left
    jump_special_left
    face_right
}

movement HAM_Movement5_4{
    walk_faster_left * 1
    walk_faster_up * 7
}

movement HAM_Movement5_5{
    emote_exclamation_mark
    face_right
}

movement HAM_Movement5_6{
    walk_fast_right * 2
    walk_fast_up * 10
}

movement HAM_Movement5_7{
    walk_in_place_fast_left * 3
}

movement HAM_Movement5_8{
    walk_slow_right * 1
    face_left
}
    
movement HAM_Movement5_9{
    walk_fast_right * 1
    walk_fast_up * 10
}

//-----------------------------------------------------------


script BLOCK_ROUTE2_Highwayman
{
        lock
        namebox ("Highwayman")
		msgbox(format("Stand and deliver!"))
		closemessage
        hidenamebox
        playse(SE_WALL_HIT)
        applymovement(1, HAM_Movement5_2)
        applymovement(OBJ_EVENT_ID_PLAYER, HAM_Movement5_3)
        waitmovement(0)
        applymovement(1, HAM_Movement5_8)
        waitmovement(0)
        release
        end
}


script NPC_ROUTE2_Wizard
{
    faceplayer
    namebox ("Cordelia")
    msgbox(format("They say that there are hidden floors within the Wizards' Tower.\pI've never found them though."))
    hidenamebox
    release
    end
}


script BATTLE_ROUTE2_Trainer_1
{
    trainerbattle_single(TRAINER_ROUTE2_TRAINER_1, format("Please no!\pI'm so close to the Bank I don't want to be sent back to Lumbridge!"),format("Nooo!\pBack to Lumbridge I go!"))
    //namebox ("H0w2Typ3")
    msgbox(format("If the swamp wasn't hard enough, there was also a forest to pass through!?\pThis is too much for a skiller…"))
    hidenamebox
    release
    end
}

script BATTLE_ROUTE2_Trainer_2
{
    trainerbattle_single(TRAINER_ROUTE2_TRAINER_2, format("Passing this bridge gets you to the Wizards' Tower."),format("Wizards use runes to conjure magic."))
    //namebox ("Tam McGrubor")
    msgbox(format("Runes are marvellous.\pUsing them on monsters might make them evolve!"))
    hidenamebox
    release
    end
}

script BATTLE_ROUTE2_Trainer_3
{
    trainerbattle_single(TRAINER_ROUTE2_TRAINER_3, format("A game is not a good game without a fishing minigame."),format("Now you too can enjoy the joy of fishing."))
    //namebox ("Cakebaws")
    msgbox(format("Now you too can enjoy the joy of fishing."))
    hidenamebox
    release
    end
}

script BATTLE_ROUTE2_Trainer_4
{
    trainerbattle_single(TRAINER_ROUTE2_TRAINER_4, format("Noobs fear me, fish fear me, pkers turns their eyes away from me as I walk."),format("No fish dare make a sound in my presence.\pI walk alone on this barren earth."))
    //namebox ("SlimyTem")
    msgbox(format("No fish dare make a sound in my presence.\pI walk alone on this barren earth."))
    hidenamebox
    release
    end
}

script BATTLE_ROUTE2_Trainer_5
{
    trainerbattle_single(TRAINER_ROUTE2_TRAINER_5, format("You don't look very strong, this battle will be over in an instant!"),format("Impossible!\pI was sure you were weak…\pWait, does this mean I'm weaker than you?!"))
    namebox ("Dark Wizard")
    msgbox(format("Ugh… how!?\pWas my foresight wrong…?\pNo, it can't be…"))
    hidenamebox
    release
    end
}

script BATTLE_ROUTE2_Trainer_6
{
    trainerbattle_single(TRAINER_ROUTE2_TRAINER_6, format("Let's just get this over with…"),format("What a surprise, I lost.\pWho would have guessed…"))
    namebox ("Dark Wizard")
    msgbox(format("I suppose it's not all bad.\pAt least you must feel better about yourself… right?"))
    hidenamebox
    release
    end
}

script BATTLE_ROUTE2_Trainer_7
{
    trainerbattle_single(TRAINER_ROUTE2_TRAINER_7, format("…"),format("…"))
    //namebox ("Bot")
    msgbox(format("…"))
    hidenamebox
    closemessage
    release
    end
}



script NPC_ROUTE2_BLACK_KNIGHT{
    lock
    faceplayer
    namebox("Black Knight")
    msgbox(format("For the Kinshra!"))
    closemessage
    hidenamebox
    release
    end
}



script TRIGGER_WISEOLDMAN_RIVAL_1{
    lock
    playse(SE_PIN)
    applymovement (14, movement_wom_route2_0)
    waitmovement(0)
    delay(60)
    playbgm (MUS_PS_WISE_OLD_MAN_THEME, 1)
    getplayerxy(VAR_TEMP_A, VAR_TEMP_B)
    if (var(VAR_TEMP_B) == 14){
        applymovement (14, movement_wom_route2_1)
    } 
    elif (var(VAR_TEMP_B) == 15){
        applymovement (14, movement_wom_route2_2)
    }
    waitmovement(0)
    namebox("???")
    msgbox(format("My, a new face it seems.\pYou beat the Duke?\nImpressive.\pHow about you and me do a little battle?\pNow don't be shy, surely you can beat an old man."))
    hidenamebox
    closemessage
    trainerbattle_no_intro(TRAINER_RIVAL_WISE_OLD_MAN_1, format("Seems like you might just have what it takes.") )
    namebox("???")
    msgbox(format("Impressive.\pMight I ask your name?"))
    hidenamebox
    closemessage
    applymovement(OBJ_EVENT_ID_PLAYER, MOVEMENT_INTERACT_LEFT)
    waitmovement(0)
    namebox("???")
    msgbox(format("Nice to meet you {PLAYER}."))
    namebox("Wise Old Man")
    msgbox(format("I'm Dionysius but everyone just calls me the Wise old man.\pI used to be one of the biggest adventurers in the world, but now im just an old man looking back on his glory days.\pMaybe you will become something of a legend yourself?\pI hope to see you around on your adventures, try not to get lost in the Wizards' Tower.\pUntil next time."))
    hidenamebox
    closemessage
    savebgm(MUS_DUMMY)
	fadedefaultbgm
    applymovement (14, movement_wom_route2_3)
    waitmovement(0)
    removeobject(14)
    setflag(FLAG_TEMP_1F) //Wise old man
    setvar(VAR_WISE_OLD_MAN_RIVAL, 1)
    release
    end
}
movement movement_wom_route2_0{
    face_right
    emote_exclamation_mark
    delay_16
}
movement movement_wom_route2_1{
    walk_right * 4
}
movement movement_wom_route2_2{
    walk_down
    walk_right * 4
}
movement movement_wom_route2_3{
    walk_left * 4
    walk_up * 8
}



script nowhiteout_test_1 {
    cleartrainerflag(TRAINER_RIVAL_WISE_OLD_MAN_1)
    trainerbattle_no_intro_no_whiteout (TRAINER_RIVAL_WISE_OLD_MAN_1, "I lost, you win.")
    specialvar (VAR_RESULT, GetBattleOutcome)
    if (var(VAR_RESULT) == B_OUTCOME_LOST) {
        special (HealPlayerParty)
        msgbox ("You lost! Suck it!")
    }
    else {
        msgbox ("You defeated me.")
    }
    closemessage
    end
}

script nowhiteout_test_2 {
    specialvar (VAR_RESULT, GetBattleOutcome)
	compare (VAR_RESULT, B_OUTCOME_WON)
	goto_if_eq (nowhiteout_test_3)
    special (HealPlayerParty)
	msgbox ("You lost. Oh no.")
    closemessage
    end
}

script nowhiteout_test_3 {
    msgbox ("You won.")
    closemessage
    end
}

//USEFUL FOR TRAINER SIGHTS.
//trainerbattle_no_whiteout_continue_script (TRAINER_RIVAL_WISE_OLD_MAN_1, "INTRO", "YOU WIN", nowhiteout_test_2)