const ODDENSTEIN_ID = 1

mapscripts Draynor_Manor_3F_MapScripts {

	MAP_SCRIPT_ON_TRANSITION {
        setvar(VAR_EVOLUTION_MAP_STATE, 1) //MAP LOCATIONS: 0-NOTHING, 1-LUMBRIDGE, 16-VARROCK, 3-FALADOR, 4-MEMBERS, 5-OCEAN, 6-WILDERNESS, 7-MORYTANIA, 8-CAVES, 9-ABYSS, 10-OFFWORLD, 11-DESERT,
		call(DRAYNOR_MANOR_3F_STATE)
	}


}


script DRAYNOR_MANOR_3F_STATE {
	removeobject(6)
	removeobject(7)
	setflag(FLAG_TEMP_18)
	setflag(FLAG_TEMP_19)
	if (var(VAR_POKESCAPE_ERNEST_QUEST_STATE) == 4) {
		addobject(6)
		clearflag(FLAG_TEMP_18)
		setflag(FLAG_TEMP_1D)
		setflag(FLAG_TEMP_1E)
		setflag(FLAG_TEMP_1F)
		setflag(FLAG_TEMP_1B)
		removeobject(2)
		removeobject(3)
		removeobject(4)
		removeobject(5)
	}
	elif (var(VAR_POKESCAPE_ERNEST_QUEST_STATE) == 6) {
		subquestmenu(QUEST_MENU_CHECK_COMPLETE, QUEST_ERNEST_THE_CHICKEN, 3)
		if (var(VAR_RESULT) == FALSE) {
			removeobject(5)
			setflag(FLAG_TEMP_1B)
			setobjectxyperm(6, 8, 17)
			addobject(6)
			clearflag(FLAG_TEMP_18)
		}
	}
}


			





script NPC_DraynorManor_Chicken_1{
    lock
	faceplayer
	namebox("Chicken")
	playmoncry(SPECIES_ERNEST, 0)
    msgbox(format("BWawkrk!"))
	waitmoncry
    hidenamebox
	closemessage
    namebox("Oddenstein")
    msgbox(format("I call that an Ernest Chicken!\pI named it this breed after its father.\pBasically I turned this guy called Ernest into a chicken, he laid some eggs and these chickens are what hatched from them."))
	hidenamebox
	closemessage
    release
	end
}

script NPC_DraynorManor_OddenstienLog{
    msgbox(format("It is Professor Oddenstien's Log book.\pWant to read it?"), MSGBOX_YESNO)
		if (var(VAR_RESULT)){
			msgbox(format("Log 1: I met a man named Ernest.\pThis was perfect, before he could say a word I told him to be my assistant for my newest experiment.\pAnd it was a success I could now transform humans into chickens!\pWith this I will never have a shortage of eggs for breakfast."))
			msgbox(format("Log 2: Ernest seemed like he wanted to go outside so I opened a window.\pI wonder where he will go."))
			msgbox(format("Log 3: An adventurer brought Ernest back to me and told me his fiancee wanted him turned back into a human.\pDoes this mean I will no longer have eggs for breakfast?"))
			msgbox(format("Log 4: With Ernest gone only his eggs remain.\pI probably have enough supply for a week but I think I will need to find another assistant soon otherwise I will run out of eggs."))
			msgbox(format("Log 5: Nooo!\pThis is so unfortunate, the eggs have hatched!\pNow I must eat them before they hatch so I can have my breakfast."))
			msgbox(format("Log 6: These are some weird-looking hybrid chickens.\pI think I will name them Ernest Chickens after thier father."))
			msgbox(format("Log 7: I wonder what will happen if I turn these Ernest Chickens into humans…"))
			msgbox(format("End of log."))
		}
	closemessage
    release
	end
}

script NPC_DraynorManor_Chicken_2{
    lock
	faceplayer
	namebox("Chicken")
	playmoncry(SPECIES_ERNEST, 0)
    msgbox(format("BWawkrk!"))
	waitmoncry
    hidenamebox
	closemessage
	if (var(VAR_POKESCAPE_ERNEST_QUEST_STATE) == 2) {
		msgbox(format("You pick up the weird-looking chicken."))
		removeobject(VAR_LAST_TALKED)
		closemessage
		setvar(VAR_POKESCAPE_ERNEST_QUEST_STATE, 3)
	}
    release
	end
}


script ITEM_DRAYNORMANOR_FISHFOOD
{
    finditem(ITEM_FISH_FOOD)
    closemessage
    end
}

script ITEM_DRAYNORMANOR_OILCAN
{
    finditem(ITEM_OIL_CAN)
    closemessage
	subquestmenu (QUEST_MENU_COMPLETE_QUEST, QUEST_ERNEST_THE_CHICKEN, 0)
    end
}

script ITEM_DRAYNORMANOR_RUBBERTUBE
{
    finditem(ITEM_RUBBER_TUBE)
    closemessage
	subquestmenu (QUEST_MENU_COMPLETE_QUEST, QUEST_ERNEST_THE_CHICKEN, 1)
    end
}


script SCRIPT_DraynorManor_Poison {
    msgbox(format("You search the shelves…{PAUSE 15} …{PAUSE 15}\pYou find a bunch of products labeled with a skull and bones."))
    checkitem (ITEM_FISH_FOOD,1)
	if (var(VAR_RESULT)){
        msgbox(format("Would you like to mix it them with the Fish Food?"), MSGBOX_YESNO)
		if (var(VAR_RESULT)){
			msgbox(format("You mix the poison in with the Fish Food."))
            removeitem(ITEM_FISH_FOOD)
            giveitem(ITEM_FISH_FOOD_POISONED)
            closemessage
			call(STORYMODE_STATE_DRAYNORMANOR_1F)
			end
		}
		else {
            closemessage
			end
		}
    }
    closemessage
    end
}


script SCRIPT_DraynorManor_Fountain {
    
	
	if (var(VAR_POKESCAPE_ERNEST_QUEST_STATE) == 1) {

		checkitem (ITEM_PRESSURE_GAUGE,1)
		if (var(VAR_RESULT)){
			msgbox(format("There appears to be a lot of dead piranha in there.\p…{PAUSE 32}Oops."))
			hidenamebox
			closemessage
			release
			end
		}
		else {
			msgbox(format("You spot the Pressure Gauge among a school of piranhas."))
		}
		
	}
	else {
		msgbox(format("There appears to be a lot of piranha in there."))
	}
	closemessage
	checkitem (ITEM_FISH_FOOD, 1)
	if (var(VAR_RESULT)){
		msgbox(format("Would you like to use FISH FOOD?"), MSGBOX_YESNO)
		if (var(VAR_RESULT)){
			msgbox(format("Seeing the piranhas in the fountain you throw the fish food into the fountain."))
			msgbox(format("…{PAUSE 32}… They eat the food within seconds.\pMaybe something from the kitchen could help slow the piranhas down long enough to grab the pressure gauge."))
		}
		closemessage
		release
		end
	}
    checkitem (ITEM_FISH_FOOD_POISONED, 1)
	if (var(VAR_RESULT)){
        msgbox(format("Would you like to use FISH FOOD P++?"), MSGBOX_YESNO)
		if (var(VAR_RESULT)){
			msgbox(format("Seeing the piranhas in the fountain you throw the fish food into the fountain."))
            removeitem(ITEM_FISH_FOOD_POISONED)
            finditem(ITEM_PRESSURE_GAUGE)
            closemessage
			subquestmenu (QUEST_MENU_COMPLETE_QUEST, QUEST_ERNEST_THE_CHICKEN, 2)
			end
		}
		else {
            closemessage
			end
		}
    }
    end
}


script SCRIPT_DraynorManor_Gravestone {
    msgbox(format("It is a gravestone.\pIt reads 'here lies Polly'."))
	closemessage
	if (flag(FLAG_RECIEVED_EX_EX_PARROT) == FALSE) {
		//checkitem (ITEM_TELEPORT_TOOL,1) //SPADE
		//if (var(VAR_RESULT)){
		msgbox(format("Would you like to dig up Polly?"), MSGBOX_YESNO)
		if (var(VAR_RESULT) == TRUE){
			checkitem (ITEM_MAGICAL_CAGE,1)
			if (var(VAR_RESULT)) {
					msgbox(format("You dig up the corpse of an ex-parrot.\pYou place the body in the cage.\p…{PAUSE 15} …{PAUSE 15} The cage begins to glow."))
					removeitem(ITEM_MAGICAL_CAGE)
					msgbox(format("The ex-parrot comes to life!"))
					call(EventScript_DraynorManor_ExExParrot)
					closemessage
					end
			}
			else {
				msgbox(format("You dig up the corpse of an ex-parrot.\pBut have nowhere to put, so you bury it again.\pMaybe if you had a cage…"))
				closemessage
				end
			}
		}
		else {
			msgbox(format("You decide it is best to let Poly rest in peace."))
			closemessage
			end
		}
	}
	else {
		msgbox(format("Looks like someone looted this grave.\pHow could they…"))
		closemessage
	}
    end
}

script SCRIPT_DraynorManor_FindCage {
	msgbox(format("You search through the shelves…"))
	if (flag(FLAG_RECIEVED_EX_EX_PARROT) == FALSE) {
		checkitem (ITEM_MAGICAL_CAGE,1)
		if (!var(VAR_RESULT)){
			closemessage
			giveitem(ITEM_MAGICAL_CAGE, 1)
			closemessage
			end
		}
	}
	msgbox(format("…{PAUSE 15}…{PAUSE 15} and find nothing!"))
	closemessage
	end
}


script EventScript_DraynorManor_ExExParrot {
    setvar(VAR_TEMP_1, SPECIES_EXEXPARROT)
    givemon (SPECIES_EXEXPARROT,5, ITEM_NONE)
    compare(VAR_RESULT, 0)
	goto_if_eq(EventScript_DraynorManor_SendParty_ExExParrot)
    compare(VAR_RESULT, 1)
	goto_if_eq(EventScript_DraynorManor_SendPC_ExExParrot)
    goto(Common_EventScript_NoMoreRoomForPokemon)
    end
}

script EventScript_DraynorManor_SendParty_ExExParrot{
	call(EventScript_DraynorManor_ReceivedFanfare_ExExParrot)
	msgbox(format("Would you like to give your monster a nickname?"), MSGBOX_YESNO)
    compare(VAR_RESULT, NO)
	goto_if_eq(EventScript_DraynorManor_Received_ExExParrot)
	call(Common_EventScript_GetGiftMonPartySlot)
	call(Common_EventScript_NameReceivedPartyMon)
	goto(EventScript_DraynorManor_Received_ExExParrot)
	end
}

script EventScript_DraynorManor_SendPC_ExExParrot {
	call(EventScript_DraynorManor_ReceivedFanfare_ExExParrot)
	msgbox(format("Would you like to give your monster a nickname?"), MSGBOX_YESNO)
    compare(VAR_RESULT, NO)
	goto_if_eq(EventScript_DraynorManor_TransferredToPC_ExExParrot)
	call (Common_EventScript_NameReceivedBoxMon)
	goto (EventScript_DraynorManor_TransferredToPC_ExExParrot)
	end
}

script EventScript_DraynorManor_TransferredToPC_ExExParrot{
	call (Common_EventScript_TransferredToPC)
	goto (EventScript_DraynorManor_Received_ExExParrot)
	end
}

script EventScript_DraynorManor_ReceivedFanfare_ExExParrot{
	bufferspeciesname(1, SPECIES_EXEXPARROT)
	//removeobject LOCALID_BELDUM_BALL
	playfanfare (MUS_OBTAIN_ITEM)
	msgbox(format("{PLAYER} obtained a {STR_VAR_2}."))
	waitmessage
	waitfanfare
	bufferspeciesname(0, SPECIES_EXEXPARROT)
	return
}

script EventScript_DraynorManor_Received_ExExParrot{
	subquestmenu (QUEST_MENU_COMPLETE_QUEST, QUEST_ERNEST_THE_CHICKEN, 4)
	setflag(FLAG_RECIEVED_EX_EX_PARROT)
    //msgbox(format("EVENT COMPLETE."))
	releaseall
	return
}

//Hello there, you seem to be an adventurer.
//You see, recently a human named Ernest was turned into a chicken. We managed to transform him back but not before he laid a bunch of eggs. 
//Originally I thought nothing of it and was planning to have the eggs for breakfast tomorrow but they ended up hatching and now there are chicken all over the place.
//Now I've come up with a great idea involving the increasing number of chickens. Could you help me with an experiment? 


script NPC_DraynorManor_Oddenstein{
	lock
	faceplayer
	switch (var(VAR_POKESCAPE_ERNEST_QUEST_STATE)){ 
		case 0: 
			namebox ("Oddenstein")
			msgbox(format("Hello there, you seem to be an adventurer.\pYou see, recently a human named Ernest was turned into a chicken.\pWe managed to transform him back but not before he laid a bunch of eggs.\pOriginally I thought nothing of it and was planning to have the eggs for breakfast tomorrow but they ended up hatching and now there are chicken all over the place."))
			msgbox(format("I've come up with a great idea involving the increasing number of chickens.\pBut I need an assistant, can you help me with my experiment?"), MSGBOX_YESNO)
			if (var(VAR_RESULT)){
				hidenamebox
				startquest (QUEST_ERNEST_THE_CHICKEN)
				namebox ("Oddenstein")
				msgbox(format("I need a few items for my experiment to succeed can you go get me them?\pI need a Oil Can, Pressure Gauge and Rubber Tube."))
                msgbox(format("The Oil Can should be with Ava.\pShe is in the room next to the living room.\pShe loves puzzles so I'd imagine she would have hidden the door to her room behind a bookcase or something."))
                msgbox(format("I believe the last place I saw the Pressure Gauge being used was outside in the fountain."))
                msgbox(format("As I recall McBoneFace borrowed the Rubber Tube, he should be in his room.\pWhat he needed it for I have no idea."))
                msgbox(format("Alright off you go my assistant!\pI cannot do this experiment without those items!"))
				setvar(VAR_POKESCAPE_ERNEST_QUEST_STATE, 1)
				hidenamebox
                closemessage
				release
				end
			}
			else {
				msgbox(format("Shame."))
				hidenamebox
                closemessage
				release
				end
			}

    	case 1: 
			namebox ("Oddenstein")
			msgbox(format("Have you found the parts?"), MSGBOX_YESNO)
			if (var(VAR_RESULT)){
				namebox ("Oddenstein")
				msgbox(format("Let me check…"))
				call(CooksAssistant_Check_OilCan)
				call(CooksAssistant_Check_PressureGauge)
				call(CooksAssistant_Check_RubberTube)
				msgbox(format("It looks like you have everything!\pAllow me to take those off your hands."))
				closemessage
				hidenamebox
                removeitem(ITEM_OIL_CAN)
				removeitem(ITEM_PRESSURE_GAUGE)
				removeitem(ITEM_RUBBER_TUBE)
                applymovement(ODDENSTEIN_ID, MOVEMENT_DraynorManor_Oddenstein_1)
                waitmovement(0)
				namebox ("Oddenstein")
                msgbox(format("… {PAUSE 15}Just a few more adjustments…"))
				closemessage
				hidenamebox
                applymovement(ODDENSTEIN_ID, MOVEMENT_DraynorManor_Oddenstein_2)
                waitmovement(0)
				namebox ("Oddenstein")
                msgbox(format("Done!\pNow all we need is one of those chickens.\pCan you go grab one for me."))
				closemessage
				hidenamebox
                //giveitem(ITEM_MAGICAL_CAGE,1)
                //msgbox(format("All we need now is a dead bird.\pGo dig up old polly and put her inside that cage!\pI buried her out the back by the gravestone."))
				hidenamebox
                closemessage
                setvar(VAR_POKESCAPE_ERNEST_QUEST_STATE, 2)
				release
				end
			}
			else {
				msgbox(format("Shame."))
				hidenamebox
				release
				end
			}

        case 2:    
            namebox ("Oddenstein")
			msgbox(format("Don't be shy, I know they look weird but they are just chickens.\pGo pick one up and bring it over here to me."))
            //msgbox(format("Go dig up old polly and put her inside that cage!\pI buried her out the back by the gravestone."))
			hidenamebox
            closemessage
			release
			end

		case 3: 
			namebox ("Oddenstein")
			msgbox(format("Perfect, thats an Ernest Chicken exactly what we needed!\pStand back and let the experiment begin!"))
            closemessage
            hidenamebox
			fadenewbgm(MUS_PS_KOUREND_CATACOMBS)
			fadescreenswapbuffers(FADE_TO_BLACK)
			getplayerxy(VAR_TEMP_1, VAR_TEMP_2)
			if (var(VAR_TEMP_2) == 3){
				applymovement(OBJ_EVENT_ID_PLAYER, movement_ENEREST_CHICKEN_QUEST_1)
			} 
			elif (var(VAR_TEMP_2) == 4){
				applymovement(OBJ_EVENT_ID_PLAYER, movement_ENEREST_CHICKEN_QUEST_2)
			}



			applymovement(ODDENSTEIN_ID, movement_ENEREST_CHICKEN_QUEST_3)
			waitmovement(0)
			//delay(20)
			
			applymovement(OBJ_EVENT_ID_PLAYER, movement_ENEREST_CHICKEN_QUEST_6)
			waitmovement(OBJ_EVENT_ID_PLAYER)
			setflag(FLAG_TEMP_1D)
			setflag(FLAG_TEMP_1E)
			setflag(FLAG_TEMP_1F)
			removeobject(2)
			removeobject(3)
			removeobject(4)
			removeobject(5)
			checkfollower
			if (var(VAR_RESULT)) {
				setobjectxy(OBJ_EVENT_ID_FOLLOWER, 7, 3)
				applymovement(OBJ_EVENT_ID_FOLLOWER, Movement_InteractFACEDOWN)
				waitmovement(0)
			}
			fadescreenswapbuffers(FADE_FROM_BLACK)

			special(SpawnCameraObject)
			applymovement(OBJ_EVENT_ID_CAMERA, movement_ENEREST_CHICKEN_QUEST_5)
			waitmovement(0)



			//fadescreenswapbuffers(FADE_TO_BLACK)
			
			namebox ("Oddenstein")
			msgbox(format("Okay, first we place the chicken down."))
			closemessage
			hidenamebox


			applymovement(ODDENSTEIN_ID, movement_ENEREST_CHICKEN_QUEST_7)
			waitmovement(0)

			addobject(7)
			clearflag(FLAG_TEMP_19)
			delay(64)
			//fadescreenswapbuffers(FADE_FROM_BLACK)
			applymovement(ODDENSTEIN_ID, movement_ENEREST_CHICKEN_QUEST_4)
			waitmovement(0)
			namebox ("Oddenstein")
			msgbox(format("Then we press this button and fiddle with these doohickles…"))
			closemessage
			hidenamebox
			applymovement(ODDENSTEIN_ID, movement_ENEREST_CHICKEN_QUEST_8)
			playse(SE_BANG)
			waitmovement(0)
			delay(10)
			applymovement(ODDENSTEIN_ID, movement_ENEREST_CHICKEN_QUEST_8)
			playse(SE_SELECT)
			waitmovement(0)
			delay(10)
			applymovement(ODDENSTEIN_ID, movement_ENEREST_CHICKEN_QUEST_8)
			playse(SE_TRUCK_DOOR)
			waitmovement(0)
			delay(30)
			
			applymovement(ODDENSTEIN_ID, Movement_InteractFACEDOWN)
			waitmovement(0)


			//special(SpawnCameraObject)
			//applymovement(OBJ_EVENT_ID_CAMERA, movement_ENEREST_CHICKEN_QUEST_5)
			//waitmovement(0)
			delay(64)
			fadeoutbgm(1)
			namebox ("Oddenstein")
			msgbox(format("Wait!\nSomething is wrong!"))
			closemessage
			hidenamebox

			setvar (VAR_0x8004, 1  @ vertical pan)
			setvar (VAR_0x8005, 1  @ horizontal pan)
			setvar (VAR_0x8006, 8  @ num shakes)
			setvar (VAR_0x8007, 3  @ shake delay)
			special (ShakeCamera)
			waitstate
			setvar (VAR_0x8004, 1  @ vertical pan)
			setvar (VAR_0x8005, 1  @ horizontal pan)
			setvar (VAR_0x8006, 8  @ num shakes)
			setvar (VAR_0x8007, 3  @ shake delay)
			special (ShakeCamera)
			waitstate

			//fadescreen(FADE_TO_BLACK)
			applymovement(7, movement_chickentransform)
			delay(10)
			namebox ("Oddenstein")
			msgbox(format("Oh nooo!"))
			closemessage
			hidenamebox
			waitmovement(0)

			

			playse(SE_ORB)
			special(DoOrbEffect)
			delay(60)
			fadescreenswapbuffers(FADE_TO_WHITE)
			
			special(FadeOutOrbEffect)
			delay(120)

			setvar (VAR_0x8004, 1  @ vertical pan)
			setvar (VAR_0x8005, 1  @ horizontal pan)
			setvar (VAR_0x8006, 8  @ num shakes)
			setvar (VAR_0x8007, 3  @ shake delay)
			special (ShakeCamera)
			waitstate
			setvar (VAR_0x8004, 1  @ vertical pan)
			setvar (VAR_0x8005, 1  @ horizontal pan)
			setvar (VAR_0x8006, 8  @ num shakes)
			setvar (VAR_0x8007, 3  @ shake delay)
			special (ShakeCamera)
			waitstate
			
			delay(120)

			setflag(FLAG_TEMP_19)
			clearflag(FLAG_TEMP_18)
			addobject(6)
			removeobject(7)
			delay(32)
			fadescreenswapbuffers(FADE_FROM_WHITE)
			delay(30)
			applymovement(ODDENSTEIN_ID, Movement_InteractFACELEFT)
			waitmovement(0)
			namebox ("Oddenstein")
			msgbox(format("Drat my experiment has ended in failure!\pLet's call it there.\pPlease dispose of the failed experiment."))
			closemessage
			hidenamebox
			applymovement(OBJ_EVENT_ID_CAMERA, movement_ENEREST_CHICKEN_QUEST_9)
			waitmovement(0)
			special(RemoveCameraObject)
			fadeinbgm(1)
			setvar(VAR_POKESCAPE_ERNEST_QUEST_STATE, 4)
            end

        case 4: 
            namebox ("Oddenstein")
			msgbox(format("Please dispose of that chicken."))
            closemessage
            hidenamebox
			release
            end
		case 5: 
            namebox ("Oddenstein")
			msgbox(format("Ah, thank you for disposing of that fowl creature.\pDespite my experiment ending in failure I would like you to have this."))
			questmenu(QUEST_MENU_COMPLETE_QUEST, QUEST_ERNEST_THE_CHICKEN)
			hidenamebox
			giveitem(ITEM_MINT_CAKE, 2)
			setvar(VAR_POKESCAPE_ERNEST_QUEST_STATE, 6)
            closemessage
            hidenamebox
			namebox ("Oddenstein")
			msgbox(format("Thank you for your assistance with the experiment."))
			closemessage
            hidenamebox
			release
            end
		case 6: 
			namebox ("Oddenstein")
			msgbox(format("Hello again {PLAYER}."))
			subquestmenu(QUEST_MENU_CHECK_COMPLETE, QUEST_ERNEST_THE_CHICKEN, 4)
			if (var(VAR_RESULT) == FALSE) {
				checkitem (ITEM_MAGICAL_CAGE,1)
				if (var(VAR_RESULT)){
					msgbox(format("Oh that's Polly's CAGE!\nI used to have a parrot named Polly.\pShe loved that cage…\pShe has sadly passed from this world and her grave now rests in the backyard of the manor…"))
				}
				else {
					msgbox(format("Oh by the way, have you seen a CAGE anywhere?\pI could have swore I left it in the room next to us somewhere but I haven't seen it in awhile."))
				}
			}
			elif (var(VAR_RESULT) == TRUE) {
				subquestmenu(QUEST_MENU_CHECK_COMPLETE, QUEST_ERNEST_THE_CHICKEN, 3)
				if (var(VAR_RESULT) == FALSE) {
					msgbox(format("By the way, it appears that ever since we experimented on that chicken it appears to keep coming back.\pIt would be great if you could get rid of it for me."))
					hidenamebox
					closemessage
					release
					end
				}
				subquestmenu(QUEST_MENU_CHECK_COMPLETE, QUEST_ERNEST_THE_CHICKEN, 4)
				if (var(VAR_RESULT) == TRUE) {
					msgbox(format("OMFG I CANNOT BELIEVE THIS!\pSOMEONE DUG UP MY POOR POLLY!\pWhat kind of monster would disturb my poor Polly's rest!"))
				}
			}
			hidenamebox
			closemessage
			release
			end
    }
}


script EvilChickenBattleScript{
    lock
    playmoncry(SPECIES_EVILCHICKEN, 0)
    namebox("Evil Chicken")
    msgbox(format("BWAWWWK!\pBEGONE {PLAYER}!"))
    waitmoncry
    closemessage
    setwildbattle (SPECIES_EVILCHICKEN, 18, ITEM_NONE)
    dowildbattle
    if (var(VAR_RESULT) == B_OUTCOME_CAUGHT) {
        fadescreenswapbuffers(FADE_TO_BLACK)
        removeobject (VAR_LAST_TALKED)
		if (var(VAR_POKESCAPE_ERNEST_QUEST_STATE) == 4) {
			setvar(VAR_POKESCAPE_ERNEST_QUEST_STATE, 5)
			subquestmenu (QUEST_MENU_COMPLETE_QUEST, QUEST_ERNEST_THE_CHICKEN, 3)
		}
		elif (var(VAR_POKESCAPE_ERNEST_QUEST_STATE) == 6) {
			subquestmenu (QUEST_MENU_COMPLETE_QUEST, QUEST_ERNEST_THE_CHICKEN, 3)
		}
        fadescreenswapbuffers(FADE_FROM_BLACK)
    }
	if (var(VAR_RESULT) == B_OUTCOME_WON) {
        fadescreenswapbuffers(FADE_TO_BLACK)
        removeobject (VAR_LAST_TALKED)
		if (var(VAR_POKESCAPE_ERNEST_QUEST_STATE) == 4) {
			setvar(VAR_POKESCAPE_ERNEST_QUEST_STATE, 5)
		}
        fadescreenswapbuffers(FADE_FROM_BLACK)
    }
    if (var(VAR_RESULT) == B_OUTCOME_RAN ) {
        namebox ("Oddenstein")
        msgbox(format("Don't listen to that failed experiment!\pCatch it or defeat it!\pDo something and get it out of my lab."))
        closemessage
        hidenamebox
        release
    }
    end
}


 
movement movement_chickentransform {
	levitate
	face_left
	delay_16 * 4
	
	face_right
	delay_16  * 4
	face_down
	delay_16 * 4
	stop_levitate
	
	//figure_8
}
			
movement MOVEMENT_DraynorManor_Oddenstein_1 {
    walk_in_place_down * 3
}

movement MOVEMENT_DraynorManor_Oddenstein_2 {
    walk_in_place_down * 3
    face_player
}
movement movement_ENEREST_CHICKEN_QUEST_1 {
	face_down 
	delay_16 * 2
}
movement movement_ENEREST_CHICKEN_QUEST_2 {
	face_down
	lock_facing_direction
	walk_up
	unlock_facing_direction
}
movement movement_ENEREST_CHICKEN_QUEST_3 {
	walk_left * 2
	face_down * 2
}
movement movement_ENEREST_CHICKEN_QUEST_4 {
	walk_right * 2
}
movement movement_ENEREST_CHICKEN_QUEST_5 {
	walk_slow_down * 3
}
movement movement_ENEREST_CHICKEN_QUEST_6 {
	delay_16 * 2
	walk_left * 2
	walk_down
}
movement movement_ENEREST_CHICKEN_QUEST_7 {
	walk_down * 2
}

movement movement_ENEREST_CHICKEN_QUEST_8 {
	walk_in_place_right
}
movement movement_ENEREST_CHICKEN_QUEST_9 {
	walk_slow_up * 3
}





//----
script CooksAssistant_Check_OilCan {
	checkitem (ITEM_OIL_CAN,1)
	if (!var(VAR_RESULT)){
		namebox ("Oddenstein")
		msgbox(format("You don't have the Oil Can.\pCheck with Ava.\pShe is in the room next to the living room.\pShe loves puzzles so I'd imagine she would have hidden the door to her room behind a bookcase or something."))
		hidenamebox
        closemessage
		release
		end
	}
	else {
		namebox ("Oddenstein")
		msgbox(format("You have the Oil Can!"))
        closemessage
		return
	}
}

script CooksAssistant_Check_PressureGauge {
	checkitem (ITEM_PRESSURE_GAUGE,1)
	if (!var(VAR_RESULT)){
		namebox ("Oddenstein")
		msgbox(format("You don't have the Pressure Gauge.\pI believe the last place I saw that was outside in the fountain."))
		hidenamebox
		closemessage
		release
		end
	}
	else {
		namebox ("Oddenstein")
		msgbox(format("You have the Pressure Gauge!"))
        closemessage
		return
	}
}

script CooksAssistant_Check_RubberTube {
	checkitem (ITEM_RUBBER_TUBE,1)
	if (!var(VAR_RESULT)){
		namebox ("Oddenstein")
		msgbox(format("You don't have the Rubber Tube.\pAs I recall McBoneFace borrowed it last."))
		hidenamebox
		closemessage
		release
		end
	}
	else {
		namebox ("Oddenstein")
		msgbox(format("You have the Rubber Tube!"))
        closemessage
		return
	}
}
//------



script NPC_DraynorManor_Fisherman{
	lock
	faceplayer
    namebox ("Man")
    checkitem (ITEM_PRESSURE_GAUGE,1)
	if (var(VAR_RESULT)){
		msgbox(format("Why did you do that?\pI just wanted to fish for piranhas."))
		hidenamebox
        closemessage
		release
		end
    }
	checkitem (ITEM_FISH_FOOD_POISONED,1)
	if (var(VAR_RESULT)){
		msgbox(format("Hey, that fish food looks ominous.\pWhat are you planning to do?\p…{PAUSE 15}Murder the fish?"))
		hidenamebox
		closemessage
		release
		end
    }
	checkitem (ITEM_FISH_FOOD,1)
	if (var(VAR_RESULT)){
		msgbox(format("Where did you get that fish food from?\pHopefully you didn't get it from the kitchen, there's a bunch of poisons in there."))
		hidenamebox
		closemessage
		release
		end
    }
    else
    {
        switch (var(VAR_POKESCAPE_ERNEST_QUEST_STATE)){ 
		case 0: 
			msgbox(format("Rumours say there is a rare fish called piranha in this fountain."))
			hidenamebox
            closemessage
			release
			end
        case 1: 
			msgbox(format("The fish are looking a bit hungry.\pI think I saw some fish food somewhere upstairs in that Manor."))
			hidenamebox
            closemessage
			release
			end
        case 2:
		case 3:
		case 4: 
			msgbox(format("The piranhas went extinct…"))
			hidenamebox
            closemessage
			release
			end
        }
    
    }
}




	
			